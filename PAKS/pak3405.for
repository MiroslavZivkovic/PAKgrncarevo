C=======================================================================
C
C   PLASTICNOST 3/D ELEMENT
C
C    SUBROUTINE MDMAT3
C               D3M5
C               TAUI35
C               TEQBI3
C               PRILA3
C               DEVEQ3
C
C=======================================================================
      SUBROUTINE D3M5(TAU,DEF,IRAC,LPOCG,LPOC1)
      USE PLAST3D
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PROGRAM ZA ODREDIVANJE LOKACIJA VELICINA KOJE SE CUVAJU
C     NA NIVOU INTEGRACIONE TACKE

      include 'paka.inc'
      
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /REPERM/ MREPER(4)
      COMMON /DUPLAP/ IDVA
      DIMENSION TAU(6),DEF(6)
C
      LFUN=MREPER(1)
      LNTA=MREPER(2)
      MATE=MREPER(4)
C
      LTAU=LPOCG
      LDEFT=LTAU + 6
      LDEFPT=LDEFT + 6
      LTEQT=LDEFPT + 6
      LTEQYT=LTEQT + 1
      LDQPT=LTEQYT + 1
      LIPL = LDQPT + 1
C
      LTAU1=LPOC1
      LDEFT1=LTAU1 + 6
      LDEFP1=LDEFT1 + 6
      LTEQT1=LDEFP1 + 6
      LTEQY1=LTEQT1 + 1
      LDQPT1=LTEQY1 + 1
      LIPL1 =LDQPT1 + 1
C
      CALL TAUI35(PLAST(LIPL),PLAST(LTAU),PLAST(LDEFT),PLAST(LDEFPT),
     1            PLAST(LTEQT),PLAST(LTEQYT),PLAST(LDQPT),
     1            PLAS1(LIPL1),PLAS1(LTAU1),PLAS1(LDEFT1),PLAS1(LDEFP1),
     1            PLAS1(LTEQT1),PLAS1(LTEQY1),PLAS1(LDQPT1),
     1            A(LFUN),A(LNTA),MATE,TAU,DEF,IRAC)
C
      RETURN
      END
C=======================================================================
      SUBROUTINE TAUI35( PL,TAUT,DEFT,DEFPT,TEQT,TEQYT,DEFQPT,
     1            PL1,TAU1,DEF1,DEFP,TEQ,TEQY,DEFQP,FUN,NTA,MATE,
     1           TAU,DEF,IRAC)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA INTEGRACIJU KONSTITUTIVNIH RELACIJA ZA ELASTOPLASTIC
C     ELASTOPLASTICAN MATERIJAL SA IZOTROPNIM OJACANJEM
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TAUD3/ TAUD(6),DEFDPR(6),DEFDS(6),DDEFP(6),
     1              DETAU(6),DDEF(6)
      COMMON/MAT2D/E,ANI,ET,TEQY0
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON/PLASTI/LPLAST,LPLAS1,LSIGMA
C
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON/ITERBR/ITER
C
      COMMON /CONMAT/ AE,EP,DVT
C
      DIMENSION TAUT(*),DEFT(*),DEFPT(*),TAU(*),DEF(*),TAU1(*),DEF1(*),
     1          DEFP(*)
      DIMENSION FUN(2,MATE,*),NTA(*)
C
C     OSNOVNE KONSTANTE
C
      ISEG=0
      NTT=NTA(MAT)
      IF(NTT.GT.1) ISEG=NTT-1
      IPL=PL
      IPL1=PL1
C
      DVT=2.0D0/3.0D0
C
C     INICIJALIZACIJA OSNOVNIH VELICINA
C
      ET=FUN(2,MAT,2)
      E=FUN(1,MAT,1)
      ANI=FUN(2,MAT,1)
      TEQY0=FUN(2,MAT,2)
      IF(ISEG.EQ.0) TEQY0=FUN(1,MAT,2)
      TEQ=TEQT
       IF(KOR.EQ.1) THEN
        TEQY=TEQY0
        TEQYT=TEQY0 
       ENDIF
      IF(IPL.EQ.1) TEQY=TEQT
      CALL MEL35(FUN,NTA,MATE)
      AE=(1.0D0+ANI)/E
      IF(IRAC.EQ.2) RETURN
      DO 10 I=1,6
      TAU(I)=0.0D0
      DDEF(I)=DEF(I)-DEFT(I)
   10 CONTINUE
C
C     PROVERA ELASTICNOG RESENJA
C
      EP=E*ET/(E-ET)
C
C     ODREDIVANJE NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
C
      DO 20 I=1,6
      DO 20 J=1,6
   20 TAU(I)=TAU(I) + ELAST(I,J)*(DEF(J) - DEFPT(J))
C     ODREDIVANJE EFEKTIVNOG NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
      II=1
      CALL DEVEQ3(TAU,II,TAUEQE,TAUD)
      IF(TAUEQE.GT.TEQYT) GO TO 340
      TEQ=TAUEQE
      DEFQP=0.0D0
      GO TO 100
  340 PL1=1.0D0
C
C     RESENJE JE ELASTOPLASTICNO , TREBA ODREDITI PRIRASTAJ PLASTICNE
C     DEFORMACIJE IPRIRASTAJ NAPONA PRIMENOM FUNKCIJE EFEKTIVNOG NAPONA
C
C     ODREDIVANJE DEVIJATORA UKUPNE DEFORMACIJE
C
      EMT = (DEF(1)+DEF(2)+DEF(3))/3.0D0
      DO 180 I=1,6
      IF(I.LE.3) THEN
      DEFDPR(I)=DEF(I)-EMT
      ELSE
      DEFDPR(I)=DEF(I)
      ENDIF 
  180 CONTINUE
C
      DO 30 I=1,6
   30 DEFDS(I)=DEFDPR(I)-DEFPT(I)
C
C     ODREDIVANJE POJEDINIH CLANOVA FUNKCIJE EFEKTIVNOG NAPONA
C
      DKV=1.5D0*(DEFDS(1)*DEFDS(1)+DEFDS(2)*DEFDS(2)+DEFDS(3)*DEFDS(3)
     1+2.0D0*(DEFDS(4)*DEFDS(4)+DEFDS(5)*DEFDS(5)+DEFDS(6)*DEFDS(6)))
      DD=DSQRT(DKV)
      TAUEQE=DD/AE
C
      IF(ISEG.EQ.0) GO TO 330
       ISE=1
  400 ISE=ISE+1
      IF(ISE.GT.ISEG) GO TO 422
      TEQ=  FUN(2,MAT,ISE)
      DEFPP=FUN(1,MAT,ISE)
      IF(DEFPP.LT.DEFQPT) GO TO 400
      DDP=DEFPP-DEFQPT
      F=  AE*TEQ+1.5D0*DDP-DD
       IF(F) 400,410,420
  410 TEQ= (DD-1.5D0*DDP)/AE
      DLAM=1.5D0*DDP/TEQ
      GO TO 331
  420 EPI=(FUN(2,MAT,ISE+1)-FUN(2,MAT,ISE))/
     1    (FUN(1,MAT,ISE+1)-FUN(1,MAT,ISE))
      DDP=FUN(1,MAT,ISE)-DEFQPT
      TEQ= (DD-1.5D0*(DDP-FUN(2,MAT,ISE)/EPI))/(AE+1.5D0/EPI)
      DLAM=1.5D0*(DDP+(TEQ-FUN(2,MAT,ISE))/EPI)/TEQ    
      GO TO 331
  422 CONTINUE
      TEQ=FUN(2,MAT,ISEG)
      DLAM=1.5D0*(FUN(1,MAT,ISEG)-DEFQPT)/TEQ
      GO TO 331
C
C     PRIMENA POSTUPKA BISEKCIJE ZA
C     ODREDIVANJE NULE FUNKCUJE EFEKTIVNOG NAPONA
C
      IBISE=0
      IM=0
      IP=0
      TM=0.0D0
      TFM=0.0D0
      TP=0.0D0
      TFP=0.0D0
      IBISM=100
      TEQ1=TEQ
      TOLBIS=1.0D-6
      DTEQ=0.01D0*(TAUEQE-TEQT)
C
   50 IBISE=IBISE+1
      IF(IBISE.EQ.1)TEQ=TEQY
      IF(IBISE.EQ.2)TEQ=TAUEQE
C
C     ODREDIVANJE DELTA LAMBDA
C
      CALL PRILA3(TEQ,ISEG,DLAM,TEQYT,EP,FUN,NTA,DEFQPT,MAT,MATE)
C
      ADL=AE+DLAM
      TFQ=ADL*ADL*TEQ*TEQ-DKV
C
      TEQ1=TEQ
      CALL TEQBI3(TEQ,DTEQ,TFQ,TM,TFM,TP,TFP,IM,IP,IBISE)
      IF(IBISE.EQ.1) GO TO 50
      DTEQB=DABS(TEQ1-TEQ)
      TOLSR=DABS(TEQ+TEQ1)/2.0D0
      IF(TOLSR.LT.1.D-9) GO TO 79
      TTOL=DTEQB/TOLSR
      IF(TTOL.LT.TOLBIS)GO TO 60
   79 IF((DABS(TFP).LE.TOLBIS).AND.(DABS(TFM).LE.TOLBIS)) GO TO 60
      IF(IBISE.LE.IBISM) GO TO 50
      WRITE(6,1000)
 1000 FORMAT(' ','DOSTIGNUT MAKSIMALAN BROJ BISEKCIJA U TAUINT5')
      STOP
C
C     ODREDIVANJE KOMPONENATA DEVIJATORA NAPONA
C
   60 CONTINUE
  330 CONTINUE
      IF(ISEG.GT.0) GO TO 331
      TEQ=(2.0D0*EP*DD+3.0D0*TEQYT)/(2.0D0*EP*AE+3.0D0)
C
      CALL PRILA3(TEQ,ISEG,DLAM,TEQYT,EP,FUN,NTA,DEFQPT,MAT,MATE)
  331 CONTINUE
      ADL=AE+DLAM
      ADLR=1.0D0/ADL
      DO 65 I=1,6
   65 TAUD(I)=ADLR*DEFDS(I)
C
C     ODREDIVANJE PRIRASTAJA PLASTICNE DEFORMACIJE
C
      CONTINUE
      DO 70 I=1,6
      DDEFP(I)=DLAM*TAUD(I)
   70 CONTINUE
C
C     ODREDIVANJE PLASTICNE DEFORMACIJE
C
      DO 170 I=1,6
  170 DEFP(I)=DEFPT(I)+DDEFP(I)
C
C     EKVIVALENTNA PLASTICNA DEFORMACIJA
C
      DDEFQP=DDEFP(1)*DDEFP(1)+DDEFP(2)*DDEFP(2)+DDEFP(3)*DDEFP(3)+
     1 2.0D0*(DDEFP(4)*DDEFP(4)+DDEFP(5)*DDEFP(5)+DDEFP(6)*DDEFP(6))
      DDEFQP= DSQRT(DVT*DDEFQP)
      DEFQP=DEFQPT+DDEFQP
C
C     ODREDIVANJE NAPONA
C
      TAUM=E/(1.0D0-2.0D0*ANI)*EMT
      DO 260 I=1,6
       IF(I.LE.3) THEN
        TAU(I)=TAUD(I)+TAUM
         ELSE
        TAU(I)=TAUD(I)
       ENDIF
  260 CONTINUE
      TEQY=TEQ      
C
      IF((ITER.GT.0).AND.((METOD.EQ.3).OR.(METOD.EQ.4))) THEN 
C
       DO 430 I=1,6
  430  DEFDS(I)=DEFDPR(I)-DEFP(I)
C
C     ODREDIVANJE POJEDINIH CLANOVA FUNKCIJE EFEKTIVNOG NAPONA
C
       DKV=1.5D0*(DEFDS(1)*DEFDS(1)+DEFDS(2)*DEFDS(2)+DEFDS(3)*DEFDS(3)
     1 +2.0D0*(DEFDS(4)*DEFDS(4)+DEFDS(5)*DEFDS(5)+DEFDS(6)*DEFDS(6)))
       DD=DSQRT(DKV)
C
      CALL ELPLM3(DEFDS,DD,DLAM,TEQ,DDEFQP)
C
      ENDIF
C
C     KORIGOVANJE VELICINA IZ PRETHODNOG KORAKA KAD JE POSTIGNUTA
C     KONVERGENCIJA
C
  100 CONTINUE
      DO 290 I=1,6
      DEF1(I)=DEF(I)
  290 TAU1(I)=TAU(I)
      RETURN
      END
      SUBROUTINE TEQBI3(X,DX,F,XM2,FM2,XP2,FP2,IM,IP,IBISE)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA RESAVANJE NELINEARNE JEDNACINE F(X)=0.
C
C
      FUBRZ=1.0D0
      IF(IBISE.GT.2) GO TO 10
      IF(IBISE.GT.1) GO TO 5
      X1=X
      F1=F
    5 IF(IBISE.GT.2) GO TO 10
      IR=1
      IF(X.GT.X1.AND.F.GT.F1) IR=2
C
   10 IF(F)20,100,30
   20 IM=IM+1
      IF(IM.GT.1) GO TO 22
      XM2=X
      FM2=F
      GO TO 50
   22 IF(F.LT.FM2) GO TO 50
      XM1=XM2
      FM1=FM2
      XM2=X
      FM2=F
C  25 X=XM1+(XM2-XM1)/(FM2-FM1)*FM1
C     GO TO 100
      GO TO 50
   30 IP=IP+1
      IF(IP.GT.1) GO TO 32
      XP2=X
      FP2=F
      GO TO 50
   32 IF(F.GT.FP2) GO TO 50
      XP1=XP2
      FP1=FP2
      XP2=X
      FP2=F
C  35 X=XP1+(XP2-XP1)/(FP2-FP1)*FP1
C     GO TO 100
   50 CONTINUE
      IF(IBISE.EQ.1) GO TO 60
      IF(IM.GT.0.AND.IP.GT.0) GO TO 80
      IF(IP.GT.0) GO TO 60
      DX=DABS(0.3D0*X)*FUBRZ
      IF(IR.EQ.1) GO TO 55
      X=X+DX
      GO TO 100
   55 X=X-DX
      GO TO 100
   60 DX=DABS(0.3D0*X)*FUBRZ
      IF(IR.EQ.1) GO TO 65
      X=X-DX
      GO TO 100
   65 X=X+DX
      GO TO 100
C
C     KORENI SU RAZDVOJENI - METOD BISEKCIJE
C
   80 IF(IM.GT.5.AND.IP.GT.5) GO TO 90
C  80 CONTINUE
      X=(XP2+XM2)/2.0D0
      DX=XP2-XM2
      GO TO 100
C
C     METOD SECICE
C
   90 CONTINUE
      X=XP2-(XM2-XP2)/(FM2-FP2)*FP2
      DX=XP2-XM2
  100 CONTINUE
      RETURN
      END
C=======================================================================
      SUBROUTINE PRILA3(TEQ,ISEG,DLAM,TEQYT,EP,FUN,NTA,DEFQPT,
     1                  MAT,MATE)
C  
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA ODREDIVANJE PRIRASTAJA EFEKTIVNE PLASTICN
C     DEFORMACIJE I ODREDIVANJE PRIRASTAJA LAMBDA ZA DATU VREDNOST
C     EFEKTIVNOG NAPONA
C
      COMMON/MAT2D/E,ANI,ET,TEQY0
C
      DIMENSION FUN(2,MATE,*),NTA(*)
C
C     BILINEARNA ZAVISNOST
C
      IF(ISEG.GT.0) GO TO 50
C     BILINEARNA KRIVA
      DE=E-ET
      IF(DE.GT.0.0001)GO TO 30
      WRITE(3,100)
  100 FORMAT(' ','PERFEKTNA PLASTICNOST NIJE UGRADENA')
      STOP
   30 CONTINUE
      IF(DABS(TEQ).GT.TEQYT) GO TO 140
      DLAM=0.0D0
      GO TO 150
  140 DLAM=1.5D0*(1.0D0-TEQYT/TEQ)/EP
  150 CONTINUE
      RETURN
C
C     MULTILINEARNA KRIVA
C
   50 CONTINUE
      I=1
  60  I=I+1
      IF(I.GT.NTA(MAT)) THEN 
      DPTD=FUN(1,MAT,NTA(MAT))
      GO TO 250
      ENDIF
      TI=FUN(2,MAT,I)
      IF(TEQ.LE.TI)THEN
      DLAM=0.0D0
      GO TO 90
      ENDIF
      TI1=FUN(2,MAT,I+1)
      IF(TEQ.GT.TI.AND.TEQ.LE.TI1) GO TO 61
      GO TO 60
   61 DPI = FUN(1,MAT,I)
      DPI1= FUN(1,MAT,I+1)
      DPTD= DPI + (TEQ-TI)*(DPI1-DPI)/(TI1-TI)
C
 250  DLAM=1.5D0*(DPTD-DEFQPT)/TEQ
C
  90  RETURN
      END
      SUBROUTINE DEVEQ3(TAU,II,TEQ,TAUD)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA ODREDIVANJE DEVIJATORA NAPONA EVIVALENTNOG NAPONA
C
      DIMENSION TAU(6),TAUD(6)
C
      GO TO(10,20),II
C
C     RAVNO STANJE DEFORMACIJE ILI OSNOSIMETRICAN PROBLEM
C
   10 TAUM=(TAU(1)+TAU(2)+TAU(3))/3.0D0
C
      DO 15 I=1,6
      IF(I.LE.3) THEN
      TAUD(I)=TAU(I)-TAUM
      ELSE
      TAUD(I)=TAU(I)
      END IF
   15 CONTINUE
C
      TEQ=TAUD(1)*TAUD(1)+TAUD(2)*TAUD(2)+TAUD(3)*TAUD(3)+
     1    2.0D0*(TAUD(4)*TAUD(4)+TAUD(5)*TAUD(5)+TAUD(6)*TAUD(6))
      TEQ=DSQRT(1.5D0*TEQ)
      RETURN
C     RAVNO STANJE NAPONA
   20 WRITE(6,100)
  100 FORMAT(' ','RAVNO STANJE NAPONA NIJE UGRADENO- DEVEQ')
      STOP
      END
C======================================================================
      SUBROUTINE ELPLM3(DEFDS,DD,DLAM,TEQ,DDEFQP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE MATRICE ELAST
CE     ELAST MATRIX
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON/MAT2D/E,ANI,ET,TEQY0
      COMMON /CONMAT/ AE,EP,DVT
      COMMON /CDEBUG/ IDEBUG
      DIMENSION DEFDS(6),CP(6,6) 
C
      IF(IDEBUG.GT.0) PRINT *, ' ELPLM3 '
C
C      E, V, ALFA
C
      DO 10 I=1,6
      DO 10 J=1,6
      CP(I,J)=0.0D0
   10 ELAST(I,J)=0.0D0
C
      A=1.0D0/(AE+DLAM)
      B=1.5D0*A*A*(TEQ-DDEFQP*EP)/(AE*EP+1.5D0)/(TEQ*TEQ*DD)
C       S6=DSQRT(6.0D0)
C       B=(3.0D0-2.0D0*EP*DLAM)/(S6*(DVT*AE*EP-1.0D0)*TEQ*DD)*A*A
C
      DO 20 I=1,6
      DO 20 J=1,6
      DSKJ=DEFDS(J)
      IF(J.GT.3)DSKJ=2.0D0*DEFDS(J)
        IF(I.EQ.J) THEN
        CP(I,J)=A-B*DEFDS(I)*DSKJ
         ELSE
         CP(I,J)=-B*DEFDS(I)*DSKJ
        ENDIF
   20 CONTINUE
C
      TR=DVT/2.0D0
      CM=E/(1.0D0-2.0D0*ANI)
C
      ELAST(1,1)=TR*(2.0D0*CP(1,1)-CP(1,2)-CP(1,3)+CM)
      ELAST(2,2)=TR*(2.0D0*CP(2,2)-CP(1,2)-CP(2,3)+CM)
      ELAST(3,3)=TR*(2.0D0*CP(3,3)-CP(1,3)-CP(2,3)+CM)
      ELAST(1,2)=TR*(2.0D0*CP(1,2)-CP(1,1)-CP(1,3)+CM)
      ELAST(1,3)=TR*(2.0D0*CP(1,3)-CP(1,1)-CP(1,2)+CM)
      ELAST(2,1)=TR*(2.0D0*CP(2,1)-CP(2,2)-CP(2,3)+CM)
      ELAST(2,3)=TR*(2.0D0*CP(2,3)-CP(2,1)-CP(2,2)+CM)
      ELAST(3,1)=TR*(2.0D0*CP(3,1)-CP(3,2)-CP(3,3)+CM)
      ELAST(3,2)=TR*(2.0D0*CP(3,2)-CP(3,1)-CP(3,3)+CM)
C
      DO 50 I=1,3
      DO 50 J=4,6
      ELAST(I,J)=CP(I,J)  
   50 CONTINUE
      DO 51 I=4,6
      DO 51 J=1,3
      ELAST(I,J)=CP(I,J)  
   51 CONTINUE
C
      DO 60 I=1,3
      ELAST(4,3+I)=0.5D0*CP(4,3+I)
      ELAST(5,3+I)=0.5D0*CP(5,3+I)
      ELAST(6,3+I)=0.5D0*CP(6,3+I)
   60 CONTINUE
C      
      RETURN
      END
