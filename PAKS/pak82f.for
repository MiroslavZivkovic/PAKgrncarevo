C=======================================================================
C
C=======================================================================
      SUBROUTINE PLOCAF(TAU,SKE,FTDT,T,WTU,GKS,LM,MSL,NOP,DRGTM0,DEB,
     1                  TGT,LPLAS,LPLA1,ISNA,MTR,TTR,IND6,R,S,COR,GMT,
     1                  CORT,GNT,GERS,ALFE,HAEM,HINV,GEEK,
     1                  N45,LA,CEGE,ESILA,IPG,DEF)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.      TO FORM ELEMENT MATRIX FOR ISOPARAMETRIC SHELL ELEMENT 
CS.   P R O G R A M
CS.      ZA FORMIRANJE MATRICA ELEMENATA IZOPARAMETARSKE LJUSKE
C .
C .      IPODT.EQ.0; (4-9 CVOROVA)
C .      IPODT.EQ.1; (3-6 CVOROVA)
C .
C ......................................................................
C
      include 'paka.inc'
      
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /IZLE4B/ H(9,3),GM(3,9),BLT(6,54),BE(9,54),ETP(6,6),UEL(54)
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /PLASTV/ LPLAVT,LPLAV1,LSIGMV
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ANALIZ/ LINEAR,ITERGL,INDDIN
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /SIMO2D/ LALFE,LHAEM,LHINV,LGEEK,IALFA
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /VELIKE/ LCOR0,LGM0,JG,NGR,NGS,NGT,NGS4
      COMMON /SOPSVR/ ISOPS,ISTYP,NSOPV,ISTSV,IPROV,IPROL
      COMMON /DUPLAP/ IDVA
      COMMON /ITERBR/ ITER
      COMMON /INDNAP/ NAPON
      COMMON /FILTER/ TOLNAP,TOLPOM
      COMMON /IKOVAR/ INDKOV
      COMMON /ZADATA/ LNZADJ,LNZADF,LZADFM,NZADP
      COMMON /CUVAJN/ TAUCEN(6,5),INDSEL,NGAUSU
      COMMON /MATIZO/ E,V
      COMMON /MATANI/ EX,EY,EZ,VXY,VYZ,VZX,GXY,GYZ,GZX
C
      DIMENSION NOP(NE,*),SKE(*),LM(*),FTDT(*),TAU(N45,NGS12,NE,*),
     1          TA(6),STRAIN(6),STRESS(6),DEF(N45,NGS12,NE,*),
     1          ALFE(LA,*),HAEM(LA,*),HINV(LA,LA,*),GEEK(LA,ND,*)
      DIMENSION GKS(3,2,*),ESILA(ND,*)
      DIMENSION DRGTM0(9,*),MTR(*),TTR(3,3,*),DEB(*),BNL(9,54)
      DIMENSION TSS(6,6),
     1          TRLN(6,6),COR(9,*),CON(9,3),GMT(3,*),CORT(9,*),GNT(3,*)
      DIMENSION GERS(6,*),CEGE(LA,*),
     +          SKOV(6)
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' MATEIL'
C
CS    JAKOBIJAN - XJJ, INVERTOVAN JAKOBIJAN - XJ
CE    JACOBIAN - XJJ, INVERSE JACOBIAN - XJ
C
      CALL JEDNA1(XJJ,XJ,9)  
      CALL MINV3(XJ,DET)
C
CS    MATRICA TRANSFORMACIJE - TRLN (KOVARIJANTNI - GLOBALNI DEKARTOV) 
CE    TRANSFORMATION MATRIX - TRLN (COVARIANT - GLOBAL CARTESIAN)
C
      IF(INDKOV.LT.0) CALL TRANSE(TRLN,XJ)
C      IF(NLM.EQ.1.AND.JG.EQ.1) CALL WRR(TRLN,36,'TRLN') 
C
CS    FORMIRANJE MATRICE BL LINEARNO  (BLT)
CE    FORM LINEAR BL MATRIX (BLT) 
C
      IF(INDSEL.EQ.1) CALL CLEAR(BLT,6*54)
C
      CALL BETBE5(H,BLT,BNL,NOP,GKS,T,MTR)
C      IF(NLM.EQ.1.AND.JG.EQ.1) CALL WRR(BLT,6*ND,'BLT ') 
C
CS    TRANSFORMACIJA MATRICE - BLT (KOVARIJANTNI - GLOBALNI DEKARTOV) 
CE    TRANSFORM MATRIX - BLT (COVARIANT - GLOBAL CARTESIAN)
C
      IF(INDKOV.LT.0) CALL TRANS4(BLT,TRLN,ND)
C
CS    FORMIRANJE MATRICE BL1 GEOMETRIJSKA NELINEARNOST - T.L.
CE    FORM MATRIX BL1 GEOMETRICAL NONLINEARITY -  T.L.
C
      IF(IATYP.EQ.2.AND.INDKOV.GE.0)
     1CALL BETL18(BLT,NOP,LM,UEL,STRAIN,H,DRGTM0,GKS,T,DEB(NLM),0)
C
CS    TRANSFORMACIJA MATRICE - BLT ZA LOKALNI SISTEM CVORA
CE    TRANSFORM MATRIX - BLT FOR LOCAL NODAL SISTEM
C
      IF(IND6.GT.0) THEN
         ICD=6
         CALL TRAB(BLT,ICD,MTR,TTR,NCVE)
      ENDIF
C
CS    MATRICA TRANSFORM. DEFORMACIJA - TSG (GLOBALNI - LOKALNI DEKARTOV)
CE    STRAIN TRANSFORMATION MATRIX - TSG (GLOBAL - LOCAL CARTESIAN) 
C
C          CALL WRR(XJJ,9,'XJJ ')
      CALL TRANAL(XJJ,TSG,0)
C  MATRICA KOSINUSA BETA IZ MAGISTARSKOG ZA TRASFORMACIJU TENZ. KOMP.
C      CALL JEDNA1(TBET,XJJ,9)
C      IF(IST.EQ.1) CALL WRR(TBET,9,'TBET')
C
      IF(ISKNP.NE.1) THEN
C
C
CS       RACUNANJE DEFORMACIJA U GLOBALNOM DEKARTOVOM
CE       CALCULATE STRAINS IN GLOBAL CARTESIAN 
C
C
         CALL CLEAR(STRAIN,6) 
C
CS       LINEARNOST I M.N.O.
CE       LINEAR PART AND M.N.O.
C
         IF(IATYP.EQ.0.OR.IATYP.EQ.1)
     1   CALL MNOZI1(STRAIN,BLT,UEL,6,ND)
C
CS       GEOMETRIJSKA NELINEARNOST - UKUPNE DEFORMACIJE ZA T.L. I U.L.
CE       GEOMETRICAL NONLINEARITY - TOTAL STRAIN FOR T.L. AND U.L.
C
          IF(INDKOV.GE.0) THEN
            IF(IATYP.EQ.2)
     1      CALL BETL18(BLT,NOP,LM,UEL,STRAIN,H,DRGTM0,GKS,T,DEB(NLM),1)
            IF(IATYP.EQ.3)
     1      CALL STRUL8(H,NOP,LM,DRGTM0,UEL,STRAIN,T,DEB(NLM))
          ELSE
            IF(IATYP.EQ.2.OR.IATYP.EQ.3)
     1      CALL UKDEFI(COR,CON,GM,GMT,UEL,H,R,S,T,STRAIN,TRLN,TSS,
     1                  IATYP,NCVE)
          ENDIF
C
CS       INKOMPATIBILNE DEFORMACIJE
CE       INCOMPATIBILE STRAIN
C
         IF(IALFA.GE.0) CALL MNOZI1(STRAIN,GERS,ALFE(1,NLM),6,LA)
C
CS       TRANSFORMACIJA DEFORMACIJA (GLOBALNI - LOKALNI DEKARTOV)
CE       TRANSFORM STRAIN (GLOBAL - LOCAL CARTESIAN) 
C
         CALL CLEAR(TA,6) 
         CALL MNOZI1(TA,TSG,STRAIN,6,6)
         TA(3)=0.D0
         CALL JEDNA1(STRAIN,TA,6)
C
CS       TERMICKE DEFORMACIJE   ETH=ALFA*(T-T0)
CE       THERMAL STRAINS
C
         IF(NMODM.EQ.3.OR.NMODM.EQ.4) THEN
            DTGT0=TGT-TEMP0
            STRAIN(1)=STRAIN(1)-ALFA(1)*DTGT0
            STRAIN(2)=STRAIN(2)-ALFA(2)*DTGT0
            IF(NAPON.EQ.1) STRAIN(3)=ALFA(3)*DTGT0
            STRAIN(4)=STRAIN(4)-ALFA(4)*DTGT0
         ENDIF
C          CALL WRR(STRAIN,6,'STRA')
C
CS       RACUNANJE NAPONA
CE       CALCULATE STRESS 
C
         CALL CLEAR(STRESS,6)
C
         IF(NMODM.LT.5) THEN
C
CS          MATERIJALNA LINEARNOST
CE          MATERIAL LINEAR
C
CS          KOSIJEVI NAPONI U GLOBAL. ILI LOKAL. DEKART. SISTEMU
CE          CAUCHY STRESS IN GLOBAL OR LOCAL CARTESIAN SYSTEM 
C
            CALL MNOZI1(STRESS,ETP,STRAIN,6,6)
C           CALL WRR(STRESS,6,'STRE')
C
CS          TRANSFORMACIJA NAPONA NA GLOBALNI SISTEM
CE          TRANSFORM STRESS TO GLOBAL COORDS.
C
            CALL CLEAR(TA,6) 
            CALL MNOZI2(TA,TSG,STRESS,6,6)
            IF(ISNA.EQ.2) CALL JEDNA1(STRESS,TA,6)
C
CS          CISCENJE NUMERICKIH GRESAKA ZA NAPONE
CE          CLEANING NUMERICAL ERRORS FOR STRESS
C
C           IF(IATYP.EQ.0) CALL CISTIN(STRESS,6)
C
            IF(INDSEL.EQ.1) THEN
               IF(NGAUSU.LT.NGS12) THEN
                  CALL JEDNA1(TAUCEN(1,NGT),STRESS,6)
               ELSE
                  CALL ZBIR2B(TAU(1,JG,NLM,MSL),TAUCEN(1,NGT),STRESS,6)
               ENDIF
            ELSE
               CALL JEDNA1(TAU(1,JG,NLM,MSL),STRESS,6)
            ENDIF
C            IF(NGAUSU.LT.NGS12) CALL WRR(TAUCEN(1,NGT),6,'TAUC')
C            IF(NGAUSU.LT.NGS12) CALL WRR(TAUCEN(1,1),6,'TAUC')
C
            IF(NAPON.EQ.1) THEN
               IF(NMODM.EQ.1.OR.NMODM.EQ.3) THEN
                  STRAIN(3)=STRAIN(3)-V*(STRAIN(1)+STRAIN(2))/(1.-V)
               ENDIF
               IF(NMODM.EQ.2.OR.NMODM.EQ.4) THEN
                  DUM=(1.-VXY*VXY*EY/EX)
                  DUM1=(VZX*EX/EZ+VXY*VYZ)/DUM
                  DUM2=(VYZ+VXY*VZX*EY/EZ)/DUM
                  STRAIN(3)=STRAIN(3)-DUM1*STRAIN(1)-DUM2*STRAIN(2)
               ENDIF
            ENDIF
C
         ELSE
C
CS          NAPONI ZA PLASTICAN MODEL
CE          STRESS FOR MATERIAL NONLINEARITY 
C
            IRAC=1
            CALL MODMA8(STRAIN,STRESS,NMODM,IRAC,LPLAS,LPLA1,TGT)
C
CS          TRANSFORMACIJA NAPONA NA GLOBALNI SISTEM
CE          TRANSFORM STRESS TO GLOBAL COORDS.
C
            CALL CLEAR(TA,6) 
            CALL MNOZI2(TA,TSG,STRESS,6,6)
         ENDIF
C
C        DEFORMACIJE ZBOG KOREKCIJE DEBLJINE
C          CALL WRR(ETP,36,'ELAS')
         IF(NAPON.EQ.1.AND.NMODM.LT.5) THEN
            CALL JEDNA1(DEF(1,JG,NLM,MSL),STRAIN,6)
C            CALL WRR(STRAIN,6,'STRA')
         ENDIF
C
        IF(NGENL.GT.0.OR.(NZADP.GT.0.AND.ISKNP.EQ.2.AND.NGENL.EQ.0))THEN
C
CS          RACUNANJE UNUTRASNJIH SILA
CE          CALCULATE INTERNAL FORCES
C           r = BT * S 
C
            CALL INTEGF(FTDT,BLT,TA,LM,WTU,ND,6)
C            CALL WRR(FTDT,JEDN,'F82I')
C
         ENDIF
C
CS       INTEGRACIJA VEKTORA - h
CE       INTEGRATE VEKTOR - h
C        h = GT * S
C
         IF(NGENL.GT.0.AND.IALFA.GE.0) 
     +   CALL INTEV2(HAEM(1,NLM),GERS,TA,WTU,LA,6)
C
         IF(IPG.GT.0) THEN
            CALL JEDNAK(STRAIN,TA,WTU,6)
            CALL MNOZI2(ESILA(1,IPG),BLT,STRAIN,ND,6)
         ENDIF
C
      ENDIF
C
C
CS    INTEGRACIJA MATRICE KRUTOSTI ELEMENTA - SKE
CE    INTEGRATE ELEMENT STIFFNESS MATRIX - SKE
C
C
      IF(ISKNP.EQ.2) RETURN
C
CS    TRANSFORM. MATRICE ELAST. - ETP (LOKALNI - GLOBALNI DEKART.)
CE    TRANSFORM ELASTICITY MATRIX - ETP (LOCAL - GLOBAL CARTESIAN)
C
      CALL TRAETP(ETP,ELAST,TSG)
C
CS    INTEGRACIJA MATRICE KRUTOSTI ELEMENTA - SKE
CE    INTEGRATE ELEMENT STIFFNESS MATRIX - SKE
C     SKE = BT * C * B
C
      CALL INTEGK(SKE,BLT,ELAST,LM,WTU,ND,6)
C
CS    GEOMETRIJSKI NELINEARAN DEO MATRICE KRUTOSTI
CE    GEOMETRIC NONLINEAR PART OF STIFFNESS MATRIX   
C
      IF(IATYP.GT.1) THEN
      IF(INDKOV.EQ.0.OR.INDKOV.EQ.1)
     1CALL KNL8(SKE,H,NOP,LM,TA,T,WTU,BNL,GKS,ND,MTR,TTR,IND6)
      IF(INDKOV.EQ.-1) THEN
         CALL CLEAR(BE,81)
         CALL CLEAR(SKOV,6) 
C
CS       TRANSFORMACIJA NAPONA SA GLOBALNOG NA KONTRAVARIJANTNI SISTEM
CE       TRANSFORM STRESS FROM GLOBAL TO CONTRAVARIANT COORD. SYSTEM
C
         CALL MNOZI2(SKOV,TRLN,TA,6,6)
C         CALL WRR(SKOV,6,'SKOV')
         SKOV(3)=0.D0
         II=-3
         DO 39 I=1,3
            II=II+3
         DO 39 J=1,3
   39    BE(II+J,II+J)=SKOV(I)
         BE(1,4)=SKOV(4)
         BE(2,5)=SKOV(4)
         BE(3,6)=SKOV(4)
         BE(1,7)=SKOV(6)
         BE(2,8)=SKOV(6)
         BE(3,9)=SKOV(6)
         BE(4,7)=SKOV(5)
         BE(5,8)=SKOV(5)
         BE(6,9)=SKOV(5)
         DO 41 I=1,9
         DO 41 J=I,9
   41    BE(J,I)=BE(I,J)
C
         CALL INTEGK(SKE,BNL,BE,LM,WTU,ND,9)
      ENDIF
      ENDIF
C
      IF(IALFA.GE.0) THEN
C
CS       PROIZVOD MATRICA - GT * C
CE       MULTIPLY MATRIX - GT * C
C
         CALL MNOZM2(CEGE,GERS,ELAST,LA,6,6)
C
CS       INTEGRACIJA MATRICE - H
CE       INTEGRATE MATRIX - H
C        H = GT * C * G
C
         CALL INTEM1(HINV(1,1,NLM),CEGE,GERS,WTU,LA,LA,6)
C
CS       INTEGRACIJA MATRICE - GAMA
CE       INTEGRATE MATRIX - GAMA
C        GAMA = GT * C * B
C
         CALL INTEM1(GEEK(1,1,NLM),CEGE,BLT,WTU,LA,ND,6)
      ENDIF
      RETURN
      END
C=======================================================================
      SUBROUTINE BETBE5(H,BET,BNL,NEL,GKS,T,MTR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS    MATRICE BET (BE TRANSPONOVANO)
CE    MATRIX BET (BE TRANSPOSED)
C
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /IKOVAR/ INDKOV
      COMMON /CUVAJN/ TAUCEN(6,5),INDSEL,NGAUSU
      COMMON /CDEBUG/ IDEBUG
      DIMENSION H(9,*),BET(6,*),NEL(NE,*),GKS(3,2,*),MTR(*),RJ(2,3),
     1          BNL(9,*)
C
      IF(IDEBUG.GT.0) PRINT *, ' BETBE5'
      JJ=-2
      JF=NCVE*3-2
C
      DO 40 I=1,NCVE
         JJ=JJ+3
         JF=JF+3
         IF(NEL(NLM,I).EQ.0) GO TO 40
         JJ1=JJ+1
         JJ2=JJ+2
         JF1=JF+1
         JF2=JF+2
C
         IF(INDKOV.GE.0) THEN
C
CS          DEO VEZAN ZA TRANSLACIJE
CE          TRANSLATION PART
C
            IF(INDSEL.EQ.1.AND.NGAUSU.LT.NGS12) GO TO 981
C           D/DX
            BET(1,JJ )=0.D0
            BET(1,JJ1)=0.D0
            BET(1,JJ2)=0.D0
C           D/DY
            BET(2,JJ )=0.D0
            BET(2,JJ1)=0.D0
            BET(2,JJ2)=0.D0
C           D/DZ
            BET(3,JJ )=0.D0
            BET(3,JJ1)=0.D0
            BET(3,JJ2)=0.D0
C           D/DY , D/DX
            BET(4,JJ )=0.D0
            BET(4,JJ1)=0.D0
            BET(4,JJ2)=0.D0
  981       IF(INDSEL.EQ.1.AND.NGAUSU.EQ.NGS12) GO TO 982
C           D/DZ , D/DY
            BET(5,JJ )=0.D0
            BET(5,JJ1)=0.D0
            BET(5,JJ2)= XJ(2,1)*H(I,2)+XJ(2,2)*H(I,3)
C           D/DZ , D/DX
            BET(6,JJ )=0.D0
            BET(6,JJ1)=0.D0
            BET(6,JJ2)= XJ(1,1)*H(I,2)+XJ(1,2)*H(I,3)
C
CS          ROTACIJE
CE          ROTATION PART
C
  982       IF(INDSEL.EQ.1.AND.NGAUSU.LT.NGS12) GO TO 983
            BET(1,JF )= 0.D0
            BET(1,JF1)= XJ(1,1)*H(I,2)+XJ(1,2)*H(I,3)
            BET(2,JF )=-XJ(2,1)*H(I,2)-XJ(2,2)*H(I,3)
            BET(2,JF1)= 0.D0
            BET(3,JF )= 0.D0
            BET(3,JF1)= 0.D0
            BET(4,JF )=-XJ(1,1)*H(I,2)-XJ(1,2)*H(I,3)
            BET(4,JF1)= XJ(2,1)*H(I,2)+XJ(2,2)*H(I,3)
  983       IF(INDSEL.EQ.1.AND.NGAUSU.EQ.NGS12) GO TO 984
            BET(5,JF )=-H(I,1)
            BET(5,JF1)= 0.D0
            BET(6,JF )= 0.D0
            BET(6,JF1)= H(I,1)
         ELSE
C
CS          DEO VEZAN ZA TRANSLACIJE
CE          TRANSLATION PART
C
C           PT=1.D0-T*T
            PT=1.D0
C
            IF(INDSEL.EQ.1.AND.NGAUSU.LT.NGS12) GO TO 991
C           D/DR
            BET(1,JJ )=XJJ(1,1)*H(I,2)
            BET(1,JJ1)=XJJ(1,2)*H(I,2)
            BET(1,JJ2)=XJJ(1,3)*H(I,2)
C           D/DS
            BET(2,JJ )=XJJ(2,1)*H(I,3)
            BET(2,JJ1)=XJJ(2,2)*H(I,3)
            BET(2,JJ2)=XJJ(2,3)*H(I,3)
C           D/DT
            BET(3,JJ )=0.D0
            BET(3,JJ1)=0.D0
            BET(3,JJ2)=0.D0
C           D/DR , D/DS
            BET(4,JJ )=XJJ(1,1)*H(I,3)+XJJ(2,1)*H(I,2)
            BET(4,JJ1)=XJJ(1,2)*H(I,3)+XJJ(2,2)*H(I,2)
            BET(4,JJ2)=XJJ(1,3)*H(I,3)+XJJ(2,3)*H(I,2)
  991       IF(INDSEL.EQ.1.AND.NGAUSU.EQ.NGS12) GO TO 992
C           D/DS , D/DT
            BET(5,JJ )=XJJ(3,1)*H(I,3)*PT
            BET(5,JJ1)=XJJ(3,2)*H(I,3)*PT
            BET(5,JJ2)=XJJ(3,3)*H(I,3)*PT
C           D/DT , D/DR
            BET(6,JJ )=XJJ(3,1)*H(I,2)*PT
            BET(6,JJ1)=XJJ(3,2)*H(I,2)*PT
            BET(6,JJ2)=XJJ(3,3)*H(I,2)*PT
C
CS          ROTACIJE
CE          ROTATION PART
C
  992       CALL CLEAR(RJ,6)
            DO 20 JC=1,3
            DO 20 JZ=1,3
               RJ(1,JC)=RJ(1,JC)+GKS(JZ,1,I)*XJJ(JC,JZ)
               RJ(2,JC)=RJ(2,JC)+GKS(JZ,2,I)*XJJ(JC,JZ)
   20       CONTINUE
            IF(INDSEL.EQ.1.AND.NGAUSU.LT.NGS12) GO TO 993
C           D/DR
            BET(1,JF )=T*H(I,2)*RJ(1,1)
            BET(1,JF1)=T*H(I,2)*RJ(2,1)
C           D/DS
            BET(2,JF )=T*H(I,3)*RJ(1,2)
            BET(2,JF1)=T*H(I,3)*RJ(2,2)
C           D/DT
            BET(3,JF )=0.D0
C           BET(3,JF )=  H(I,1)*RJ(1,3)
            BET(3,JF1)=0.D0
C           BET(3,JF1)=  H(I,1)*RJ(2,3)
C           D/DR , D/DS
            BET(4,JF )=T*H(I,3)*RJ(1,1)+T*H(I,2)*RJ(1,2)
            BET(4,JF1)=T*H(I,3)*RJ(2,1)+T*H(I,2)*RJ(2,2)
  993       IF(INDSEL.EQ.1.AND.NGAUSU.EQ.NGS12) GO TO 994
C           D/DS , D/DT
            BET(5,JF )=(T*H(I,3)*RJ(1,3)+  H(I,1)*RJ(1,2))*PT
            BET(5,JF1)=(T*H(I,3)*RJ(2,3)+  H(I,1)*RJ(2,2))*PT
C           D/DT , D/DR
            BET(6,JF )=(T*H(I,2)*RJ(1,3)+  H(I,1)*RJ(1,1))*PT
            BET(6,JF1)=(T*H(I,2)*RJ(2,3)+  H(I,1)*RJ(2,1))*PT
C
  994       IF(IATYP.GT.1.AND.INDKOV.EQ.-1) THEN
               DO 25 KZ=1,9
               BNL(KZ,JJ )=0.D0
               BNL(KZ,JJ1)=0.D0
               BNL(KZ,JJ2)=0.D0
   25          BNL(KZ,JF2)=0.D0
C
CS             DEO VEZAN ZA TRANSLACIJE
CE             TRANSLATION PART
C
C              D/DR
               BNL(1,JJ )=H(I,2)
               BNL(2,JJ1)=H(I,2)
               BNL(3,JJ2)=H(I,2)
C              D/DS
               BNL(4,JJ )=H(I,3)
               BNL(5,JJ1)=H(I,3)
               BNL(6,JJ2)=H(I,3)
C
CS             ROTACIJE
CE             ROTATION PART
C
C              D/DR
               BNL(1,JF )=T*H(I,2)*GKS(1,1,I)
               BNL(1,JF1)=T*H(I,2)*GKS(1,2,I)
               BNL(2,JF )=T*H(I,2)*GKS(2,1,I)
               BNL(2,JF1)=T*H(I,2)*GKS(2,2,I)
               BNL(3,JF )=T*H(I,2)*GKS(3,1,I)
               BNL(3,JF1)=T*H(I,2)*GKS(3,2,I)
C              D/DS
               BNL(4,JF )=T*H(I,3)*GKS(1,1,I)
               BNL(4,JF1)=T*H(I,3)*GKS(1,2,I)
               BNL(5,JF )=T*H(I,3)*GKS(2,1,I)
               BNL(5,JF1)=T*H(I,3)*GKS(2,2,I)
               BNL(6,JF )=T*H(I,3)*GKS(3,1,I)
               BNL(6,JF1)=T*H(I,3)*GKS(3,2,I)
C              D/DT
               BNL(7,JF )=  H(I,1)*GKS(1,1,I)
               BNL(7,JF1)=  H(I,1)*GKS(1,2,I)
               BNL(8,JF )=  H(I,1)*GKS(2,1,I)
               BNL(8,JF1)=  H(I,1)*GKS(2,2,I)
               BNL(9,JF )=  H(I,1)*GKS(3,1,I)
               BNL(9,JF1)=  H(I,1)*GKS(3,2,I)
            ENDIF
         ENDIF
  984    DO 30 KZ=1,6
   30    BET(KZ,JF2)=0.D0
   40 CONTINUE
      RETURN
      END
