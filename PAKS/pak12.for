C=======================================================================
C
C   SUBROUTINE S02EGL
C              READE1
C              SISTT1
C              ELTE1
C              MEL011
C              MEL031
C=======================================================================
      SUBROUTINE S02EGL
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA FORMIRANJE MATRICA I VEKTORA 
CS     1/D ELEMENATA
CE     MAIN PROGRAM FOR CALCULATION OF ELEMENT MATRIX FOR 
CE     1/D ELEMENTS
C
      include 'paka.inc'
      
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /DUPLAP/ IDVA
      COMMON /SRPSKI/ ISRPS
C
      LAU=LMAX
      CALL READE1(A(LAU),0)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      LAE=LMAX
      NCVE3=NCVE*3
      MXAE = NCVE3+(NCVE3+NCVE3*(NCVE3+1)/2)*IDVA
      LMAX = LAE + MXAE
      IF(LMAX.LT.MTOT) GO TO 70
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2009) LMAX,MTOT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6009) LMAX,MTOT
      STOP
C
C
C
   70 CALL SISTT1(A(LAE),A(LAU))
C
      RETURN
C-----------------------------------------------------------------------
 2009 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A ZA MATRICE ELEMENATA'
     1/' POTREBNA DIMENZIJA, LMAX=',I10/
     2' RASPOLOZIVA DIMENZIJA, MTOT=',I10)
 6009 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE READE1(AU,INA)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
CE     MENAGEMENT ROUTINE FOR INPUT DATA IN    AU
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /INCONF/ LINDBEL,LBIRTHC
      COMMON /ZAPISI/ LSTAZA(5)
C
      DIMENSION AU(*)
      REAL AU
C
CS     CITANJE SKALARA IZ DIREKT ACCES FILE 
CE     READ SCALARS FROM A DIRECT ACCESS FILE
C
      LSTAZA(1)=LMAX8
      READ(IELEM,REC=LMAX8)
     1 NCVE,ITERME,MXAU,LNEL,LNMAT,LAPRS,LISNA,LLMEL,LCEL,LELC,NMA,NMI,
     1 INDBTH,INDDTH,LTBTH,LTDTH,LINDBEL,LBIRTHC
      LSTAZA(2)=LMAX8+1
      CALL READDD(AU(LAPRS),MXAU/IDVA,IELEM,LMAX8,LDUZI)
      LMAX=LAU+MXAU
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      IF(IATYP.EQ.0) GO TO 10
      IF(NMODM.LE.4.OR.NMODM.EQ.11.OR.NMODM.EQ.12) GO TO 10
CS....  PLASTICNOST
CS....  VELICINE NA KRAJU PRETHODNOG KORAKA
CE     PLASTICITY
      LPLAST=LMAX
      NPROS=NE*MODPR1( NMODM )*IDVA
      LPLAS1=LPLAST+NPROS
CS....  VELICINE ZA TEKUCI KORAK
      NPROS1 = NPROS
      LMAX=LPLAS1+NPROS1
      LSIGMA = LMAX
      IF(NMODM.EQ.5) LSIGMA=LPLAS1+2*NE*IDVA
C
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(3)=LMAX8+1
      CALL READDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      LSTAZA(4)=LMAX8+1
      CALL READDD(A(LPLAS1),NPROS1/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
C
   10 LSIGMA=LMAX
      NPROS=2*NE*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(5)=LMAX8+1
      IF(INA.EQ.1) CALL READDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      IF(INA.EQ.0) CALL CLEAR(A(LSIGMA),NPROS/IDVA)
      RETURN
      END
C======================================================================
      SUBROUTINE SISTT1(AE,AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM  ZA MATRICE ELEMENATA I SISTEMA
CE     MAIN MANAGEMENT  PROGRAM  FOR ELEMENT MATRIX
C
      include 'paka.inc'
      
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TEMPCV/ LTECV,ITEMP
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /DUPLAP/ IDVA
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /INCONF/ LINDBEL,LBIRTHC
      COMMON /UPDLAG/ LUL,LCORUL
C
      DIMENSION AE(*),AU(*)
      REAL AE,AU
C       REPERI U VEKTORU ELEMENATA AE
      NCVE3=NCVE*3
      ND=NCVE3
      NWE=NCVE3*(NCVE3+1)/2
      LRTHE=1
      LSKE=LRTHE+NCVE3*IDVA
      LLM=LSKE+NWE*IDVA
      MXAE1=LLM+NCVE3
C
CS            P E T L J A    P O    E L E M E N T I M A
CE            L O O P    O V E R    E L E M E N T S
C
      DO 100 NLM=1,NE
C
CS       NASTAJANJE I NESTAJANJE ELEMENATA
CE       ELEMENT BIRTH AND DEATH OPTION
C
         IBD=0
         CALL DTHBTH(AU(LTBTH),AU(LTDTH),VREME,NLM,IBD)
         IF(IBD.EQ.1) GO TO 100
c	write(3,*) 'ulazi u stap',kor,vreme
C
      IZBR=MXAE/IDVA
      CALL CLEAR(AE,IZBR)
C
      KORD=LCORD
      CALL ELTE1(AE(LSKE),AE(LLM),AU(LNEL),AU(LNMAT),
     1AU(LAPRS),A(KORD),A(LRTDT),A(LFTDT),
     2A(LSIGMA),AU(LLMEL),A(LTECV),ND,NCVE3,AU(LINDBEL),AU(LBIRTHC),
     3AU(LTBTH))
C
CS     RASPOREDJIVANJE MATRICE KRUTOSTI (SKE)
CE     ASSEMBLE STIFFNESS MATRIX
C
      IF(ISKNP.EQ.2) GO TO 100
      CALL SPAKUJ(A(LSK),A(LMAXA),AE(LSKE),AE(LLM),ND)
  100 CONTINUE
      IF(indbth.eq.1) THEN
C
C       PRIPREMA ZA RESTART
C
        LMA8=LSTAZA(2)-1
        CALL WRITDD(AU,MXAU/IDVA,IELEM,LMA8,LDUZI)
      ENDIF
C
      IF(IATYP.EQ.0) GO TO 110
      IF(NMODM.LE.4.OR.NMODM.EQ.11.OR.NMODM.EQ.12) GO TO 110
      NPROS=NE*MODPR1( NMODM )*IDVA
      LMA8=LSTAZA(4)-1
      CALL WRITDD(A(LPLAS1),NPROS/IDVA,IELEM,LMA8,LDUZI)
      RETURN
C
  110 NPROS=2*NE*IDVA
      LMA8=LSTAZA(5)-1
      CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMA8,LDUZI)
      RETURN
      END
C=======================================================================
      SUBROUTINE ELTE1(SKE,LM,NEL,NMAT,APRS,CORD,
     1RTDT,FTDT,SIGMA,LMEL,TECV,ND,NCVE3,INDBEL,BIRTHC,TBTH)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE MATRICA ELEMENATA I SISTEMA
CE     FORM ELEMENT MATRIX
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /REPERM/ MREPER(4)
      COMMON /TEMPCV/ LTECV,ITEMP
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ANALIZ/ LINEAR,ITERGL,INDDIN
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /MPOINC/ MMP,NMPC,NEZAV,LCMPC,LMPC,NEZA1
      COMMON /SRPSKI/ ISRPS
      COMMON /VISK1/ APR
      COMMON /ZADATA/ LNZADJ,LNZADF,LZADFM,NZADP
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
C
      DIMENSION SKE(*),LM(*),NEL(NE,*),NMAT(*),APRS(*),
     1    CORD(NP,*),RTDT(*),FTDT(*),SIGMA(NE,*),LMEL(NCVE3,*),TECV(*)
      DIMENSION INDBEL(*),BIRTHC(NE,*),TBTH(*)
      DIMENSION UE(6),FE(6),ST(6),D(3),DUE(3)
C
      SQRT(X)=DSQRT(X)
C
      IF(IATYP.EQ.3)IATYP=2
C
CE       LM(ND): LOCAL ELEMENT CONNECTIVITY MADE BY COPYING FROM
CE       LMEL(ND,NE): THE GLOBAL ELEMENT CONNECTIVITY ARRAY
         CALL IJEDN1(LM,LMEL(1,NLM),ND)
C
CE    INITIAL CONFIGURATION FOR BIRDTH ELEMENT
C
         IF(INDBTH.EQ.1) THEN
            IF(INDBEL(NLM).EQ.0.AND.
     1         VREME.GE.TBTH(NLM).AND.TBTH(NLM).GT.1.E-10) THEN
               DO 241 I=1,ND
                  IF(LM(I).EQ.0) GO TO 241
                  J=LM(I)
                  if(j.lt.0) THEN
                     BIRTHC(NLM,I)=condof(rtdt,a(lcmpc),a(lmpc),j)
                  ELSE
                     BIRTHC(NLM,I)=RTDT(J)
                  ENDIF
  241          CONTINUE
               INDBEL(NLM)=1
C               write(3,1241) vreme,TBTH(nlm),NLM,(birthc(nlm,i),i=1,nd)
C 1241          format(' T',2F6.1,I9,8(1X,3(1PE8.1)))
            ENDIF
         ENDIF
C
CS     VEKTOR LM, POMERANJA NA ELEMENTU
CE     VECTOR LM, ELEMENT DISPLACEMENTS
C
      DO 10 NC=1,ND
        K=LM(NC)
        IF(K.GT.0)THEN
          UE(NC)=RTDT(K)
          ELSEIF(K.EQ.0)THEN
            UE(NC)=0.D0
          ELSE
            UE(NC)=CONDOF(RTDT,A(LCMPC),A(LMPC),K)
        ENDIF
            IF(INDBTH.EQ.1) THEN
               IF(INDBEL(NLM).EQ.1) THEN
                  UE(NC)=UE(NC)-BIRTHC(NLM,NC)
               ENDIF
            ENDIF 
   10 CONTINUE
C            IF(INDBTH.EQ.1) THEN
C               IF(INDBEL(NLM).EQ.1) THEN 
cC               write(3,1242) vreme,TBTH(nlm),NLM,(birthc(nlm,i),i=1,nd)
c               write(3,1242) vreme,TBTH(nlm),NLM,(ue(i),i=1,nd)
C 1242          format(' K',2F6.1,I9,8(1X,3(1PE8.1)))
C               ENDIF
C            ENDIF
C
CS...... PETLJA PO GAUSOVIM TACKAMA 
CE...... LOOP OVER GAUSS POINTS
C
      NC1=NEL(NLM,1)
      NC2=NEL(NLM,2)
      MAT=NMAT(NLM)
      APR=APRS(NLM)
CS     TEMPERATURA GAUSOVE TACKE
CE     TEMPHERATURE OF GAUSS POINT
      IF(ITERME.NE.0) TGT=(TECV(NC1)+TECV(NC2))/2.D0
      GO TO (     1,999,  2,999,  5,999,999,999,999,999,
     1            5,  6,999,  5,999,999,999,999,999,999,
     2          999,999,999,  5,999,999,999,999,999,  5,
     3          999,999,999,999,999,999,999,999,999,999,
     4          999,999,999,999,999,999,999,999,999,999,
     5          999,999,999,999,999,999,999,999,999,999,
     6          999,999,999,999,999,999,999,999,999,999,
     7          999,999,999,999,999,999,999,999,999,999,
     8          999,999,999,999,999,999,999,999,999,999,
     9          999,999,999,999,999,999,999,999,999,999),NMODM
    1 LFUN=MREPER(1)
      CALL MEL011(A(LFUN))
      GO TO 40
    2 LFUN=MREPER(1)
      LNTA=MREPER(2)
      LTEM=MREPER(3)
      MATE=MREPER(4)
      CALL MEL031(A(LFUN),A(LNTA),A(LTEM),MATE,TGT)
      GO TO 40
    5 CALL MODMA1(STR,STRESS,TGT,NMODM,NLM,2)
      GO TO 40
    6 DL2=0.D0
      XL2=0.D0
      DO 503 L=1,3
      D(L)=CORD(NC1,L)-CORD(NC2,L)
      XL2=XL2+D(L)*D(L)
      DUE(L)=D(L)+UE(L)-UE(L+3)
  503 DL2=DL2+DUE(L)*DUE(L)
      DL=SQRT(DL2)-SQRT(XL2)
      CALL MODMA1(DL,FORCE,TGT,NMODM,NLM,2)
      GO TO 40
CS.....  LINEARNA ANALIZA
CE       LINEAR ANALYSIS
   40 XL2=0.D0
      DO 505 L=1,3
      D(L)=CORD(NC1,L)-CORD(NC2,L)
  505 XL2=XL2+D(L)*D(L)
      XL=SQRT(XL2)
      IF (NMODM.EQ.12) ELAST(1,1)=ELAST(1,1)*XL/APR
CS.....  TOTAL LAGRANZ
CE       T.L.
      IF(IATYP.EQ.2)THEN
        XLT2=0.D0
        DO 504 L=1,3
        D(L)=D(L)+UE(L)
        D(L)=D(L)-UE(L+3)
        XLT2=XLT2+D(L)*D(L)
  504   CONTINUE
        XLT=SQRT(XLT2)
      ENDIF
CS.....  PRAVAC STAPA
      DO 510 L=1,3
        ST(L)=D(L)/XL2
  510   ST(L+3)=-ST(L)
C
CS     RACUNANJE UKUPNIH DEFORMACIJA
CE     TOTAL STRAINS
C
      IF(ISKNP.EQ.1) GO TO 400
CS.....  LINEARNA ANALIZA I MNO
CE       LINEAR ANALYSIS AND M.N.O.
      IF(IATYP.EQ.0.OR.IATYP.EQ.1) THEN
        STR=0.D0
        DO 110 L=1,ND
  110   STR=STR+ST(L)*UE(L)
      ENDIF
CS.....  NELINEARNA ANALIZA  (T.L.   ,   U.L.)
CE       T.L.  ,   U.L.
      IF(IATYP.EQ.2) STR=(XLT2/XL2-1.D0)/2.D0
CS.....   RACUNANJE TERMICKIH DEFORMACIJA - ETH; ETH=ALFA*(T-T0)
CE        THERMAL  STRAINS
      IF(ITERME.EQ.0) GO TO 100
      DTGT0=TGT-TEMP0
      STR=STR-ALFA(1)*DTGT0
  100 CONTINUE
C
CS     RACUNANJE NAPONA TAU=C*(E-ETH)
CE     CALCULATE STRESS
C
CS.....  MATERIJALNO LINEARNA ANALIZA
      IF(NMODM.LE.4) THEN
        STRESS=ELAST(1,1)*STR
        SIGMA(NLM,1)=STRESS
        SIGMA(NLM,2)=STR
      ELSE
CS   RACUNANJE NAPONA ZA PLASTICNOST
CE   MATERIAL NONLINEARITY
        ARG=STR
        IF (NMODM.EQ.12) ARG=DL
        CALL MODMA1(ARG,STRESS,TGT,NMODM,NLM,1)
        IF (NMODM.EQ.12) THEN
          ELAST(1,1)=ELAST(1,1)*XL/APR
          STRESS=STRESS/APR
        END IF
        IF(NMODM.EQ.11.OR.NMODM.EQ.12)THEN
          SIGMA(NLM,1)=STRESS
          SIGMA(NLM,2)=STR
        ENDIF
      ENDIF
C
CS    RACUNANJE UNUTRASNJIH SILA FTDT=BET*TAU
CE    INTERNAL FORCE
C
C      IF(NGENL.EQ.0) GO TO 400
      IF(NGENL.GT.0.OR.(NZADP.GT.0.AND.ISKNP.EQ.2.AND.NGENL.EQ.0)) THEN
         BETT=STRESS*APR*XL
         DO 310 II=1,ND
  310    IF(LM(II).NE.0) FE(II)=ST(II)*BETT
C
CS       RAZMESTANJE UNUTRASNJIH SILA FE U GLOBALNI VEKTOR FTDT
CE       ASSEMBLE INTERNAL FORCE VECTOR
C
         CALL SILPAK(FTDT,FE,LM,ND,A(LCMPC),A(LMPC))
C
      ENDIF
C
CS    INTEGRACIJA SKE
CE    ELEMENT STIFFNESS MATRIX
C
  400 IF(ISKNP.EQ.2) GO TO 50
CS.....  LINEARNI DEO
CE       LINEAR PART
      XX=ELAST(1,1)*APR*XL
      KL=0
      DO 600 L=1,6
      YY=ST(L)*XX
        DO 600 K=L,6
        KL=KL+1
  600   SKE(KL)=ST(K)*YY
CS.....  NELINEARNI DEO (T.L.)
CE       NONLINEAR PART
      IF(IATYP.EQ.2.OR.IATYP.EQ.3) THEN
          CNL=SIGMA(NLM,1)*APR/XL
          L=7
          KN=1
  615     SKE(KN)=SKE(KN)+CNL
          KN3=KN+3
          IF(KN3.GT.15) GO TO 620
          SKE(KN3)=SKE(KN3)-CNL
  620     L=L-1
          KN=KN+L
          IF(KN.GT.21) GO TO 630
          GO TO 615
  630     CONTINUE
      ENDIF
C
   50 CONTINUE
      RETURN
C
  999 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2000) NMODM
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6000) NMODM
      STOP 'ELTE1'
C-----------------------------------------------------------------------
 2000 FORMAT(//' ','MODEL MATERIJALA  = ',I3,'  NEMA SMISLA ZA STAPOVE')
 6000 FORMAT(//' ','MATERIAL MODEL  = ',I3,'  HAVE NO SENSE FOR TRUSS')
C-----------------------------------------------------------------------
      END
C======================================================================
      SUBROUTINE MEL011(FUN)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE MATRICE ELAST
CE     ELAST MATRIX
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      DIMENSION FUN(2,*)
C
      ELAST(1,1)=FUN(1,MAT)
      RETURN
      END
C======================================================================
      SUBROUTINE MEL031(FUN,NTA,TEM,MATE,TGT)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE MATRICE ELAST
CE     ELAST MATRIX
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION FUN(2,MATE*3,*),NTA(*),TEM(*)
C
C     E , V , ALFA
C
      DO 6 J=1,3
      NFE=(MAT-1)*3+J
      CALL TABF(FUN,NTA,NFE,MATE*3,TGT,EVA,NTMX,IND)
      IF(IND.GT.0) GO TO 120
      IF(J.EQ.1) ELAST(1,1)=EVA
    6 CONTINUE
      ALFA(1)=EVA
      TEMP0=TEM(MAT)
      RETURN
  120 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2000) NFE,TGT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6000) NFE,TGT
      STOP
C-----------------------------------------------------------------------
 2000 FORMAT(///' ARGUMENT VAN OPSEGA ZADATE KRIVE U MEL13'/
     1' TEMPERATURSKA FUNKCIJA BROJ =',I5/
     2' ARGUMENT TEMPERATURA =',1PD12.4)
 6000 FORMAT(///' ARGUMENT IS OUT OF RANGE'/
     1' TEMPERATURE FUNCTION  =',I5/
     2' ARGUMENT TEMPERATURE  =',1PD12.4)
C-----------------------------------------------------------------------
      END
C=======================================================================
      SUBROUTINE MODMA1(DEF,TAU,TGT,NMODM,NLM,IRAC)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA POZIVANJE PODPROGRAMA INTEGRACIJU KONSTITUTIVNIH
C     RELACIJA
C
      GO TO (   999,999,999,999,  5,999,999,999,999,999,
     1           11, 11,999,  5,999,999,999,999,999,999,
     2          999,999,999, 24,999,999,999,999,999, 30,
     3          999,999,999,999,999,999,999,999,999,999,
     4          999,999,999,999,999,999,999,999,999,999,
     5          999,999,999,999,999,999,999,999,999,999,
     6          999,999,999,999,999,999,999,999,999,999,
     7          999,999,999,999,999,999,999,999,999,999,
     8          999,999,999,999,999,999,999,999,999,999,
     9          999,999,999,999,999,999,999,999,999,999),NMODM
C
    5 CALL D1M05(DEF,TAU,TGT,NMODM,NLM,IRAC)
      RETURN
   11 CALL D1M11(DEF,TAU,IRAC)
      RETURN
C     VISKOZNI OTPOR - MODEL 24
   24 CALL D1M24(DEF,TAU,NLM,IRAC)
      RETURN
C     FIBER
   30 CALL D1M30(DEF,TAU,NLM,IRAC)
  999 RETURN
      END
