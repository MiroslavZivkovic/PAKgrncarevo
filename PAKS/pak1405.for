C=======================================================================
C
C   PLASTICNOST I ZADATO NELINEARNO PONASANJE STAPNOG ELEMENTA
C
C    SUBROUTINE MODMA1
C               D1M05
C               TAUIN1
C               TEQDEP
C               FESF1
C               MEL051
C               MEL071
C               D1M11
C               TAU11
C               MEL111
C
C======================================================================
      SUBROUTINE D1M05(DEF,TAU,TGT,MODEL,NLM,IRAC)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA POZIVANJE PODPROGRAMA INTEGRACIJU KONSTITUTIVNIH
C     RELACIJA ZA STAP
C
      include 'paka.inc'
      
      COMMON /REPERM/ MREPER(4)
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
C
      LFUN=MREPER(1)
      LNTA=MREPER(2)
      LTEM=MREPER(3)
      MATE=MREPER(4)
C
      NLL=NE*IDVA
      NLM1=(NLM-1)*IDVA
C
      LTEQT =LPLAST+NLM1
      LDEFPT=LPLAST+NLL+NLM1
      LSIGT =LPLAST+2*NLL+NLM1
      LSTRT =LPLAST+3*NLL+NLM1
C
      LTEQ  =LPLAS1+NLM1
      LDEFP =LPLAS1+NLL+NLM1
      LSIG  =LPLAS1+2*NLL+NLM1
      LSTR  =LPLAS1+3*NLL+NLM1
      CALL TAUIN1(DEF,TAU,TGT,A(LTEQT),A(LDEFPT),A(LSIGT),A(LSTRT),
     1   A(LTEQ),A(LDEFP),A(LSIG),A(LSTR),A(LFUN),A(LNTA),A(LTEM),
     1   MODEL,MATE,IRAC)
      RETURN
      END
C======================================================================
      SUBROUTINE TAUIN1(DEF,TAU,TGT,TEQT,DEFPT,SIGT,STRT,TEQ,DEFP,
     1                  SIG,STR,FUN,NTA,TEM,MODEL,MATE,IRAC)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA INTEGRACIJU KONSTITUTIVNIH RELACIJA ZA ELASTOPLASTIC
C     ELASTOPLASTICAN MATERIJAL SA IZOTROPNIM OJACANJEM
C
C        TEQT   = NAPON TECENJA NA KRAJU PRETHODNOG KORAKA
C        TEQ    = TEKUCI NAPON TECENJA
C        TAU    = TEKUCI ELASTO-PLASTICNI NAPON
C        DEFPT  = PLASTICNE DEFORMACIJE NA KRAJU PRETHODNOG KORAKA
C        DEFP   = TEKUCE PLASTICNE DEFORMACIJE
C        DEF    = TEKUCE UKUPNE DEFORMACIJE
C        SIGT   = NAPONI NA KRJU PRETHODNOG KORAKA
C        STRT   = DEFORMACIJE NA KRAJU PRETHODNOG KORAKA
C        SIG    = VEKTOR SA VREDNOSTIMA  TAU
C        STR    = VEKTOR SA VREDNOSTIMA  DEF
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /MATSTP/ E,ET,EP,TEQY0
C
      IS=IRAC+1
      GO TO (25,50,100),IS
C
C     INICIJALIZACIJA
C
   25 IF(MODEL.EQ.5) CALL MEL051(FUN,NTA,MATE,MAT,TEQ,TEQT)
      IF(MODEL.EQ.7) CALL MEL071(FUN,NTA,TEM,MATE,MAT,TEQ,TEQT,TGT)
      TEQT =TEQY0
      TEQ  =TEQY0
      DEFPT=0.D0
      DEFP =0.D0
      SIGT=0.D0
      SIG =0.D0
      STRT=0.D0
      STR =0.D0
      RETURN
C
C     ODREDIVANJE NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
C
   50 DELEPS=DEF-STRT
      DELSIG=E*DELEPS
      TAU=SIGT+DELSIG
C....  NAPON TECENJA ZA TERMO-PLASTICNOST
      IF(MODEL.EQ.7) TEQT=TEQY0+EP*DEFPT
      TEQ=TEQT
      DEFP=DEFPT
C
      SIG=TAU
      STR=DEF
C     WRITE(3,*)'TAU elasticno',TAU
C     WRITE(3,*)'DEF          ',DEF
C
C     ODREDIVANJE EFEKTIVNOG NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
C     JEDNAK JE NAPONU TAU
C
C....  RESENJE JE ELASTICNO
      IF(DABS(TAU).LE.TEQT)RETURN
C
C     RESENJE JE ELASTOPLASTICNO , TREBA ODREDITI PRIRASTAJ PLASTICNE
C     DEFORMACIJE IPRIRASTAJ NAPONA PRIMENOM FUNKCIJE EFEKTIVNOG NAPONA
C
C....  ODREDJIVANJE EFEKTIVNOG NAPONA I PRIRASTAJA PLASTICNE DEFORMAC.
      ISEG=1
      CALL TEQDEP(TEQ,DDEFP,TEQT,DEF,DEFPT,DEF2,
     1            FUN,NTA,MATE,MAT,MODEL,ISEG)
C
C     ODREDIVANJE PLASTICNE DEFORMACIJE U TEKUCOJ ITERACIJI
C
      DEFP=DEFPT+DSIGN(DDEFP,DEF2)
C
C     ODREDIVANJE NAPONA   I  DEFORMACIJE (NEOPHODNO ZA PERF. PLAST.)
C
      TAU=DSIGN(TEQ,DEF2)
      SIG=TAU
      RETURN
C
  100 IF(MODEL.EQ.5) CALL MEL051(FUN,NTA,MATE,MAT,TEQ,TEQT)
      IF(MODEL.EQ.7) CALL MEL071(FUN,NTA,TEM,MATE,MAT,TEQ,TEQT,TGT)
      RETURN
      END
C======================================================================
      SUBROUTINE TEQDEP(TEQ,DDEFP,TEQT,DEF,DEFPT,DEF2,FUN,NTA,MATE,MAT,
     1                  MODEL,ISEG)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PROGRAM ZA NALAZENJE EFEKTIVNOG NAPONA I PRIRASTAJA PLAST. DEFORM
C     (PRILAGODJENO ZA STAP)
C
      COMMON /MATSTP/ E,ET,EP,TEQY0
      DIMENSION FUN(2,MATE,*),NTA(*)
C
C....  DEFORMACIJA E''= T+DT(E) - T(EP)
      DEF2=DEF-DEFPT
      ADEF2=DABS(DEF2)
      DDEFP=0.D0
C
      ISGMAX=2
      IF(MODEL.EQ.5)ISGMAX=NTA(MAT)+1
C....  BILINEARNA KRIVA
      IF(ISGMAX.EQ.2)THEN
        ZEZA=ADEF2
      ELSE
C....  MULTILINEARNA KRIVA
C....  PROVERA ZA SLEDECI SEGMENT
   10   ISEG=ISEG+1
        IF(ISEG.GT.ISGMAX)THEN
          DEFPEF=FUN(1,MAT,ISEG-1)
          SIGEF =FUN(2,MAT,ISEG-1)
          DDEFP0=DEFPEF-DEFPT
          GO TO 60
        ENDIF
        DEFPEF=FUN(1,MAT,ISEG)
        SIGEF =FUN(2,MAT,ISEG)
        DDEFP0=DEFPEF-DABS(DEFPT)
        ZEZA0=DABS(DEF-DSIGN(DEFPEF,DEF2))
        IF(FESF1(SIGEF,ZEZA0,E))40,50,60
C....  SLEDECI SEGMENT
   40     DDEFP=DDEFP0
          TEQT=SIGEF
          DFPT=DEFPEF
          ZEZA=ZEZA0
          GO TO 10
C....  PRONADJENE TACNE VREDNOSTI
   50     TEQ=SIGEF
          DDEFP=DDEFP0
          RETURN
C....  TRAZENE VELICINE NA PRETHODNOM SEGMENTU
   60     EP=(SIGEF-TEQT)/(DEFPEF-DFPT)
          IF(DABS(EP).LT.1.D-8) EP=E/1.D8
      ENDIF
C.... PERFEKTNA PLASTICNOST
      IF(DABS(EP-E/1.D8).LT.1.D-20)THEN
        TEQ=TEQY0
        DDEFP=DDEFP+ZEZA-TEQ/E
      ELSE
C....  FINALNE VREDNOSTI
        TEQ=E/(EP+E)*(TEQT+ZEZA*EP)
        DDEFP=DDEFP+(TEQ-TEQT)/EP
      ENDIF
      RETURN
      END
C======================================================================
      FUNCTION FESF1(SIGEF,ADEF2,E)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C....  FUNKCIJA EFEKTIVNOG NAPONA ZA STAP
      FESF1=SIGEF/E-ADEF2
      RETURN
      END
C======================================================================
      SUBROUTINE MEL051(FUN,NTA,MATE,MAT,TEQ,TEQT)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST
C
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /MATSTP/ E,ET,EP,TEQY0
      COMMON /ITERBR/ ITER
      DIMENSION FUN(2,MATE,*),NTA(*)
C
C     UCITAVANJE FUNKCIJE
C
      NCT=NTA(MAT)
        E    =FUN(1,MAT,1)
        ELAST(1,1)=E
      IF(NCT.EQ.1)THEN
C.. BILINEARNA KRIVA
        TEQY0=FUN(1,MAT,2)
        ET   =FUN(2,MAT,2)
        EP=E*ET/(E-ET)
      ELSE
C.. MULTILINEARNA KRIVA
        TEQY0=FUN(2,MAT,2)
        CALL EPPLAS(FUN,NTA,MATE,MAT,DABS(TEQ),EP)
      ENDIF
C
        IF(DABS(EP).LT.1.D-20) EP=E/1.D8
C.... NIJE PERFEKTNA PLASTICNOST
        IF(ITER.GT.0)THEN
          IF(DABS(TEQ).GT.TEQT) ELAST(1,1)=EP
        ENDIF
      RETURN
      END
C======================================================================
      SUBROUTINE MEL071(FUN,NTA,TEM,MATE,MAT,TEQ,TEQT,TGT)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST ZA TERMO-ELASTO-PLASTICAN MODEL 7
C
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /MATSTP/ E,ET,EP,TEQY0
      COMMON /ITERBR/ ITER
      COMMON /SRPSKI/ ISRPS
      DIMENSION FUN(2,MATE,*),NTA(*),TEM(*)
C
C     E , V , TEQY , ET  , ALFA , TEMP0
C
      MAT5=MAT*5
      MATE5=MATE*5
      DO 6 J=1,5
      NFE=MAT5-5+J
      CALL TABF(FUN,NTA,NFE,MATE5,TGT,EVA,NTMX,IND)
      IF(IND.GT.0) GO TO 120
      IF(J.EQ.1) E      =EVA
      IF(J.EQ.3) TEQY0  =EVA
      IF(J.EQ.4) ET     =EVA
    6 CONTINUE
      ALFA(1)=EVA
      TEMP0=TEM(MAT)
C
      ELAST(1,1)=E
      EP=E*ET/(E-ET)
C...PERFEKTNA PLASTICNOST
      IF(DABS(EP).LT.1.D-20) EP=E/1.D8
C...NIJE PERFEKTNA PLASTICNOST
      IF(ITER.GT.0.AND.DABS(TEQ).GT.TEQT) ELAST(1,1)=EP
      RETURN
  120 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2000) NFE,TGT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6000) NFE,TGT
      STOP
C-----------------------------------------------------------------------
 2000 FORMAT(///' ARGUMENT VAN OPSEGA ZADATE KRIVE U MEL071'/
     1' TEMPERATURSKA FUNKCIJA BROJ =',I5/
     2' ARGUMENT TEMPERATURA =',1PD12.4)
 6000 FORMAT(///' ARGUMENT IS OUT OF RANGE'/
     1' TEMPERATURE FUNCTION  =',I5/
     2' ARGUMENT TEMPERATURE  =',1PD12.4)
C-----------------------------------------------------------------------
      END
C======================================================================
      SUBROUTINE EPPLAS(FUN,NTA,MATE,MAT,TEQ,EP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     PLASTICNI MODUL EP  IZ MULTILINEARNE KRIVE
C
      DIMENSION FUN(2,MATE,*),NTA(*)
C
      NCT=NTA(MAT)
      NCT1=NCT+1
      J=1
   10 J=J+1
      IF(FUN(2,MAT,J).LT.TEQ)THEN
        IF(J.EQ.NCT1)GO TO 50
        GO TO 10
      ENDIF
   50 IF(J.GT.2)THEN
      EP=(FUN(2,MAT,J)-FUN(2,MAT,J-1))/(FUN(1,MAT,J)-FUN(1,MAT,J-1))
      ENDIF
      RETURN
      END
