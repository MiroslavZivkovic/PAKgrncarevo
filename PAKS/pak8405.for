C=======================================================================
C
C   PLASTICNOST  ELEMENTA LJUSKE
C
C    SUBROUTINE MODMAT
C               D2M5
C               TAUIN5
C               TEQBIS
C               PRILAM
C               DEVEQ
C
C=======================================================================
      SUBROUTINE D8M5(TAU,DEF,IRAC,LPOCG,LPOC1)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PROGRAM ZA ODREDIVANJE LOKACIJA VELICINA KOJE SE CUVAJU
C     NA NIVOU INTEGRACIONE TACKE
      include 'paka.inc'
      
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
C
      COMMON /REPERM/ MREPER(4)
C
      COMMON /DUPLAP/ IDVA
C
      DIMENSION TAU(*),DEF(*)
C
      LFUN=MREPER(1)
      LNTA=MREPER(2)
      MATE=MREPER(4)
      LTAU=LPOCG
      LDEFT=LTAU + 6*IDVA
      LDEFPT=LDEFT + 6*IDVA
      LTEQT=LDEFPT + 6*IDVA
      LTEQYT=LTEQT + 1*IDVA
      LDQPT=LTEQYT + 1*IDVA
      LIPL = LDQPT + IDVA
C
      LTAU1=LPOC1
      LDEFT1=LTAU1 + 6*IDVA
      LDEFP1=LDEFT1 + 6*IDVA
      LTEQT1=LDEFP1 + 6*IDVA
      LTEQY1=LTEQT1 + 1*IDVA
      LDQPT1=LTEQY1 + 1*IDVA
      LIPL1 = LDQPT1 + IDVA
      KN=LDQPT1+1
C
      CALL TAUIL5(A(LIPL),A(LTAU),A(LDEFT),A(LDEFPT),A(LTEQT),
     1A(LTEQYT),A(LDQPT),A(LIPL1),A(LTAU1),A(LDEFT1),A(LDEFP1),A(LTEQT1)
     1,A(LTEQY1),A(LDQPT1),A(LFUN),A(LNTA),MATE,TAU,DEF,IRAC)
C
      RETURN
      END
C======================================================================
      SUBROUTINE TAUIL5( PL,TAUT,DEFT,DEFPT,TEQT,TEQYT,DEFQPT,
     1            PL1,TAU1,DEF1,DEFP,TEQ,TEQY,DEFQP,FUN,NTA,MATE,
     1           TAU,DEF,IRAC)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA INTEGRACIJU KONSTITUTIVNIH RELACIJA ZA ELASTOPLASTIC
C     ELASTOPLASTICAN MATERIJAL SA IZOTROPNIM OJACANJEM
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
C
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
C
      COMMON/TAUD8/ TAUD(6),DEFDPR(6),DEFDS(6),DDEFP(6),
     1              DETAU(6),DDEF(6)
C
      COMMON/MAT2D/E,ANI,ET,TEQY0
C
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
C
      COMMON/PLASTI/LPLAST,LPLAS1,LSIGMA
C
      COMMON/ITERBR/ITER
C
      COMMON /CONMAT/ AE,EP,DVT
C
      DIMENSION TAUT(*),DEFT(*),DEFPT(*),TAU(*),DEF(*),TAU1(*),DEF1(*),
     1          DEFP(*)
      DIMENSION FUN(2,MATE,*),NTA(*),ETP(6,6),COEFE(2)
C
C     OSNOVNE KONSTANTE
C
      COEFE(1)=0.83333333D0
      COEFE(2)=COEFE(1)
      ISEG=0
      IPLSS=0
      NTT=NTA(MAT)
      IF(NTT.GT.1) ISEG=NTT-1
      IPL=PL
      IPL1=PL1
      DVT=2.0D0/3.0D0
C
C     INICIJALIZACIJA OSNOVNIH VELICINA
C
      ET=FUN(2,MAT,2)
      E=FUN(1,MAT,1)
      ANI=FUN(2,MAT,1)
      TEQY0=FUN(2,MAT,2)
      IF(ISEG.EQ.0)TEQY0=FUN(1,MAT,2)
      IF(IPL.EQ.1)TEQYT=TEQT
      IF(ISEG.EQ.0.AND.IPL.EQ.0)TEQYT=TEQY0
      TEQ=TEQT
      TEQY=TEQY0
       IF(KOR.EQ.1) THEN
        TEQY=TEQY0
        TEQYT=TEQY0 
       ENDIF
      IF(IPL.EQ.1) TEQY=TEQT
      DUM=0.
      CALL MEL81(FUN,COEFE,ETP,1,DUM)
      AE=(1.0D0+ANI)/E
      IF(IRAC.EQ.2) RETURN
      DO 10 I=1,6
C     TAU(I)=TAUT(I)
      TAU(I)=0.0D0
      DDEF(I)=DEF(I)-DEFT(I)
   10 CONTINUE
C
C     PROVERA ELASTICNOG RESENJA
C
      EP=E*ET/(E-ET)
C     TEQY=TEQT
C
C     ODREDIVANJE NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
C
      DO 20 I=1,6
      DO 20 J=1,6
   20 TAU(I)=TAU(I) + ETP(I,J)*(DEF(J) - DEFPT(J))
C
C     ODREDIVANJE EFEKTIVNOG NAPONA KOJI ODGOVARA ELASTICNOM RESENJU
      II=1
      CALL DEVEQ8(TAU,II,TAUEQE,TAUD)
      IF(TAUEQE.GT.TEQY) GO TO 340
      TEQT=TEQY0
      TEQ=TAUEQE
      DEFQP=0.0D0
      GO TO 100
340    PL1=1.0D0
C
C     RESENJE JE ELASTOPLASTICNO , TREBA ODREDITI PRIRASTAJ PLASTICNE
C     DEFORMACIJE IPRIRASTAJ NAPONA PRIMENOM FUNKCIJE EFEKTIVNOG NAPONA
C     PRIMENA POSTUPKA BISEKCIJE ZA
C     ODREDIVANJE NULE FUNKCUJE EFEKTIVNOG NAPONA
C     UBACITI FILE BISE
C
      IBISE=0
      IM=0
      IP=0
      TM=0.0D0
      TFM=0.0D0
      TP=0.0D0
      TFP=0.0D0
      DLAM=0.0D0
      IBISM=100
      TEQ1=TEQ
      TOLBIS=1.0D-6
      DTEQ=TAUEQE-TEQT
C
   50 IBISE=IBISE+1
C     TEQ1=TEQ
      IF(IBISE.EQ.1)TEQ=TAUEQE
      IF(IBISE.EQ.2)TEQ=TEQY
C
C     ODREDIVANJE DELTA LAMBDA
C
      CALL PRILA8(TEQ,ISEG,DLAM,TEQYT,EP,FUN,NTA,DEFQPT,MAT,MATE)
C
      CN=(1.0D0-2.D0*ANI)/(3.0D0*(1.0D0-ANI))
      CN1=1.0D0-CN
      ADL=AE+DLAM
      ADLR=1.0D0/ADL
C
      DEFDS(1)=CN1*DEF(1)-CN*DEF(2)-CN1*DEFPT(1)+CN*DEFPT(2)
      DEFDS(2)=CN1*DEF(2)-CN*DEF(1)-CN1*DEFPT(2)+CN*DEFPT(1)
C
      DEFDS(4)=DEF(4)-DEFPT(4)
      DEFDS(5)=DEF(5)-DEFPT(5)
      DEFDS(6)=DEF(6)-DEFPT(6)
C
      B1=AE+CN1*DLAM
      B2=CN*DLAM
      B1B2=B1*B1-B2*B2
C
C     KOMPONENTE DEVIJATORA NAPONA ZA RAVNO STANJE NAPONA
C
      TAUD(1)=(B1*DEFDS(1)+B2*DEFDS(2))/B1B2
      TAUD(2)=(B2*DEFDS(1)+B1*DEFDS(2))/B1B2
      TAUD(3)=-TAUD(1)-TAUD(2)
      TAUD(4)=ADLR*DEFDS(4)
      TAUD(5)=ADLR*DEFDS(5)
      TAUD(6)=ADLR*DEFDS(6)
C
C     FUNKCIJA EFEKTIVNOG NAPONA ZA RAVNO STANJE NAPONA
C
      TFQ=TAUD(1)*TAUD(1)+TAUD(2)*TAUD(2)+TAUD(3)*TAUD(3)
     1    +2.0D0*(TAUD(4)*TAUD(4)+TAUD(5)*TAUD(5)
     2    + TAUD(6)*TAUD(6))  -DVT*TEQ*TEQ
C
      TEQ1=TEQ
      CALL TEQBS8(TEQ,DTEQ,TFQ,TM,TFM,TP,TFP,IM,IP,IBISE)
      IF(IBISE.EQ.1) GO TO 50
      DTEQB=DABS(TEQ1-TEQ)
      TOLSR=DABS(TEQ+TEQ1)/2.0D0
      IF(TOLSR.LT.1.D-9) GO TO 79
      TTOL=DTEQB/TOLSR
      IF(TTOL.LT.TOLBIS)GO TO 60
   79 IF((DABS(TFP).LE.TOLBIS).AND.(DABS(TFM).LE.TOLBIS)) GO TO 60
      IF(IBISE.LE.IBISM) GO TO 50
      WRITE(6,1000)
 1000 FORMAT(' ','DOSTIGNUT MAKSIMALAN BROJ BISEKCIJA U TAUINT5')
      STOP
C
C     ODREDIVANJE KOMPONENATA DEVIJATORA NAPONA
C
   60 CONTINUE
C     ODREDIVANJE PRIRASTAJA PLASTICNE DEFORMACIJE
C
      DDEFP(1)=DLAM*TAUD(1)
      DDEFP(2)=DLAM*TAUD(2)
      DDEFP(4)=DLAM*TAUD(4)
      DDEFP(5)=DLAM*TAUD(5)
      DDEFP(6)=DLAM*TAUD(6)
      DDEFP(3)=-DDEFP(1)-DDEFP(2)
C
C     ODREDIVANJE PLASTICNE DEFORMACIJE
C
      DO 170 I=1,6
  170 DEFP(I)=DEFPT(I)+DDEFP(I)
C
C     EKVIVALENTNA PLASTICNA DEFORMACIJA
C
      DDEFQP=DDEFP(1)*DDEFP(1)+DDEFP(2)*DDEFP(2)+DDEFP(3)*DDEFP(3)+
     1 2.0D0*(DDEFP(4)*DDEFP(4)+DDEFP(5)*DDEFP(5)+DDEFP(6)*DDEFP(6))
      DDEFQP=DSQRT(DVT*DDEFQP)
      DEFQP=DEFQPT+DDEFQP
C
C     ODREDIVANJE NAPONA
C
      EMT=CN*(DEF(1)+DEF(2)-DEFP(1)-DEFP(2))
      TAUM=E/(1.0D0-2.0D0*ANI)*EMT
      TAU(1)=TAUD(1)+TAUM
      TAU(2)=TAUD(2)+TAUM
      TAU(4)=TAUD(4)
      TAU(5)=TAUD(5)
      TAU(6)=TAUD(6)
      TEQY=TEQ
      DEF(3)=-ANI/(1.0D0-ANI)*(DEF(1)+DEF(2)-DEFP(1)-DEFP(2))-
     1        DEFP(1)-DEFP(2)
C
C     KORIGOVANJE VELICINA IZ PRETHODNOG KORAKA KAD JE POSTIGNUTA
C     KONVERGENCIJA
C
  100 CONTINUE
      DO 290 I=1,6
      DEF1(I)=DEF(I)
  290 TAU1(I)=TAU(I)
      RETURN
      END
C======================================================================
      SUBROUTINE TEQBS8(X,DX,F,XM2,FM2,XP2,FP2,IM,IP,IBISE)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA RESAVANJE NELINEARNE JEDNACINE F(X)=0.
C
      COMMON /BISEK/ X1,F1,XM1,FM1,XP1,FP1,IR
      FUBRZ=1.0D0
      IF(IBISE.GT.2) GO TO 10
      IF(IBISE.GT.1) GO TO 5
      X1=X
      F1=F
    5 IF(IBISE.GT.2) GO TO 10
      IR=1
      IF(X.GT.X1.AND.DABS(F).LT.DABS(F1)) IR=2
C
   10 IF(F)20,100,30
   20 IM=IM+1
      IF(IM.GT.1) GO TO 22
      XM2=X
      FM2=F
      GO TO 50
   22 IF(F.LT.FM2) GO TO 50
      XM1=XM2
      FM1=FM2
      XM2=X
      FM2=F
C  25 X=XM1+(XM2-XM1)/(FM2-FM1)*FM1
C     GO TO 100
      GO TO 50
   30 IP=IP+1
      IF(IP.GT.1) GO TO 32
      XP2=X
      FP2=F
      GO TO 50
   32 IF(F.GT.FP2) GO TO 50
      XP1=XP2
      FP1=FP2
      XP2=X
      FP2=F
C  35 X=XP1+(XP2-XP1)/(FP2-FP1)*FP1
C     GO TO 100
   50 CONTINUE
      IF(IBISE.EQ.1) GO TO 60
      IF(IM.GT.0.AND.IP.GT.0) GO TO 80
      IF(IP.GT.0) GO TO 60
      DX=DABS(0.3D0*X)*FUBRZ
      IF(IR.EQ.1) GO TO 55
      X=X+DX
      GO TO 100
   55 X=X-DX
      GO TO 100
   60 DX=DABS(0.3D0*X)*FUBRZ
      IF(IR.EQ.1) GO TO 65
      X=X-DX
      GO TO 100
   65 X=X+DX
      GO TO 100
C
C     KORENI SU RAZDVOJENI - METOD BISEKCIJE
C
   80 IF(IM.GT.5.AND.IP.GT.5) GO TO 90
C  80 CONTINUE
      X=(XP2+XM2)/2.0D0
      DX=XP2-XM2
      GO TO 100
C
C     METOD SECICE
C
C  90 IF(IBISE.GT.10) GO TO 200
   90 CONTINUE
      X=XP2-(XM2-XP2)/(FM2-FP2)*FP2
      DX=XP2-XM2
  100 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE PRILA8(TEQ,ISEG,DLAM,TEQYT,EP,FUN,NTA,DEFQPT,
     1                  MAT,MATE)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA ODREDIVANJE PRIRASTAJA EFEKTIVNE PLASTICN
C     DEFORMACIJE I ODREDIVANJE PRIRASTAJA LAMBDA ZA DATU VREDNOST
C     EFEKTIVNOG NAPONA
C
      COMMON/MAT2D/E,ANI,ET,TEQY0
C
      DIMENSION FUN(2,MATE,*),NTA(*)
C
C     UGRADENA SAMO BILINEARNA ZAVISNOST
C
      IF(ISEG.GT.0) GO TO 50
C     BILINEARNA KRIVA
      DE=E-ET
      IF(DE.GT.0.0001)GO TO 30
      WRITE(3,100)
  100 FORMAT(' ','PERFEKTNA PLASTICNOST NIJE UGRADENA')
      STOP
   30 CONTINUE
      IF(DABS(TEQ).GT.TEQYT) GO TO 140
      DLAM=0.0D0
      GO TO 150
  140 DLAM=1.5D0*(1.0D0-TEQYT/TEQ)/EP
  150 CONTINUE
      RETURN
C
C     MULTILINEARNA KRIVA
C
   50 CONTINUE
      I=1
  60  I=I+1
      IF(I.GT.NTA(MAT)) THEN 
      DPTD=FUN(1,MAT,NTA(MAT))
      GO TO 250
      ENDIF
      TI=FUN(2,MAT,I)
      IF(TEQ.LE.TI)THEN
      DLAM=0.0D0
      GO TO 90
      ENDIF
      TI1=FUN(2,MAT,I+1)
      IF(TEQ.GT.TI.AND.TEQ.LE.TI1) GO TO 61
      GO TO 60
   61 DPI = FUN(1,MAT,I)
      DPI1= FUN(1,MAT,I+1)
      DPTD= DPI + (TEQ-TI)*(DPI1-DPI)/(TI1-TI)
C
 250  DLAM=1.5D0*(DPTD-DEFQPT)/TEQ
      IF(DLAM.LT.0.0D0) DLAM=0.0D0
C
  90  RETURN
      END
C======================================================================
      SUBROUTINE DEVEQ8(TAU,II,TEQ,TAUD)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     PODPROGRAM ZA ODREDIVANJE DEVIJATORA NAPONA EVIVALENTNOG NAPONA
C
      DIMENSION TAU(*),TAUD(*)
C
C
C     RAVNO STANJE DEFORMACIJE ILI OSNOSIMETRICAN PROBLEM
C
      TAUM=(TAU(1)+TAU(2))/3.0D0
C
      DO 15 I=1,6
      IF(I.GT.3) GO TO 16
      TAUD(I)=TAU(I)-TAUM
      GO TO 15
   16 TAUD(I)=TAU(I)
   15 CONTINUE
C
      TEQ=TAUD(1)*TAUD(1)+TAUD(2)*TAUD(2)+TAUD(3)*TAUD(3)+
     1    2.0D0*(TAUD(4)*TAUD(4)+TAUD(5)*TAUD(5)+TAUD(6)*TAUD(6))
      TEQ=DSQRT(1.5D0*TEQ)
      RETURN
      END
