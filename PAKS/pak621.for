C=======================================================================
C
C=======================================================================
      SUBROUTINE PROFIL (COR,ELM,CORK,NNKAR,NBROJ,EL,PRR,IPOMP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     CALCULATION OF CROSS SECTION DATA
C
      COMMON /PAKDFL/ IND16,IR16,IND40,IND49,IND69,IND99
      COMMON /PGREDC/ NPUN,NPRBR,MAXPRR,NPRES,ICV1,ICV2,MATG,
     1                NKARD,MG,NVR,NWRITE,NAPG,INDOF
      COMMON /COMPRF/ YC,ZC,YI,ZI,ALFB,TK,CAPAY,CAPAZ,AREA,POVR,YM,ZM,
     1                DDELT,YP,SWO(1),OM(1),DELTAP(3),TAUTK(3),OY,OZ,
     2                NT,NELM,NTIP,NKAR,IND,INDRPR(3),KR1,NEXTR,NPRPR
      COMMON/TRAN/X1,Y1,Z1,X2,Y2,Z2,DUZ,TRR(3,3),AKOR(4)
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /RAPRES/ MCOR,MELM,MCORK,MNNKAR,MNBROJ,MSTEX,MSZ,MSY,MEL,
     1                MCSI,MEXC,MSEX,MINDI,MDV,MELV,MKT,MPRRMX
      COMMON /SRPSKI/ ISRPS
C 
      DIMENSION COR(NT,*),ELM(NELM,*),CORK(NKARD,*),NNKAR(*),
     1NBROJ(NKARD,*),EL(*),PRR(*)
C
C     DEFINICIJE PROMENLJIVIH
C     NTIP - TIP PRESEKA(1-OTVORENI; 2-ZATVORENI; 3-KRSTASTI)
C     NT   BROJ TACAKA PROFILA
C     COR(N,1) - BROJ TACKE NA KONTURI-PRVA TACKA NA KRAJU KONTURE
C     COR(N,2),COR(N,3) - KOORDINATE KARAKTERISTICNIH TACAKA PROFILA
C     ENATA PARAL. SA OSOM Z, ODNOSNO Y(0.-NE UZIMA SE KOD CVORA N,
C                                       1.-UZIMA SE KOD CVORA N)
C     U PROIZVOLJNOM KOORD. SISTEMU
C     DEB - DEBLJINE SEGMENATA IZ KOJIH JE SASTAVLJEN POPRECNI PRESEK
C     ELM - MATRICA SPOJEVA I DEBLJINA
C     ELM(N,1) - TO JE BROJ SEGMENTA-N
C     ELM(N,2) - TO JE PRVA TACKA SEGMENTA N OD KOJEG JE SASTAVLJEN PROF
C     ELM(N,3) - TO JE DRUGA TACKA SEGMENTA N
C     ELM(N,4) - TO JE DEBLJINA SEGMENTA N
C     EL(I) - VEKTOR DUZINA
C     POVR - POVRSINA POPRECNOG PRESEKA
C     ZC - KOORDINATA TEZISTA
C     YC - KOORDINATA TEZISTA
C     ZIP,YIP - MOMENTI INERCIJE U ODNOSU NA TEZISNE  OSE
C     ZI,YI,CI - MOMENTI  INERCIJE U ODNOSU NA GLAVNE TEZISNE OSE
C     ALFB - UGAO ZAOKRETANJA GLAVNE OSE U ODNOSU NA OSU
C            COR(N,2)-(PRVU).
C     OM - OMEGA
C     SWOY,SWOZ - SEKTORSKI LINIJSKI STATICKI MOMENTI
C     WPM - OMEGA PRIM TACKE M
C     SM - LUCNA KOORDINATA GLAVNE NULTE TACKE M
C     YM,ZM - KOORDINATE GLAVNE NULTE TACKE M
C     AY,AZ - KOORDINATE CENTRA SMICANJA
C     SWO(I) - SEKTORSKI STATICKI MOMENT ODSECKA
C     SEX(N,1) - EKSTREMUM TAUW(0.-NEMA EKSTREMUM,
C                               1.-IMA  EKSTREMUM NA SEGMENTU N)
C     SEX(N,2) - VREDNOST S0  NA SEGMENTU N-ZA EKSTR. TAUW
C     STEX(1,J,K) - VREDNOST STATICKOG MOMENTA ZA OSU 2 SEGMENTA J
C        K=1 - EKSTR. SW0
C        K=2 - EKSTR. SY
C        K=3 - EKSTR. SZ
C     CSI(80,2)- SIRINA PRESEKA ZA ODREDJIVANJE SMICAJNIH NAPONA
C     EXC(2,20,3)- SIRINA PRESEKA ZA TACKE EKSTREMUMA
C     NKAR - BROJ KARAKTERISTICNIH TACAKA ZA RACUNANJE OMEDJENE
C     POVRSINE KOD ZATVORENOF PROFILA
C     CORK(5,3) - BROJEVI KARAKTERISTICNIH TACAKA I NJIHOVE KOORDINATE
C     NNKAR(5) - BROJ TACAKA KOJI PRIPADA POJEDINIM KARAKTERISTICNIM
C     TACKAMA
C     NBROJ(5,20) - BROJEVI TACAKA PROFILA KOJE PRIPADAJU POJEDINIM
C     KARAKTERISTICNIM TACKAMA
C     RJ - MOMENT INERCIJE KOD ZARVORENIH PROFILA
C     NVR - VRSTA TORZIJE(0-SAINT-VENANT,1-OMETANA TORZIJA)
C     TK - MOMENTI INERCIJE PRI UVIJANJU
C     KOORDINATNI SISTEM U KOME SE ZADAJU KOORDINATE
C     MORA BITI DESNI
C     UCITATI ULAZNE PODATKE ZA PROFIL
C     ODREDJIVANJE KOORDINATA TEZISTA PRESEKA
C
      PI = 1.
      PI = 4.*DATAN(PI)
C
      DO 5 I=1,NELM
      II=ELM(I,2)
      IK=ELM(I,3)
    5 EL(I)=DSQRT((COR(IK,3)-COR(II,3))**2+(COR(IK,2)-COR(II,2))**2)
      POVR=0.
      DO 6 I=1,NELM
    6 POVR=POVR+ELM(I,4)*EL(I)
      ZC=0.
      YC=0.
      DO 7 I=1,NELM
      II=ELM(I,2)
      IK=ELM(I,3)
      YC=YC+(COR(IK,3)+COR(II,3))*ELM(I,4)*EL(I)
    7 ZC=ZC+(COR(IK,2)+COR(II,2))*ELM(I,4)*EL(I)
      ZC=ZC/2./POVR
      YC=YC/2./POVR
C     ODREDJIVANJE MOMENTA INERCIJE ZA TEZISNE OSE
      ZIP=0.
      YIP=0.
      ZYIP=0.
C
      DO 8 I=1,NELM
      II=ELM(I,2)
      IK=ELM(I,3)
C
      YIP=YIP+(1./12.*(COR(II,2)-COR(IK,2))**2+(0.5*(COR(II,2)+COR(IK,2)
     1)-ZC)**2)*EL(I)*ELM(I,4)
C
      IF(DABS(COR(II,2)-COR(IK,2)).LT.1.0D-6) THEN
       YIP=YIP+EL(I)*ELM(I,4)**3/12.
      ENDIF
C
      ZIP=ZIP+(1./12.*(COR(II,3)-COR(IK,3))**2+(0.5*(COR(II,3)+COR(IK,3)
     1)-YC)**2)*EL(I)*ELM(I,4)
C
      IF(DABS(COR(II,3)-COR(IK,3)).LT.1.0D-6) THEN
       ZIP=ZIP+EL(I)*ELM(I,4)**3/12.
      ENDIF
C
    8 ZYIP=ZYIP+(-1./12.*(COR(IK,2)-COR(II,2))*(COR(II,3)-COR(IK,3))+(0.
     15*(COR(II,2)+COR(IK,2))-ZC)*(0.5*(COR(II,3)+COR(IK,3))-YC))*EL(I)*
     2ELM(I,4)
C
C     POLOZAJ GLAVNIH OSA
      IF((DABS(ZYIP).LE.0.0001).AND.(DABS(ZIP-YIP).LE.0.0001)) GO TO 9
      GO TO 10
    9 ALFB=0.
      GO TO 11
   10 IF(DABS(ZYIP).GT.0.0001) GO TO 12
      IF(ZIP-YIP)13,14,15
   15 ALFB=0.
      GO TO 11
   13 ALFB=PI/2.
      GO TO 11
   12 IF(DABS(ZIP-YIP).GT.0.0001) GO TO 160
   14 IF(ZYIP)16,16,17
   16 ALFB=PI/4.
      GO TO 11
   17  ALFB=3.*PI/4.
      GO TO 11
  160 BETA=(DABS(2.*ZYIP/DSQRT((ZIP-YIP)**2+4.*ZYIP**2)))
      BETA=DATAN(BETA/DSQRT(1.-BETA**2))
      IF((ZYIP.LT.0.).AND.(ZIP.GT.YIP)) GO TO 18
      IF((ZYIP.LT.0.).AND.(ZIP.LT.YIP)) GO TO 19
      IF((ZYIP.GT.0.).AND.(ZIP.LT.YIP)) GO TO 20
      ALFB=-0.5*BETA
      GO TO 11
   18 ALFB=0.5*BETA
      GO TO 11
   19 ALFB=0.5*(PI-BETA)
      GO TO 11
   20 ALFB=0.5*(PI+BETA)
C     GLAVNI MOMENTI INERCIJE
   11 IF (NELM.GT.1) GO TO 24
      ZI=ELM(1,4)*EL(1)*EL(1)*EL(1)/12.
      YI=EL(1)*ELM(1,4)*ELM(1,4)*ELM(1,4)/12.
      GO TO 26
   24 CONTINUE
C RADOVAN
      IF(ZYIP.LT.1.0D-5) THEN
        ZI=ZIP
        YI=YIP
       ELSE
        ZI=0.5*(ZIP+YIP+DSQRT((ZIP-YIP)**2+4.*ZYIP**2))
        YI=0.5*(ZIP+YIP-DSQRT((ZIP-YIP)**2+4.*ZYIP**2))
      ENDIF
C   RADOVAN ??? 30.01.97
        TMP=ZI
        IF(ZI.LT.YI) THEN
         ZI=YI
         YI=TMP
        ENDIF
C   ????
C RADOVAN END
   26 CI=ZI+YI
      ALFAST = ALFB*180./PI
C      KOORDINATE TEMENA U ODNOSU NA GLAVNE CENTRALNE OSE
      DO 21 I=1,NT
      YY=COR(I,3)
      COR(I,3)=(COR(I,3)-YC)*DCOS(ALFB)-(COR(I,2)-ZC)*DSIN(ALFB)
   21 COR(I,2)=(COR(I,2)-ZC)*DCOS(ALFB)+(YY-YC)*DSIN(ALFB)
C     WRITE(IZLAZ,600)
C 600 FORMAT(' ',10X,'COORDINATES OF POINTS FOR MAIN AXES '//,10X,
C    1'POINT',6X,'AXES 1',6X,'AXES 2')
C  REPERI U VEKTORU PRR
C
      NAPP  =1
      NKAR1 = 0
      IF(NKAR.EQ.2) NKAR1 = 3
      MSZ = 1
      IF(NAPP.GT.0) GO TO 120
      MCSI = MSZ
      MSEX = MSZ
      MSTEX = MSZ
      MEXC = MSZ
      MNELP = MSZ
      GO TO 122
  120 MCSI = MSZ + 7*NELM
      IF(NKAR.EQ.2) MCSI = MCSI + NELM
      MSEX = MCSI + 4*NELM
      MSTEX = MSEX + 6*NELM
      MEXC = MSTEX + 6*NELM
      MNELP = MEXC + 6*NELM
C
  122 MINDI = MNELP + NKAR1*NELM
C
      MDV = MINDI + NT
      MELV = MDV + NELM
      MKT = MELV + NELM
      MPRRMX = MKT + NT
C
      CALL TORK (ELM,EL,NNKAR,NBROJ,CORK,COR,PRR(MNELP))
      IF(NAPP.EQ.0) GO TO  125
      NNUL = MEXC-1
      DO 352 I = MSZ,NNUL
  352 PRR(I) = 0.
      NNUL = MSEX -1
      DO 216 I = MCSI,NNUL
  216 PRR(I) = 0.
      NNUL = MNELP -1
      DO 40 I = MEXC,NNUL
   40 PRR(I) = 0.
C
C     KOEFICIJENTI OBLIKA PRI SMICANJU CAPAZI CAPAY
C
  125 IF (NELM.GT.1) GO TO 126
      CAPAY=1.2
      CAPAZ=1.2
      GO TO 127
  126 CALL CAPAS(COR,ELM,EL,PRR(MELV),PRR(MDV),PRR(MKT),PRR(MEXC),
     1 PRR(MSTEX),PRR(MSEX),PRR(MINDI))
C
  127 CONTINUE
C
C...  STAMPA
C
      IF(ISRPS.EQ.0)
C     1WRITE(IZLAZ,2000)POVR,YC,ZC,YI,ZI,ALFAST,TK,CAPAY,CAPAZ
     1WRITE(IZLAZ,2000)POVR,ZC,YC,ZI,YI,ALFAST,TK,CAPAY,CAPAZ
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6000)POVR,YC,ZC,YI,ZI,ALFAST,TK,CAPAY,CAPAZ
C
C...  KOREKCIJA ZA POMEREN PROFIL (GREDA ZAJEDNO SA LJUSKOM)
C
      IF(IPOMP.EQ.1)THEN
        DZ=COR(1,3)
        DY=COR(1,2)
        YI=YI+DZ*DZ*POVR
        ZI=ZI+DY*DY*POVR
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2010)DY,DZ,YI,ZI
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6010)DY,DZ,YI,ZI
      ENDIF
C...  ZADATI MOMENTI INERCIJE
      IF(NT.LT.2) GO TO 800
      SSS=0.
      IF(NVR.EQ.0)GO TO 700
C     SEKTORSKE KOORDINATE U ODNOSU NA POL C(TEZISTE)
C     CALL SEKTOR (OM,IND,ELM,COR,EL,SWO,PRR(MSEX))
C
  700 IF(NAPP.EQ.0) GO TO 800
C
C     RACUNANJE VELICINA POTREBNIH ZA ODREDJIVANJE NAPONA
C
C     STATICKI MOMENTI ODSECAKA ZA TACKE EKSREMUMA SMICUCIH NAPONA
C     NA SEGMENTIMA
C
      CALL STATOT (ELM,COR,PRR(MSEX),EL,PRR(MEXC),PRR(MSTEX),0,2,0,SSS,
     1 AKSI)
C
C
C  STATICKI MOMENTI U CVORNIM TACKAMA POJEDINIH SEGMENATA
C
      CALL STASEG(ELM,COR,CORK,PRR(MSEX),PRR(MEXC),PRR(MSTEX),EL,
     1 PRR(MNELP),PRR(MSZ),PRR(MCSI))
C
C
C  STAMPANJE REZULTATA O PRESEKU (STAT. MOM. ZA TACKE I TACKE EKSTREMUMA
C  I SIRINE PRESEKA)
C
C     STOP 'PRE PRINPR'
      IF(NPRPR.GT.0) CALL PRINPR(ELM,PRR(MSZ),PRR(MSTEX),PRR(MCSI),
     1 PRR(MEXC),PRR(MSEX),NT,NELM,NEXTR)
C
C
  800 NWRITE = 1
      IND16 = 1
      AKOR(1)=COR(1,2)
      AKOR(2)=COR(1,3)
      AKOR(3)=COR(2,2)
      AKOR(4)=COR(2,3)
C
      RETURN
 2000 FORMAT(//6X,'K A R A K T E R I S T I K E    P R E S E K A'//
     111X,'POVRSINA POPRECNOG PRESEKA ................  POVR =',1PD12.5/
     211X,'KOORDINATE TEZISTA         ................  YC   =',1PD12.5/
     254X,                                           '  ZC   =',1PD12.5/
     311X,'GLAVNI MOMENTI INERCIJE    ................  J(1) =',1PD12.5/
     354X,                                           '  J(2) =',1PD12.5/
     411X,'UGAO IZMEDJU INICIJALNIH I GLAVNIH OSA ....  ALFA =',1PD12.5/
     511X,'TORZIONA KONSTANTA ........................    TK =',1PD12.5/
     211X,'SMICAJNI KOEFICIJENTI .....................  CAPAY=',1PD12.5/
     254X,                                           '  CAPAZ=',1PD12.5)
 2010 FORMAT(//6X,'P O M E R E N O S T   P R E S E K A'//
     211X,'POMERANJE GLAVNIH OSA .....................  D1   =',1PD12.5/
     254X,                                           '  D2   =',1PD12.5/
     311X,'MOMENTI INERCIJE  .........................  J<1> =',1PD12.5/
     354X,                                           '  J<2> =',1PD12.5)
 6000 FORMAT(//6X,'C R O S S   S E C T I O N   P R O P E R I T Y'//
     111X,'CROSS SECTION AREA ........................  POVR =',1PD12.5/
     211X,'CENTER OF GRAVITY  ........................  YC   =',1PD12.5/
     254X,                                           '  ZC   =',1PD12.5/
     311X,'MAIN MOMENTS OF INERTIA ...................  J(1) =',1PD12.5/
     354X,                                           '  J(2) =',1PD12.5/
     411X,'ANGLE BETWEEN INITIAL AND MAIN AXES .......  ALFA =',1PD12.5/
     511X,'TORSION CONSTANTS .........................    TK =',1PD12.5/
     211X,'SHEAR COEFICIJENTS ........................  CAPAY=',1PD12.5/
     254X,                                           '  CAPAZ=',1PD12.5)
 6010 FORMAT(//6X,'D I S P L A C E D   C R O S S   S E C T I O N'//
     211X,'DISPLACEMENT OF AXES ......................  D1   =',1PD12.5/
     254X,                                           '  D2   =',1PD12.5/
     311X,'MOMENTS OF INERTIA ........................  J<1> =',1PD12.5/
     354X,                                           '  J<2> =',1PD12.5)
      END
C=======================================================================
      SUBROUTINE TORK (ELM,EL,NNKAR,NBROJ,CORK,COR,NELP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     OVIM PODPROGRAMOM SE ODREDJUJE TORZIONA KONSTANTA
C     ZA TANKOZIDNE OTVORENE I ZATVORENE JEDNOSTRUKO I
C     DVOSTRUKO POVEZANE PRESEKE
C
      COMMON /PGREDC/ NPUN,NPRBR,MAXPRR,NPRES,ICV1,ICV2,MATG,
     1                NKARD,MG,NVR,NWRITE,NAPG,INDOF
      COMMON /COMPRF/ YC,ZC,YI,ZI,ALFB,TK,CAPAY,CAPAZ,AREA,POVR,YM,ZM,
     1                DDELT,YP,SWO(1),OM(1),DELTAP(3),TAUTK(3),OY,OZ,
     2                NT,NELM,NTIP,NKAR,IND,INDRPR(3),KR1,NEXTR,NPRPR
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION ELM(NELM,*),EL(*),NNKAR(*),NBROJ(NKARD,*),
     1CORK(NKARD,*),COR(NT,*),NELP(3,*)
      DIMENSION DELT(3),INDR(3)
C
C     DSIN(X)=DDSIN(X)
C     CBS(X)=DDABS(X)
C     DCOS(X)=DDCOS(X)
C
      GO TO (10,30),NTIP
C
C     OTVORENI PROFIL
C
   10 TK=0.
      DO 11 I=1,NELM
   11 TK=TK+EL(I)*ELM(I,4)*ELM(I,4)*ELM(I,4)
      TK=1.15*TK/3.
      RETURN
C
C     ZATVORENI PROFIL
   30 NPRIV=NELM
C
      IF(NKAR.LE.2) GO TO 40
      NKARZ = 2
      WRITE(IZLAZ,2001)NKAR,NKARZ
 2001 FORMAT(/2X,'BROJ VISESTRUKOST POVEZANOSTI PRESEKA VECI OD DOZVOLJE
     1NOG U PROGRAMU'/2X,'BROJ ZADATE VISESTRUKOSTI =',I5/2X,'MAKSIMALNO
     2 DOPUSTEN U PROGRAMU  P A K  =',I5)
      STOP
C
C     KOORDINATE KARAKTER. TACAKA U ODNOSU NA TEZISTE
C
   40 DO 303 I=1,NKAR
      YY=CORK(I,2)
      CORK(I,2)=(CORK(I,2)-ZC)*DCOS(ALFB)+(CORK(I,3)-YC)*DSIN(ALFB)
  303 CORK(I,3)=(CORK(I,3)-YC)*DCOS(ALFB)-(YY-ZC)*DSIN(ALFB)
C
C
      DO 304 N=1,NKAR
      AREA=0.
      I1=NNKAR(N)
      DO 305 I=1,I1
      NSEG = NBROJ(N,I)
      II = ELM(NSEG,2)
      IK = ELM(NSEG,3)
      AREA=AREA+DABS(COR(II,2)*COR(IK,3)-COR(II,3)*COR(IK,2)+CORK(N,3)*
     1COR(IK,2)-CORK(N,2)*COR(IK,3)+CORK(N,2)*COR(II,3)-CORK(N,3)*COR(II
     2,2))
  305 CONTINUE
      AREA2 = AREA/2.
      IF(NKAR.EQ.1) WRITE(IZLAZ,2012) AREA2
      IF(NKAR.EQ.2) WRITE(IZLAZ,508) N,AREA2
  508 FORMAT(//11X,'OMEDJENA POVRDSINA PRESEKA POVRDSINE BR.',I2,6X,'=',
     1 1PD12.4)
      IF(NKAR.EQ.1) GO TO 304
      IF(N.EQ.1) AREA1 = AREA
      IF(N.EQ.2) AREA2 = AREA
  304 CONTINUE
C
      TK=0.
C
C  TORZIONA KONSTANTA
C
      IF(NKAR.EQ.2) GO TO 50
C
C  JEDNOSTRUKO POVEZAN PRESEK
C
      DO 310 I=1,NELM
  310 TK=TK+EL(I)/ELM(I,4)
      TK=AREA*AREA/TK
C
      GO TO 500
C
C  DVOSTRUKO POVEZAN PRESEK
C
   50 AREA = AREA1 + AREA2
      AREAS = AREA/2.
      WRITE(IZLAZ,2012) AREAS
 2012 FORMAT(//11X,'UKUPNA OMEDJENA POVRDSINA',29X,' =',1PD14.5)
      NEL1 = 0
      NEL2 = 0
      NEL3 = 0
C
      DO 49 I =1,3
   49 INDRPR(I) = 0
      KR1 = 0
      KR2 = 0
      IK1 = NNKAR(1)
      IK2 = NNKAR(2)
C
C  PREBROJAVANJE I GRUPISANJE SEGMENATA PO GRUPAMA
C
      DO 60 N = 1,NELM
      IND1 = 0
      IND2 = 0
      DO 51 J = 1,IK1
      IF(NBROJ(1,J).EQ.N) GO TO 52
   51 CONTINUE
      GO TO 53
   52 IND1 = 1
   53 DO 54 J = 1,IK2
      IF(NBROJ(2,J).EQ.N) GO TO 55
   54 CONTINUE
      GO TO 56
   55 IND2 = 1
C
   56 IF(IND1.EQ.0) GO TO 58
      IF(IND2.EQ.0) GO TO 57
      NEL3 = NEL3 + 1
      NELP(3,NEL3) = N
      GO TO 60
   57 NEL1 = NEL1 + 1
      NELP(1,NEL1) = N
      GO TO 60
   58 NEL2 = NEL2 + 1
      NELP(2,NEL2) = N
C
   60 CONTINUE
C
      INDRPR(1) = NEL1
      INDRPR(2) = NEL2
      INDRPR(3) = NEL3
C
C  ODREDJIVANJE TACKI RACVANJA KR1 I KR2
C
      DO 80 N = 1,NT
      DO 72 I = 1,3
      INDR(I) = 0
      NN = INDRPR(I)
      DO 71 J = 1,NN
      IEL = NELP(I,J)
      I1 = ELM(IEL,2)
      I2 = ELM(IEL,3)
      IF(N.EQ.I1. OR. N.EQ.I2) GO TO 73
   71 CONTINUE
      GO TO 72
   73 INDR(I) = 1
      IF(KR1.GT.0.AND.KR2.GT.0) GO TO 72
      DELT(I) = ELM(IEL,4)
   72 CONTINUE
C
      IF(INDR(1).EQ.1.AND.INDR(2).EQ.1.AND.INDR(3).EQ.1) GO TO 74
      GO TO 80
   74 IF(KR1.GT.0) GO TO 75
      KR1 = N
      DO 76 I = 1,3
   76 DELTAP(I) = DELT(I)
      GO TO 80
   75 KR2 = N
      GO TO 81
C
   80 CONTINUE
C
C  ODREDJIVANJE KONSTANTI ZA TORZIONU KONSTANTU
C
   81 DO 90 I=1,3
      TAUTK(I) = 0.
      NN = INDRPR(I)
      DO 82 J=1,NN
      I1 = NELP(I,J)
   82 TAUTK(I) = TAUTK(I) + EL(I1)/ELM(I1,4)
      TAUTK(I) = DELTAP(I)*TAUTK(I)
   90 CONTINUE
C
      AK1 = (AREA*DELTAP(2) + AREA1*DELTAP(3)*TAUTK(2)/TAUTK(3))/
     1      (TAUTK(1)*DELTAP(2) + TAUTK(2)*(DELTAP(1) + DELTAP(3) *
     2      TAUTK(1)/TAUTK(3)))
C
      AK2 = (AREA - TAUTK(1)*AK1)/TAUTK(2)
C
      TK = AREA1*DELTAP(1)*AK1 + AREA2*DELTAP(2)*AK2
C
C
C
      TAUTK(3) = (AREA1 - TAUTK(1)*AK1)/(TAUTK(3)*TK)
      TAUTK(1) = AK1/TK
      TAUTK(2) = AK2/TK
C
C
  500 NELM=NPRIV
C
      RETURN
C
      END
      SUBROUTINE CAPAS (COR,ELM,EL,ELV,DV,KT,EXC,STEX,SEX,
     1 INDI)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     OVAJ PODPROGRAM SLUZI ZA ODREDJIVANJE KOEF.OBLIKA  PRI SMICANJU
C
C     DABS(X)=DDABS(X)
      COMMON /COMPRF/ YC,ZC,YI,ZI,ALFB,TK,CAPAY,CAPAZ,AREA,POVR,YM,ZM,
     1                DDELT,YP,SWO(1),OM(1),DELTAP(3),TAUTK(3),OY,OZ,
     2                NT,NELM,NTIP,NKAR,IND,INDRPR(3),KR1,NEXTR,NPRPR
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION COR(NT,*),ELM(NELM,*),EL(*),ELV(*),DV(*),
     1 KT(*),EXC(2,NELM,*),STEX(2,NELM,*),
     2 SEX(NELM,*),INDI(*)
C
      CP(AJ,Y2,Y22,Y1,Y11,AKSI)=(AJ*AJ*(Y2-Y1)-AJ*AKSI*(Y2*Y22-Y1*Y11)
     1/3. + AKSI*AKSI*(Y2*Y22*Y22-Y1*Y11*Y11)/20.)/AKSI
C
      YNUL = 1.D-10
C
C   DEFINISANJE TACAKA KOJE UCESTVUJU U SEGMENTIMA
      IN1=1
      IN2=1
C   - 0 - ZNACI CVOROVI UCESTVUJU U DEFINIS. SEGMENATA
      DO 20 I=1,NELM
      II=ELM(I,2)
      IK=ELM(I,3)
      IF (II.EQ.1) IN1=0
      IF (IK.EQ.1) IN1=0
      IF (II.EQ.2) IN2=0
      IF (IK.EQ.2) IN2=0
   20 CONTINUE
      DO 1000 L=2,3
      SS1=0.
      SS2=0.
      IPRVI=0
      YMIN=COR(1,L)
      IMIN=1
      IF (IN1.EQ.0) GO TO 21
      YMIN=COR(2,L)
      IMIN=2
      IF (IN2.EQ.0) GO TO 21
      YMIN=COR(3,L)
      IMIN=3
   21 DO 355 I=1,NT
  355 INDI(I)=0
      DO 353 I=2,NT
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO 353
      IF(COR(I,L)-YMIN) 354,354,353
  354 YMIN=COR(I,L)
      IMIN=I
  353 CONTINUE
      INDI(IMIN)=1
      Y1=YMIN
      CAPA=0.
C     INICIJALNO YMIN
  373 KKON = IN1 + IN2
      DO 360 I=1,NT
      IF (I.GT.2) GO TO 22
      IF (I.EQ.1.AND.IN1.EQ.1) GO TO 360
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO 360
   22 IF(INDI(I).EQ.1)GO TO 364
      YMIN=COR(I,L)
      GO TO 360
  364 KKON=KKON+1
      IF(KKON.EQ.NT) GO TO 3
  360 CONTINUE
C     STVARNO YMIN
      DO 357 I=1,NT
      IF (I.GT.2) GO TO 23
      IF (I.EQ.1.AND.IN1.EQ.1) GO TO 357
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO 357
   23 IF(INDI(I).EQ.1)GO TO 357
      IF(COR(I,L).GT.YMIN) GO TO 357
      YMIN=COR(I,L)
      IMIN=I
  357 CONTINUE
      INDI(IMIN)=1
      Y2=YMIN
      KB=0
      DO 393 I=1,NT
      IF (I.GT.2) GO TO 24
      IF (I.EQ.1.AND.IN1.EQ.1) GO TO 393
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO 393
   24 IF(KB.GT.0) GO TO 393
      IF(DABS(COR(I,L)-Y2).GT.DDELT) GO TO 393
      DO 394 K=1,NELM
      II=ELM(K,2)
      IK=ELM(K,3)
      IF((DABS(COR(II,L)-Y2).GT.DDELT).OR.(DABS(COR(IK,L)-Y2).GT.DDELT))
     1GO TO 394
      KB=KB+1
 394  CONTINUE
      IF(KB.GT.0) GO TO 393
      INDI(I)=1
  393 CONTINUE
      IPRVI=IPRVI+1
      IF((KB.EQ.0).AND.(IPRVI.EQ.1)) GO TO 373
C
C     PETLJA  PO SEGMENTIMA ZA ODREDJIVANJE KOEF.OBLIKA
C
      SZ1=0.
      AJ1=0.
      NIS=0
      AJ2=0.
      AKSI=0.
      DELK=0.
      ELEM=0.
      DELTA=0.
      DELK1=0.
C
      IF(DABS(Y1).LT.YNUL) Y1 = 0.
      IF(DABS(Y2).LT.YNUL) Y2 = 0.
C
      DO 380 I=1,NELM
      II=ELM(I,2)
      IK=ELM(I,3)
      IF(DABS(Y2-Y1).GE.DDELT) GO TO 352
      IF((DABS(COR(II,L)-Y2).LE.DDELT).AND.(DABS(COR(IK,L)-Y2).LE.DDELT)
     1)GO TO 390
      IF((DABS(COR(II,L)-Y2).LT.DDELT).AND.(COR(IK,L).LT.Y2)) GO TO 380
      IF((DABS(COR(IK,L)-Y2).LT.DDELT).AND.(COR(II,L).LT.Y2)) GO TO 380
  352 IF((COR(II,L).GT.Y2).AND.(COR(IK,L).GT.Y2))GO TO 371
      IF((DABS(COR(II,L)-Y2).LE.DDELT).AND.(COR(IK,L).GT.Y2))GO TO 371
      IF((DABS(COR(IK,L)-Y2).LE.DDELT).AND.(COR(II,L).GT.Y2))GO TO 371
      IF((DABS(COR(II,L)-Y2).LE.DDELT).AND.(DABS(COR(IK,L)-Y2).LE.DDELT) 
     1)GO TO 371
      IF((COR(II,L).GT.Y2).AND.(COR(IK,L).LT.Y2))GO TO 372
      IF((COR(IK,L).GT.Y2).AND.(COR(II,L).LT.Y2))GO TO 372
      IF((DABS(COR(II,L)-Y2).LE.DDELT).AND.(COR(IK,L).LT.Y2))GO TO 372
      IF((DABS(COR(IK,L)-Y2).LE.DDELT).AND.(COR(II,L).LT.Y2))GO TO 372
      GO TO 380
C
C     SEGMENT JE ISPOD Y1
C
  371 SZ1=SZ1+0.5*ELM(I,4)*EL(I)*(COR(II,L)+COR(IK,L))
      GO TO 380
C
C     SEGMENT JE PRESECEN SA Y=CONST.
C
C 372 AKSI=AKSI+ELM(I,4)*EL(I)/DABS(COR(IK,L)-COR(II,L))
C
  372 AKSI=AKSI+ELM(I,4)
      IF (COR(II,L)-COR(IK,L))374,374,375
  374 YK1=COR(IK,L)
      GO TO 376
  375 YK1=COR(II,L)
  376 SZ1=SZ1+0.5*ELM(I,4)*EL(I)*(YK1*YK1-Y2*Y2)/DABS(COR(IK,L)
     1 - COR(II,L))
      GO TO 380
C
C     INTEGRALI SE PO DEBLJINI SEGMENTA
C
  390 NIS=NIS+1
      DV(NIS)=ELM(I,4)
      ELV(NIS)=EL(I)
  380 CONTINUE
      IF(DABS(Y2-Y1).GT.DDELT)  GO TO 392
C
C     REDJANJE DEBLJINA PO VELICINI
      IF(NIS.EQ.1) GO TO 40
      NS1=NIS-1
      DO 41 I=1,NS1
      JJ=I
      DD=DV(I)
      J1=I+1
      DO 42 J=J1,NIS
      IF (DV(J)-DD)43,42,42
   43 DD=DV(J)
      JJ=J
   42 CONTINUE
      DDD=DV(I)
      DDL=ELV(I)
      ELV(I)=ELV(JJ)
      ELV(JJ)=DDL
      DV(I)=DD
      DV(JJ)=DDD
   41 CONTINUE
C
C     FORMIRANJE GRUPA ISTIH DEBLJINA
   40 DDD=0.
      NGR=0
      DD=0.
      DO 44 I=1,NIS
      IF(DABS(DV(I)-DD).LT.DDELT) GO TO 45
      DDD=ELV(I)
      NGR=NGR+1
      DV(NGR)=DV(I)
      DD=DV(I)
      ELV(NGR)=ELV(I)
      GO TO 44
   45 ELV(NGR)=ELV(I)+DDD
   44 CONTINUE
C
C     INTEGRALJENJE PO DEBLJINAMA SEGMENTA
      NIS=2*NGR-1
      DO 46 I=1,NIS
      IF (I-NGR) 47,48,49
C
C     INTEGRALJENJE ISPOD NAJMANJE DEBLJINE
   47 ELEM=ELEM+ELV(NGR-I+1)
      Y1D=Y1+0.5*DV(NGR-I+1)
      Y1D1=Y1+0.5*DV(NGR-I)
      Y22 = Y1D*Y1D
      Y11 = Y1D1*Y1D1
      AJ=SZ1+ELEM*Y22/2.
      CAPA=CAPA+CP(AJ,Y1D,Y22,Y1D1,Y11,ELEM)
      SZ1=SZ1+0.25*ELEM*(DV(NGR-I+1)-DV(NGR-I))*(2.*Y1+0.5*(DV(NGR-I+1)
     1+DV(NGR-I)))
      GO TO 46
C
C     SREDNJI SEGMENT
   48 ELEM=0.
      DO 50 J=1,NGR
   50 ELEM=ELEM+ELV(J)
      Y1D=Y1-0.5*DV(1)
      Y1D1=Y1+0.5*DV(1)
      Y22 = Y1D1*Y1D1
      Y11 = Y1D*Y1D
      AJ=SZ1+ELEM*Y22/2.
      CAPA=CAPA+CP(AJ,Y1D1,Y22,Y1D,Y11,ELEM)
C***
      SZ1=SZ1+DV(1)*ELEM*Y1
      GO TO 46
C
C     INTEGRALJENJE IZNAD SREDNJEG SEGMENTA
C
   49 Y1D=Y1-0.5*DV(I-NGR)
      Y1D1=Y1-0.5*DV(I-NGR+1)
      ELEM=ELEM-ELV(I-NGR)
      Y22 = Y1D*Y1D
      Y11 = Y1D1*Y1D1
      AJ=SZ1+ELEM*Y22/2.
      CAPA=CAPA+CP(AJ,Y1D,Y22,Y1D1,Y11,ELEM)
C***
      SZ1=SZ1+0.5*ELEM*(DV(I-NGR+1)-DV(I-NGR))*(2.*Y1-0.5*(DV(I-NGR+1)
     1+DV(I-NGR)))
C**
C
   46 CONTINUE
      DO 10 I=1,NT
      IF (I.GT.2) GO TO 25
      IF (I.EQ.1.AND.IN1.EQ.1) GO TO  10
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO  10
   25 IF(INDI(I).EQ.1)GOTO 10
      IF((DABS(COR(I,L)-Y2).GT.DDELT)) GO TO 10
      INDI(I)=1
   10 CONTINUE
      GO TO 373
  392 Y22 = Y2*Y2
      AJ=SZ1+AKSI*Y22/2.
      Y11 = Y1*Y1
      CAPA=CAPA+CP(AJ,Y2,Y22,Y1,Y11,AKSI)
C***
C     SEGMENTI PARALELNI SA OSOM
C
   3  KMAX=0
      DO 1 I=1,NT
      IF (I.GT.2) GO TO 26
      IF (I.EQ.1.AND.IN1.EQ.1) GO TO  1
      IF (I.EQ.2.AND.IN2.EQ.1) GO TO  1
   26 IF((DABS(COR(I,L)-Y1).GT.DDELT)) GO TO 1
      KMAX=KMAX+1
      KT(KMAX)=I
    1 CONTINUE
C
      DO 17 I=1,NELM
   17 KT(I)=0
      AKSI1=AKSI
      IF(KKON.EQ.NT) GO TO 370
      Y1=Y2
      GO TO 373
  370 IF(L.EQ.3) GO TO 1001
      CAPAZ=POVR*CAPA/(YI*YI)
      GO TO 1000
 1001 CAPAY=POVR*CAPA/(ZI*ZI)
 1000 CONTINUE
      RETURN
      END
