C=======================================================================
C
C   SUBROUTINE UL14EG
C              UL1EK
C              UL1EK2
C              LIMMH1
C              TGRAF1
C
C=======================================================================
      SUBROUTINE UL14EG
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA ULAZNIE PODATAKE STAPOVA
CE     MAIN PROGRAM FOR INPUT DATA ABOUT 1/D ELEMENTS
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
C
CS     REPERI U VEKTORU ZA ULAZNE PODATKE
CE     POINTERS IN INPUT VECTOR
C
      NCVE=2
      LAPRS=1
      LTBTH=LAPRS+3*NE*IDVA
      LTDTH =LTBTH
      IF(INDBTH.EQ.1) LTDTH=LTBTH+NE*IDVA
      LNMAT =LTDTH
      IF(INDDTH.EQ.1) LNMAT=LTDTH+NE*IDVA
      LIPGC =LNMAT+NE
      LISNA =LIPGC+NE
      LIPRC =LISNA+NE
      LNEL  =LIPRC+NE
      LCEL=LNEL
      IF(ICVEL.EQ.1) LNEL=LCEL+NE
      LAU=LMAX
C
CS     POZIVANJE PROGRAMA ZA ULAZNE PODATKE U VEKTOR AU
CE     CALL ROUTINES FOR INPUT DATA IN VECTOR  AU
C
      CALL UL14EK(A(LAU))
      NCVE3=NCVE*3
      MXAE = NCVE3+(NCVE3+NCVE3*(NCVE3+1)/2)*IDVA
      LMAX = LMAX + MXAE
C
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE UL14EK(AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
CE     MENAGEMENT PROGRAM FOR INPUT DATA IN  AU  VECTOR
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /POSTPR/ LNDTPR,LNDTGR,NBLPR,NBLGR,INDPR,INDGR
      COMMON /RESTAR/ TSTART,IREST
      COMMON /MATERM/ LMODEL,LGUSM
      COMMON /GAPELE/ GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION AU(*)
      REAL AU
C
CS     POZIVANJE PROGRAMA ZA ULAZNE PODATKE O STAPOVIMA
CE     CALL ROUTINE FOR INPUT DATA ABOUT TRUSSES
C
      LL=NE*NCVE
      CALL ICLEAR(AU(LNEL),LL)
      NMA=0
      NMI=0
      CALL UL14EK2(AU(LNEL),AU(LNMAT),AU(LISNA),AU(LAPRS),
     1             AU(LTBTH),AU(LTDTH),AU(LCEL),NMA,NMI,ICVEL,AU(LIPGC))
      IF(NBLGR.GE.0) 
     +          CALL TGRAF1(AU(LNEL),AU(LCEL),ICVEL,AU(LNMAT),AU(LAPRS))
      IF(NBLGR.GE.0) 
     +       CALL TGRAU1(AU(LNEL),AU(LCEL),ICVEL,AU(LNMAT),AU(LAPRS),49)
      IF(ICVEL.EQ.1) CALL VEZACE(AU(LNEL),A(LELCV),NE,NCVE)
C
      NCVE3=NCVE*3
      LLMEL=LNEL+NE*NCVE
      LELC=LLMEL
      IF(ICVEL.EQ.1) THEN
        LLMEL=LELC+NMA-NMI+1
        CALL ICLEAR(AU(LELC),NMA-NMI+1)
        CALL VEZAEL(AU(LCEL),AU(LELC),NE,NMI)
      ENDIF
      LMXAU=LLMEL+NE*NCVE3
      MXAU = LMXAU - 1
      CALL DELJIV(MXAU,2,INDL)
      IF(INDL.EQ.1) MXAU=MXAU+1
      LMAX=LAU+MXAU
      IF (LMAX.LE.MTOT) GO TO 5
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2005) LMAX,MTOT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6005) LMAX,MTOT
      STOP
C
CS     FORMIRANJE VEKTORA LM
CE     FORM  LM  VECTOR
C
    5 LL=NE*NCVE3
      CALL ICLEAR(AU(LLMEL),LL)
      CALL LMIMH1(A(LID),AU(LNEL),AU(LLMEL),NCVE3)
C
      LMAX8=LMAX8+1
      WRITE(IELEM,REC=LMAX8)
     1 NCVE,ITERME,MXAU,LNEL,LNMAT,LAPRS,LISNA,LLMEL,LCEL,LELC,NMA,NMI,
     1 GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR,LIPGC,LIPRC,
     1 INDBTH,INDDTH,LTBTH,LTDTH
      CALL WRITDD(AU(LAPRS),MXAU/IDVA,IELEM,LMAX8,LDUZI)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C      
      GO TO 10
C      IF(IATYP.EQ.0) GO TO 10
      IF(NMODM.LE.4.OR.NMODM.EQ.11.OR.NMODM.EQ.12) GO TO 10
CS....  PLASTICNOST
CS....  PROSTOR ZA VELICINE PRETHODNOG KORAKA
CE     PLASTICITY
      LPLAST=LMAX
      NPROS=NE*MODPR1( NMODM )*IDVA
      LPLAS1=LPLAST+NPROS
      CALL DELJIV(LPLAS1,2,INDL)
      IF(INDL.EQ.0) LPLAS1=LPLAS1+1
CS..... PROSTOR ZA VELICINE TEKUCEG KORAKA
      NPROS1 = NPROS
      LMAX=LPLAS1+NPROS1
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      IF(LMAX.GT.MTOT) CALL ERROR(1)
CS....  INICIJALIZACIJA
      IF(IREST.NE.1) THEN
         CALL INCPL1(AU(LNMAT),NE,NMODM,LMODEL)
         CALL WRITDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
         CALL WRITDD(A(LPLAS1),NPROS1/IDVA,IELEM,LMAX8,LDUZI)
      ELSE
         CALL READDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
         CALL READDD(A(LPLAS1),NPROS1/IDVA,IELEM,LMAX8,LDUZI)
      ENDIF
      RETURN
C
   10 LSIGMA=LMAX
      NPROS=6*NE*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      IF(IREST.NE.1) THEN
         CALL CLEAR(A(LSIGMA),NPROS/IDVA)
         CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      ELSE
         CALL READDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      ENDIF
      RETURN
C-----------------------------------------------------------------------
 2005 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A ZA PODATKE O STAPOVIM
     1A'/' POTREBNA DIMENZIJA , LMAX=',I10/' RASPOLOZIVA DIMENZIJA,
     2NTOT =',I10)
 6005 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE UL14EK2(NEL,MATV,ISNAP,APRS,
     1                   TBTH,TDTH,MCVEL,NMA,NMI,ICVEL,IPGC)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     PODPROGRAM ZA UCITAVANJE PODATAKA O STAPOVIMA
CE     SUBROUTINE FOR READING DATA ABOUT TRUSSES
C
      CHARACTER*250 ACOZ
      include 'paka.inc'
      
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /GAPELE/ GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION NEL(NE,*),MATV(*),ISNAP(*),APRS(3,*),
     1          TBTH(*),TDTH(*),MCVEL(*),APR(3),IPGC(*)
C
CS     P  O  D  A  C  I     O     O  S  O  B  I  N  A  M  A
CE     P  R  O  P  E  R  T  Y                                  
C
      IF(ISRPS.EQ.0.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,2000)
      IF(ISRPS.EQ.1.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,6000)
      IF(ISRPS.EQ.0.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,2500)
      IF(ISRPS.EQ.1.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,6500)
      CALL ISPITA(ACOZ)
      IF(INDFOR.EQ.1)
     1READ(IULAZ,*) GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR
      IF(INDFOR.EQ.2)
     1READ(ACOZ,1020) GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3)
     1WRITE(IZLAZ,5020) GAP,AKN,AKNZ,AKS,AMY,AMZ,PFOR
C
CS     P  O  D  A  C  I      O      E  L  E  M  E  N  T  I  M  A
CE     D  A  T  A      A  B  O  U  T      E  L  E  M  E  N  T  S
C
      IF(ISRPS.EQ.0.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,2600)
      IF(ISRPS.EQ.1.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,6600)
      NMATS=  1
      ISNAA=  0
      IPGCC=  0
      APRSS=  0.D0
      I = 0
      NAUT=0
    5 I = I + 1
      CALL ISPITA(ACOZ)
      IF(I.EQ.1) KARTI=KARTIC
      IF(INDFOR.EQ.1)
     1READ(IULAZ,*) NN,NMAT,ISNA,IPG,APR,KORC,BTH,DTH
      IF(INDFOR.EQ.2.and.ind56.eq.0)
     1READ(ACOZ,1010) NN,NMAT,ISNA,IPG,APR,KORC,BTH,DTH
      IF(INDFOR.EQ.2.and.ind56.eq.1)
     1READ(ACOZ,4010) NN,NMAT,ISNA,IPG,APR,KORC,BTH,DTH
      IF(ICVEL.EQ.1) THEN
         MCVEL(I)=NN
         NI=NN
         IF(I.EQ.1) THEN
            NMA=NN
            NMI=NN
         ELSE
            IF(NMA.LT.NN) NMA=NN
            IF(NMI.GT.NN) NMI=NN
         ENDIF
         NN=I
      ENDIF
      CALL ISPITA(ACOZ)
      IF(INDFOR.EQ.1)
     1READ(IULAZ,*) (NEL(NN,J),J=1,2)
      IF(INDFOR.EQ.2.and.ind56.eq.0)
     1READ(ACOZ,1000) (NEL(NN,J),J=1,2)
      IF(INDFOR.EQ.2.and.ind56.eq.1)
     1READ(ACOZ,4000) (NEL(NN,J),J=1,2)
      IF(NMAT.EQ.0) NMAT=NMATS
      NMATS=NMAT
      MATV(NN)= NMAT
      IF(ISNA.EQ.0) ISNA=ISNAA
      ISNAA=ISNA
      IF(ISNA.LT.0) ISNA = 0
      ISNAP(NN)=ISNA
      IF(IPG.EQ.0) IPG=IPGCC
      IPGCC=IPG
      IF(IPG.LT.0) IPG = 0
      IPGC(NN)=IPG
      DO 19 J=1,3
         APRS(J,NN)=APR(J)
   19 CONTINUE
      IF(INDBTH.EQ.1) TBTH(NN)=BTH
      IF(INDDTH.EQ.1) TDTH(NN)=DTH
      IF(ICVEL.EQ.1) IN=MCVEL(NN)
      IF(ICVEL.EQ.0) IN=NN
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3)
     1WRITE(IZLAZ,5001) IN,(NEL(NN,J),J=1,2),NMAT,ISNA,IPG,
     1                     (APRS(J,NN),J=1,3),KORC
         IF(INDBTH.EQ.1) THEN
         IF(DABS(TBTH(NN)).GT.1.0D-10) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2040) TBTH(NN)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6040) TBTH(NN)
         ENDIF
         ENDIF
         IF(INDDTH.EQ.1) THEN 
         IF(DABS(TDTH(NN)).GT.1.0D-10) THEN 
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2050) TDTH(NN)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6050) TDTH(NN)
      ENDIF
      ENDIF
      IF(NAUT.GT.0) GO TO 30
      IF(KORC.NE.0) GO TO 20
      IF(I.EQ.NE) GO TO 50
      GO TO 5
C
   20 NAUT=1
      IF(ICVEL.EQ.1) N1=NI
      IF(ICVEL.EQ.0) NN1=NN
      KORA=KORC
      GO TO 5
C
CS     AUTOMATSKO GENERISANJE PODATAKA IZMEDJU CVOROVA N1 I N2
CE     AUTOMATIC  GENERATION BETWEN NODES  N1  AND  N2
C
   30 NN2=NN
      IF(ICVEL.EQ.1) THEN
         NN1=NN-1
         N2=NI
      ENDIF
      N11=NEL(NN1,1)
      N22=NEL(NN2,1)
      N12=N22-N11
      CALL DELJIV(N12,KORA,INDD)
      IF(INDD.EQ.1) GO TO 100
      N11=NEL(NN1,2)
      N22=NEL(NN2,2)
      N21=N22-N11
      IF(N12.NE.N21) GO TO 100
      IF(ICVEL.EQ.1) NNN=N2-N1-1
      IF(ICVEL.EQ.0) NNN=NN2-NN1-1
      NGG=N12/KORA-1
      IF(NNN.NE.NGG) GO TO 150
      IAUT=N12/KORA-1
      IF(ICVEL.EQ.1) THEN
         NM=NN+IAUT
         MCVEL(NM)=MCVEL(NN)
         MATV(NM) = MATV(NN)
         ISNAP(NM)=ISNAP(NN)
         IPGC(NM) =IPGC(NN)
         DO 31 II=1,3
            APRS(II,NM)=APRS(II,NN)
   31    CONTINUE
         IF(INDBTH.EQ.1) TBTH(NM) = TBTH(NN)
         IF(INDDTH.EQ.1) TDTH(NM) = TDTH(NN)
         DO 32 II=1,2
            NEL(NM,II)=NEL(NN,II)
   32    CONTINUE
      ENDIF
      DO 34 J=1,IAUT
         JJ=NN1+J
         IF(ICVEL.EQ.1) THEN
            N1=N1+1
            MCVEL(JJ)=N1
         ENDIF
         MATV(JJ) = MATV(NN1)
         ISNAP(JJ)=ISNAP(NN1)
         IPGC(JJ) =IPGC(NN1)
         DO 33 II=1,3
            APRS(II,JJ)=APRS(II,NN1)
   33    CONTINUE
         IF(INDBTH.EQ.1) TBTH(JJ) = TBTH(NN1)
         IF(INDDTH.EQ.1) TDTH(JJ) = TDTH(NN1)
         DO 35 II=1,2
            NODP=NEL(JJ-1,II)
            NEL(JJ,II)=NODP+KORA
            IF(NODP.EQ.0) NEL(JJ,II) = 0
   35    CONTINUE
   34 CONTINUE
      IF(ICVEL.EQ.1) I=NM
      IF(ICVEL.EQ.0) I=I+IAUT
      IF(I.EQ.NE) GO TO 50
      NAUT=0
      IF(KORC.EQ.0) GO TO 5
      KORA=KORC
      NAUT=1
      IF(ICVEL.EQ.1) N1=N2
      IF(ICVEL.EQ.0) NN1=NN2
      GO TO 5
C
CS     STAMPANJE UCITANIH I GENERISANIH PODATAKA
CE     PRINT INPUT AND GENERATED DATA
C
   50 IF(NULAZ.NE.1.AND.NULAZ.NE.3) RETURN
      CALL WBROJK(KARTI,0)
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2140)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6140)
      DO 70 I=1,NE
         IF(ICVEL.EQ.1) IN=MCVEL(I)
         IF(ICVEL.EQ.0) IN=I
         WRITE(IZLAZ,5001) IN,(NEL(I,J),J=1,2),MATV(I),ISNAP(I),IPGC(I),
     1                     (APRS(J,I),J=1,3)
         IF(INDBTH.EQ.1) THEN
         IF(DABS(TBTH(I)).GT.1.0D-10) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2040) TBTH(I)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6040) TBTH(I)
         ENDIF
         ENDIF
         IF(INDDTH.EQ.1) THEN 
         IF(DABS(TDTH(I)).GT.1.0D-10) THEN 
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2050) TDTH(I)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6050) TDTH(I)
         ENDIF
         ENDIF
   70 CONTINUE
      RETURN
C
  150 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2110) NN1,NN2,NGG
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6110) NN1,NN2,NGG
      STOP
  100 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2100) N22,N11,KORA
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6100) N22,N11,KORA
      STOP
C
 1000 FORMAT(14I5)
 4000 FORMAT(14I10)
 1010 FORMAT(4I5,3F10.3,I5)
 4010 FORMAT(I10,3I5,3F10.3,I5)
 1020 FORMAT(7F10.3)
 5020 FORMAT(7(1PD10.3))
 5001 FORMAT(1X,I10,4X,2I10,1X,I6,3X,I6,3X,I6,4X,3(1PD10.3),I6)
C-----------------------------------------------------------------------
 2000 FORMAT(///6X,'U C I T A N I    P O D A C I    O    G A P     E L E
     1 M E N T I M A'/6X,66('-'))
 2600 FORMAT(///
     111X,'(MOGUCE GENERISANJE PODATAKA O ELEMENTIMA,'/
     212X,'UCITATI POTREBAN BROJ KARTICA ZA DEF. SVIH ELEMENATA)'///1X,
     3'ELEMENT','   CVOR1 CVOR2',4X,'STATUS',2X,' SILA ',4X,
     4'INDIK.',8X,'  P R A V A C    ',9X,'KORAK'/2X,'BROJ',3X,
     4'             ',4X,'    ',4X,' STAMP',4X,'PRAVCA')
 2100 FORMAT(///' BROJ CVORA N2=',I5,' NE MOZE SE DOBITI SABIRANJEM BROJ
     1A CVORA N1=',I5,' I KONACNOG BROJA KORAKA KORA=',I5)
 2110 FORMAT(///' IZMEDJU ELEMENATA N1=',I5,' I N2=',I5,' NEMA NG=',I5,
     1' ELEMENATA KOJE TRBA GENERISATI')
 2140 FORMAT(6X,'G E N E R I S A N I    P O D A C I    O    G A P   E L
     1E M E N T I M A'/6X,70('-')///1X,
     3'ELEMENT','   CVOR1 CVOR2',4X,'STATUS',2X,' SILA ',4X,
     4'INDIK.',8X,'  P R A V A C    '/2X,'BROJ',3X,
     4'             ',4X,'    ',4X,' STAMP',4X,'PRAVCA')
 2500 FORMAT(///'P O D A C I   O   O S O B I N A M A'/35('-')//
     37X,'GAP',7X,'AKN',6X,'AKNZ',7X,'AKS',7X,'AMY',7X,'AMZ',6X,'PFOR')
 2040 FORMAT(/' VREME NASTAJANJA ELEMENTA - TBTH =',1PD10.3)
 2050 FORMAT(/' VREME NESTAJANJA ELEMENTA - TDTH =',1PD10.3)
C-----------------------------------------------------------------------
 6000 FORMAT(///6X,'I N P U T   D A T A   F O R   G A P   E L E M E N T 
     1S'/6X,53('-'))
 6600 FORMAT(///1X,
     3'ELEMENT','   NODE1 NODE2',4X,'STATUS',2X,' FORCE',4X,
     4'DIREC.',8X,'D I R E C T I O N',9X,'STEP'/2X,'NUM.',3X,
     4'             ',4X,'    ',4X,' PRINT',4X,'INDIC.')
 6100 FORMAT(///' NODE NUMBER N2=',I5,' CAN NOT BE DEFINED BY SUPERPOSIT
     1ION OF NODE N1=',I5,' WITH A NUMBER OF STEPS  KORA=',I5)
 6110 FORMAT(///' AMONG ELEMENTS N1=',I5,' AND N2=',I5,' THERE IS NO  ',
     1'NG=',I5,' ELEMENTS TO GENERATE')
 6140 FORMAT(6X,'G E N E R A T E D   D A T A   F O R   G A P   E L E M E
     1 N T S'/6X,61('-')///1X,
     3'ELEMENT','   NODE1 NODE2',4X,'STATUS',2X,' FORCE',4X,
     4'DIREC.',8X,'D I R E C T I O N'/2X,'NUM.',3X,
     4'             ',4X,'    ',4X,' PRINT',4X,'INDIC.')
 6500 FORMAT(///20X,'G A P     P R O P E R T I E S'/20X,29('-')//
     37X,'GAP',7X,'AKN',6X,'AKNZ',7X,'AKS',7X,'AMY',7X,'AMZ',6X,'PFOR')
 6040 FORMAT(/' ELEMENT BIRTH TIME - TBTH =',1PD10.3)
 6050 FORMAT(/' ELEMENT DEATH TIME - TDTH =',1PD10.3)
C-----------------------------------------------------------------------
      END
