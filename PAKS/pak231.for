C=======================================================================
C ARGENTINA 
C=======================================================================
      SUBROUTINE STAN51(PLAST,CORGT,AU,ISNA,IPRC,MATV,NSLOJ,MCVEL,ICVEL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.       TO PRINTOUT STRESS IN 2/D ELEMENTS FOR MATERIAL MODEL 51
CS.   P R O G R A M
CS.       ZA STAMPANJE NAPONA 2/D ELEMENATA ZA MATERIJALNI MODEL 5
C .
C ......................................................................
C
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /UGAOVL/ TE(4,4)
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION PLAST(*),ISNA(*),IPRC(*),MCVEL(*)
      DIMENSION CORGT(3,NGS12,*),MATV(*),NSLOJ(*)
      DIMENSION STRESS(8),DEF(4),DEFP(4)
      DIMENSION AU(*)
      REAL AU
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' STAN05'
      NNLM=0
      NGXYZ=NGS12*MXS
      NPR56=MODPR2( NMODM )
      STRESS(4)=0.D0
      KK=3
      IF(IETYP.EQ.1) KK=4
      INDSN=0
      IT=0
      SEFE=0.D0
      DO 20 NLM=1,NE
C
CS       NASTAJANJE I NESTAJANJE ELEMENATA
CE       ELEMENT BIRTH AND DEATH OPTION
C
         IBD=0
         CALL DTHBTH(AU(LTBTH),AU(LTDTH),VREME,NLM,IBD)
         IF(IBD.EQ.1) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2040) NLM
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6040) NLM
         GO TO 20
         ENDIF
C
         NPROS=(NLM-1)*NGXYZ-1
         IBTC=0
         ISN=ISNA(NLM)
         IF(ISN.EQ.1) GO TO 20
C
         IF(MODORT(NMODM).EQ.1.AND.IT.EQ.0) THEN
            IT=1
            CALL TSIG2D
         ELSE
            ISN=2
         ENDIF
         IF(INDSN.EQ.0) THEN
            IF(ISN.EQ.0) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2011) NGE
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6011) NGE
            ENDIF
            IF(ISN.EQ.2) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2010) NGE
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6010) NGE
            ENDIF
         ENDIF
         INDSN=1
         IPC=IPRC(NLM)
         NMM=NLM
         IF(ICVEL.EQ.1) NMM=MCVEL(NLM)
         WRITE(IZLAZ,5000) NMM
         MSS=1
         IF(MSET.GT.0) MSS=NSLOJ(MATV(NLM))
         DO 10 MSL=1,MSS
            IF(MSET.GT.0) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2500) MSL
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6500) MSL
            ENDIF
         DO 10 NGR=1,NGAUSX
         DO 10 NGS=1,NGAUSY
            IBTC=IBTC+1
            LL=1+(NPROS+IBTC)*NPR56
CS   TRANSFORMACIJA GLOBALNIH NAPONA NA LOKALNE, LOKALNIH DEF NA GLOBALNE
            IF(ISN.EQ.0.AND.MODORT(NMODM).EQ.1) THEN
              CALL CLEAR(STRESS,4)
              CALL MNOZI1(STRESS,TE,PLAST(LL),4,4)
              CALL JEDNA1(DEF ,PLAST(LL+4),4)
              CALL JEDNA1(DEFP,PLAST(LL+8),4)
            ELSEIF(ISN.EQ.2.AND.MODORT(NMODM).EQ.1)THEN
              CALL JEDNA1(STRESS,PLAST(LL),4)
              CALL CLEAR(DEF ,4)
              CALL MNOZI2(DEF,TE,PLAST(LL+4),4,4)
              CALL CLEAR(DEFP,4)
              CALL MNOZI2(DEFP,TE,PLAST(LL+8),4,4)
            ELSE
              CALL JEDNA1(STRESS,PLAST(LL),4)
              CALL JEDNA1(DEF ,PLAST(LL+4),4)
              CALL JEDNA1(DEFP,PLAST(LL+8),4)
            ENDIF
            CALL GLAVN2(STRESS)
            IF(STRESS(8).GT.SEFE) THEN
               NNLM=NLM
               NNGR=NGR
               NNGS=NGS
               MSLL=MSL
               SEFE=STRESS(8)
            ENDIF
            WRITE(IZLAZ,5001) NGR,NGS,(STRESS(K),K=1,7)
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2002) DEF
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6002) DEF
            IPRPL=0
            DO 30 II=LL+8,LL+11
               IF(PLAST(II).GT.1.0D-10) IPRPL=1
   30       CONTINUE
            IF(IPRPL.EQ.1) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2003) DEFP
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6003) DEFP
            ENDIF
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2015) STRESS(8)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6015) STRESS(8)
   10    CONTINUE
   20 CONTINUE
      IF(NNLM.GT.0)THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2030) NNLM,NNGR,NNGS,MSLL,SEFE
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6030) NNLM,NNGR,NNGS,MSLL,SEFE
      ENDIF
      RETURN
 5000 FORMAT(/I8)
 5001 FORMAT(2I3,3X,4(1PD11.3),2(1PD10.2),1PD10.2)
C-----------------------------------------------------------------------
 2010 FORMAT(/'1'///' GLOBALNI NAPONI IZOPARAMETARSKIH 2/D ELEMENATA GRU
     1PE ELEMENATA',I6/1X,68('-')//
     1' ELEMENT /'/' IR IS      NAPON-XX',2X,' NAPON-YY',2X,
     1' NAPON-XY',2X,' NAPON-ZZ',2X,'NAPON-S1',2X,'NAPON-S2  ALFA')
 2011 FORMAT(/'1'///' LOKALNI NAPONI IZOPARAMETARSKIH 2/D ELEMENATA GRUP
     1E ELEMENATA',I6/1X,67('-')//
     1' ELEMENT /'/' IR IS      NAPON-RR',2X,' NAPON-SS',2X,
     1' NAPON-RS',2X,' NAPON-ZZ',2X,'NAPON-S1',2X,'NAPON-S2  ALFA')
 2015 FORMAT(' EF.NAPON=',1PD10.3)
 2030 FORMAT(//' MAKSIMALNI EFEKTIVNI NAPON:'/' ELEMENT =',I6,'  IR =',
     1I3,'  IS =',I3,'  SLOJ =',I3,'  MAX.EFE.NAP. =',1PD11.3//)
 2002 FORMAT(' UK.DEFO.',4(1PD11.3))
 2003 FORMAT(' PL.DEFO.',4(1PD11.3))
 2500 FORMAT(/' SLOJ =',I3)
 2040 FORMAT(/' NESTAO JE ELEMENT -',I5)
C-----------------------------------------------------------------------
 6010 FORMAT(/'1'///' GLOBAL STRESS COMPONENTS OF ISOPARAMETRIC 2/D ELEM
     1ENTS FOR GROUP',I6/1X,70('-')//
     1' ELEMENT /'/' IR IS     STRESS-XX',2X,'STRESS-YY',2X,
     1'STRESS-XY',2X,'STRESS-ZZ',2X,'STRESS-1',2X,'STRESS-2  ALFA')
 6011 FORMAT(/'1'///' LOCAL STRESS COMPONENTS OF ISOPARAMETRIC 2/D ELEME
     1NTS FOR GROUP',I6/1X,69('-')//
     1' ELEMENT /'/' IR IS     STRESS-RR',2X,'STRESS-SS',2X,
     1'STRESS-RS',2X,'STRESS-ZZ',2X,'STRESS-1',2X,'STRESS-2  ALFA')
 6015 FORMAT(' EF.STRE.=',1PD10.3)
 6030 FORMAT(//' MAXIMUM EFFECTIVE STRESS:'/' ELEMENT =',I6,'  IR =',I3,
     1'  IS =',I3,'  LAYER =',I3,'  MAX.EFF.STR. =',1PD11.3//)
 6002 FORMAT(' TO.STRA.',4(1PD11.3))
 6003 FORMAT(' PL.STRA.',4(1PD11.3))
 6500 FORMAT(/' LAYER =',I3)
 6040 FORMAT(/' DEATH ELEMENT -',I5)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE STAG51(PLAST,AU,MCVEL,ICVEL,ISDTY)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.       TO PRINTOUT STRESS FOR 2/D ELEMENTS IN UNIVERSAL FILE - PLASTI
CS.   P R O G R A M
CS.       ZA STAMPANJE NAPONA 2/D ELEMENATA U UNIVERZALNI FILE - PLASTIC
C . 
C ......................................................................
C 
      CHARACTER*250 NASLOV    
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /NASLOV/ NASLOV
      COMMON /SUMELE/ ISUMEL,ISUMGR
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION SIGM(6),MCVEL(*),PLAST(*),SR(6)
      DIMENSION AU(*)
      REAL AU
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' STAG05'
      NNCVE=NCVE
      IF(NCVE.LT.8) NCVE=4
      IF(NCVE.EQ.9) NCVE=8
      SIGM(4)=0.0D0
      SIGM(5)=0.0D0
C     STRUKTURNA ANALIZA = 1
      IMOTY=1
C     STACIONARAN = 1; NESTACIONARAN = 4   
      IANTY=1
      IFAT1=1
      IFAT2=1
      FATY8=0.0D0
      IF(NDT.GT.1) THEN 
         IANTY=4
         IFAT1=2
         FATY8=VREME
      ENDIF
C     SIMETRICAN TENZOR = 4
      IDACH=4
C     NAPONI = 2
C     ISDTY=2
C     UKUPNE DEFORMACIJE = 3
C     ISDTY=3
C     PLASTICNE DEFORMACIJE = 0
C     ISDTY=0
C     PRECIZNOST JEDNOSTRUKA = 2; DVOSTRUKA = 4 
      IDATY=2
C     BROJ PODATAKA = 6
      NDVPN=6
      IEXP=1
      NNODS=NCVE
      NVPN=6
      IND1=-1
      INA1=57
      WRITE(IGRAF,5000) IND1
      WRITE(IGRAF,5000) INA1
      WRITE(IGRAF,5003) NASLOV
      IF(ISDTY.EQ.2) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IGRAF,2001)
      IF(ISRPS.EQ.1)
     1WRITE(IGRAF,6001)
      ELSEIF(ISDTY.EQ.3) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IGRAF,2002)
      IF(ISRPS.EQ.1)
     1WRITE(IGRAF,6002)
      ELSE
      IF(ISRPS.EQ.0)
     1WRITE(IGRAF,2003)
      IF(ISRPS.EQ.1)
     1WRITE(IGRAF,6003)
      ENDIF
      IF(ISRPS.EQ.0)
     1WRITE(IGRAF,2004) KOR
      IF(ISRPS.EQ.1)
     1WRITE(IGRAF,6004) KOR
      WRITE(IGRAF,1000) IMOTY,IANTY,IDACH,ISDTY,IDATY,NDVPN
      IF(NDT.EQ.1) WRITE(IGRAF,1000) IFAT1,IFAT2,KOR
      IF(NDT.GT.1) WRITE(IGRAF,1000) IFAT1,IFAT2,KOR,KOR
      WRITE(IGRAF,5001) FATY8
C
      NPROS=MODPR2( NMODM )
      NGXY=NGS12*NPROS*MXS
      IF(ISDTY.EQ.2) THEN
         KK=1
      ELSEIF(ISDTY.EQ.3) THEN
         KK=5
      ELSE
         KK=9
      ENDIF
      DO 20 NLM=1,NE
C
CS       NASTAJANJE I NESTAJANJE ELEMENATA
CE       ELEMENT BIRTH AND DEATH OPTION
C
         IBD=0
         CALL DTHBTH(AU(LTBTH),AU(LTDTH),VREME,NLM,IBD)
         IF(IBD.EQ.1) GO TO 20
C
         LL=KK+(NLM-1)*NGXY
         NUM=NLM+ISUMEL
         IF(ICVEL.EQ.1) NUM=MCVEL(NLM)
         WRITE(IGRAF,1000) NUM,IEXP,NNODS,NVPN
         IF(NNODS.EQ.4.AND.NGAUSX.EQ.2) THEN
            CALL WRIT25(PLAST(LL+3*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+  NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL        ),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+2*NPROS),SIGM,SR,SE)
         ENDIF
         IF(NNODS.EQ.4.AND.NGAUSX.EQ.3) THEN
            CALL WRIT25(PLAST(LL+8*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+2*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL        ),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+6*NPROS),SIGM,SR,SE)
         ENDIF
         IF(NNODS.EQ.8.AND.NGAUSX.EQ.3) THEN
            CALL WRIT25(PLAST(LL+8*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+5*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+2*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+  NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL        ),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+3*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+6*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+7*NPROS),SIGM,SR,SE)
         ENDIF
         IF(NNODS.EQ.8.AND.NGAUSX.EQ.2) THEN
            CALL WRIT25(PLAST(LL+3*NPROS),SIGM,SR,SE)
            CALL WRIT26(PLAST(LL+3*NPROS),PLAST(LL+  NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+  NPROS),SIGM,SR,SE)
            CALL WRIT26(PLAST(LL+  NPROS),PLAST(LL        ),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL        ),SIGM,SR,SE)
            CALL WRIT26(PLAST(LL        ),PLAST(LL+2*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+2*NPROS),SIGM,SR,SE)
            CALL WRIT26(PLAST(LL+2*NPROS),PLAST(LL+3*NPROS),SIGM,SR,SE)
         ENDIF
         IF(NNODS.EQ.8.AND.NGAUSX.EQ.5) THEN
            CALL WRIT25(PLAST(LL+24*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+14*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+ 4*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+ 2*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL         ),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+10*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+20*NPROS),SIGM,SR,SE)
            CALL WRIT25(PLAST(LL+22*NPROS),SIGM,SR,SE)
         ENDIF
   20 CONTINUE
      WRITE(IGRAF,5000) IND1
      ISUMEL=ISUMEL+NE
      NCVE=NNCVE
      RETURN
 1000 FORMAT(8I10)
 5000 FORMAT(I6)
 5001 FORMAT(6(1PE13.5))
 5003 FORMAT(A80)
C-----------------------------------------------------------------------
 2001 FORMAT('NAPONI 2/D ELEMENATA')
 2002 FORMAT('UKUPNE DEFORMACIJE 2/D ELEMENATA')
 2003 FORMAT('PLASTICNE DEFORMACIJE 2/D ELEMENATA')
 2004 FORMAT(
     1       'DATUM I VREME'/
     1       'PRAZNA'/
     1       'SLUCAJ OPTERECENJA:',I10)
C-----------------------------------------------------------------------
 6001 FORMAT('STRESS OF 2/D ELEMENTS')
 6002 FORMAT('TOTAL STRAIN OF 2/D ELEMENTS')
 6003 FORMAT('PLASTIC STRAIN OF 2/D ELEMENTS')
 6004 FORMAT(
     1       'DATE'/
     1       'EMPTY'/
     1       'LOAD CASE         :',I10)
C-----------------------------------------------------------------------
      END
