C=======================================================================
C
C        INTEGRALJENJE MATRICA 3/D ELEMENATA
C
C   SUBROUTINE K21MAS
C              READM3
C              SIST3M
C              ELTM3
C
C=======================================================================
      SUBROUTINE K21MAS
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C    GLAVNI PROGRAM ZA POZIVANJE PROGRAMA ZA RACUNANJE MATRICA ELEMENATA
C
      include 'paka.inc'
      
C
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /DUPLAP/ IDVA
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' K21MAS'
C
      LAU=LMAX
      CALL READM3(A(LAU))
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      LAE=LMAX
      NCVE3=NCVE*3
      MXAE = NCVE3+(13*NCVE+NCVE3*(NCVE3+1)/2)*IDVA
      LMAX = LAE + MXAE
      IF(LMAX.LT.MTOT) GO TO 70
      WRITE(IZLAZ,2009) LMAX,MTOT
      STOP
C
C     FORMIRANJE MATRICE KRUTOSTI ELEMENATA I PAKOVANJE U SISTEM
C
   70 CALL SIST3M(A(LAE),A(LAU))
C
      RETURN
C-----------------------------------------------------------------------
 2009 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A ZA MATRICE ELEMENATA'
     1/' POTREBNA DIMENZIJA, LMAX=',I10/
     2' RASPOLOZIVA DIMENZIJA, MTOT=',I10)
C-----------------------------------------------------------------------
      END
C======================================================================
      SUBROUTINE READM3(AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     GLAVNI UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
C
      include 'paka.inc'
      
C
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /SIMO2D/ LALFE,LHAEM,LHINV,LGEEK,IALFA
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /INCONF/ LINDBEL,LBIRTHC
      COMMON /INTGRA/ INCOTX,INCOTY,INCOTZ
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /GAUSVR/ LTEMGT,LCORGT,ICORGT
      COMMON /CVSILE/ NSILA,LESILA
      COMMON /COEFSM/ COEF(3),ICOEF
      COMMON /IKOVAR/ INDKOV
      COMMON /CEPMAT/ INDCEP
      COMMON /LEVDES/ ILEDE,NLD,ICPM1
      COMMON /CDEBUG/ IDEBUG
C
      DIMENSION AU(*)
      REAL AU
C
C     POZIVANJE PROGRAMA ZA ULAZNE PODATKE .
C
      IF(IDEBUG.GT.0) PRINT *, ' READM3'
C
      LSTAZA(1)=LMAX8
      READ(IELEM,REC=LMAX8)
     1NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,ICORGT,LCEL,LELC,NMA,NMI,
     1MXAU,LNEL,LNMAT,LIPGC,LIPRC,LISNA,LTHID,LLMEL,IPODT,LNNOD,
     1ND,NGS12,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET,NSILA,LESILA,
     1INCOTX,INCOTY,INCOTZ,LBET0,((CPP(J,I),J=1,3),I=1,3),
     1((TSG(J,I),J=1,6),I=1,6),BETA,IBB0,LALFE,LHAEM,LHINV,LGEEK,IALFA,
     1INDBTH,INDDTH,LTBTH,LTDTH,INDKOV,INDCEP,ILEDE,NLD,ICPM1,
     1COEF(3),ICOEF,LINDBEL,LBIRTHC
      LSTAZA(2)=LMAX8+1
      CALL READDD(AU,MXAU/IDVA,IELEM,LMAX8,LDUZI)
      LMAX=LAU+MXAU
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      RETURN
      END
C======================================================================
C
C======================================================================
      SUBROUTINE SIST3M(AE,AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     GLAVNI UPRAVLJACKI PROGRAM  ZA MATRICE ELEMENATA I SISTEMA
C     RACUNANJE NAPONA
C
      include 'paka.inc'
      
C
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /DINAMI/ IMASS,IDAMP,PIP,DIP,MDVI
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /MATERM/ LMODEL,LGUSM
      COMMON /DUPLAP/ IDVA
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /UPDLAG/ LUL,LCORUL
      COMMON /CDEBUG/ IDEBUG
      DIMENSION AE(*),AU(*)
      REAL AE,AU
C
C     REPERI U VEKTORU ELEMENATA AE
C
      IF(IDEBUG.GT.0) PRINT *, ' SIST3M'
C
      NCVE3=NCVE*3
      NWE=NCVE3*(NCVE3+1)/2
      LHE=1
      LBET=LHE+4*NCVE*IDVA
      LSKE=LBET+3*NCVE3*IDVA
      LLM=LSKE+NWE*IDVA
      MXAE1=LLM+NCVE3-1
      IF(MXAE1.NE.MXAE) THEN
        WRITE(IZLAZ,2000)MXAE1,MXAE
 2000   FORMAT(//' NE POKLAPAJU SE MXAE1 I MXAE'/' PROGRAM STOP'/2I10//)
      STOP ' PROGRAM STOP - PAK35 - SIST3M'
      ENDIF
      IZBR=13*NCVE+NWE
C
C     OSNOVNA PETLJA PO ELEMENTIMA
C
      DO 100 NLM=1,NE
C
CS       NASTAJANJE I NESTAJANJE ELEMENATA
CE       ELEMENT BIRTH AND DEATH OPTION
C
         IBD=0
         CALL DTHBTH(AU(LTBTH),AU(LTDTH),VREME,NLM,IBD)
         IF(IBD.EQ.1) GO TO 100
C
      CALL CLEAR(AE,IZBR)
C
C     RACUNANJE MATRICE ELEMENATA I/ILI NAPONA
C
      KORD=LCORD
      IF(IATYP.EQ.3) KORD=LCORUL
      CALL ELTM3(AE(LSKE),AE(LLM),AU(LNEL),AU(LNMAT),
     1AE(LHE),AE(LBET),A(KORD),AU(LIPGC),AU(LLMEL),A(LGUSM),NCVE3)
  100 CONTINUE
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE ELTM3(SKE,LM,NEL,NMAT,HE,BET,CORD,IPGC,LMEL,GUSM,NCVE3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     INTEGRACIJA MATRICE MASA 3D ELEMENATA
C
      include 'paka.inc'
      
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /DINAMI/ IMASS,IDAMP,PIP,DIP,MDVI
      COMMON /CDEBUG/ IDEBUG
C
      DIMENSION SKE(*),LM(*),NEL(NE,*),NMAT(*),
     1CORD(NP,*),HE(NCVE,*),BET(3,*),IPGC(*),
     1LMEL(NCVE3,*),GUSM(50,*),CMC(21)
C
      DIMENSION XG(55),WGT(55),NREF(11),XGG(15)
C
      DATA NREF/0,1,3,6,10,15,21,28,36,45,55/
      DATA WGT/            2.D0,               1.D0,               1.D0,
     1       .555555555555556D0, .888888888888889D0, .555555555555556D0,
     2       .347854845137454D0, .652145154862546D0, .652145154862546D0,
     3       .347854845137454D0, .236926885056189D0, .478628670499366D0,
     4       .568888888888889D0, .478628670499366D0, .236926885056189D0,
     5       .171324492379170D0, .360761573048139D0, .467913934572691D0,
     6       .467913934572691D0, .360761573048139D0, .171324492379170D0,
     7       .129484966168870D0, .279705391489277D0, .381830050505119D0,
     8       .417959183673469D0, .381830050505119D0, .279705391489277D0,
     9       .129484966168870D0, .101228536290376D0, .222381034453374D0,
     9       .313706645877887D0, .362683783378362D0, .362683783378362D0,
     1       .313706645877887D0, .222381034453374D0, .101228536290376D0,
     2       .081274388361574D0, .180648160694857D0, .260610696402935D0,
     3       .312347077040003D0, .330239355001260D0, .312347077040003D0,
     4       .260610696402935D0, .180648160694857D0, .081274388361574D0,
     5       .066671344308688D0, .149451349150581D0, .219086362515982D0,
     6       .269266719309996D0, .295524224714753D0, .295524224714753D0,
     7       .269266719309996D0, .219086362515982D0, .149451349150581D0,
     8       .066671344308688D0/
      DATA XG /            0.D0,-.577350269189626D0, .577350269189626D0,
     1      -.774596669241483D0,               0.D0, .774596669241483D0,
     2      -.861136311594053D0,-.339981043584856D0, .339981043584856D0,
     3       .861136311594053D0,-.906179845938664D0,-.538469310105683D0,
     4                     0.D0, .538469310105683D0, .906179845938664D0,
     5      -.932469514203152D0,-.661209386466265D0,-.238619186083197D0,
     6       .238619186083197D0, .661209386466265D0, .932469514203152D0,
     7      -.949107912342759D0,-.741531185599394D0,-.405845151377397D0,
     8                     0.D0, .405845151377397D0, .741531185599394D0,
     9       .949107912342759D0,-.960289856497536D0,-.796666477413627D0,
     9      -.525532409916329D0,-.183434642495650D0, .183434642495650D0,
     1       .525532409916329D0, .796666477413627D0, .960289856497536D0,
     2      -.968160239507626D0,-.836031107326636D0,-.613371432700590D0,
     3      -.324253423403809D0,               0.D0, .324253423403809D0,
     4       .613371432700590D0, .836031107326636D0, .968160239507626D0,
     5      -.973906528517172D0,-.865063366688985D0,-.679409568299024D0,
     6      -.433395394129247D0,-.148874338981631D0, .148874338981631D0,
     7       .433395394129247D0, .679409568299024D0, .865063366688985D0,
     8       .973906528517172D0/
C
      DATA XGG/ 0.000000000000000,-1.000000000000000, 1.000000000000000,
     1         -1.000000000000000, 0.000000000000000, 1.000000000000000,
     2         -1.000000000000000,-0.333333333333333, 0.333333333333333,
     3          1.000000000000000,-1.000000000000000,-0.500000000000000,
     4          0.000000000000000, 0.500000000000000, 1.000000000000000/
C
C     FORMIRANJE VEKTORA LM
C
      IF(IDEBUG.GT.0) PRINT *, ' ELTM3'
C
      DO 10 NC=1,NCVE3
      LM(NC)=LMEL(NC,NLM)
   10 CONTINUE
C
C     PETLJA PO GAUSOVIM TACKAMA
C
C      CALL WRR(RTDT,JEDN,'RTDT')
      IPGCT=IPGC(NLM)
      MAT=NMAT(NLM)
      GUST=GUSM(NMODM,MAT)
      AMASA=0.D0
C
      DO 500 NGR=1,NGAUSX
      JGR=NREF(NGAUSX)+NGR
      R=XG(JGR)
      IF(IPGCT.EQ.1) R=XGG(JGR)
      WR=WGT(JGR)
C
      DO 50 NGS=1,NGAUSY
      JGS=NREF(NGAUSY)+NGS
      S=XG(JGS)
      IF(IPGCT.EQ.1) S=XGG(JGS)
      WS=WGT(JGS)
C
      DO 55 NGT=1,NGAUSZ
      JGT=NREF(NGAUSZ)+NGT
      T=XG(JGT)
      IF(IPGCT.EQ.1) T=XGG(JGT)
      WT=WGT(JGT)
CS     JAKOBIJAN
      CALL JACTE3(NEL,CORD,HE,R,S,T,0)
      WD=WR*WS*WT*DET*GUST
      AMASA= AMASA+WD
C
C     INTEGRACIJA KONZISTENTNE MATRICE MASA
C
      IF(IMASS.EQ.1)THEN
        CALL BETVDY(BET,HE,NCVE,3)
        CALL INTEGV(SKE,BET,LM,WD,NCVE3,3)
      ENDIF
C
   55 CONTINUE
   50 CONTINUE
  500 CONTINUE
C
C     KONCETRISANA MATRICA MASA I PAKOVANJE
C
      IF(IMASS.EQ.2)THEN
        CALL PODMA3(NEL,CMC)
        CALL LUMMAS(A(LSK),A(LMAXA),NEL,LM,CMC,AMASA,NCVE,NE,NLM,3)
      ENDIF
C
C     PAKOVANJE KONZISTENTNE MATRICE ELEMENTA U MATRICU SISTEMA
C
      IF(IMASS.EQ.1) CALL SPAKUJ(A(LSK),A(LMAXA),SKE,LM,NCVE3)
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE BETVDY(BET,HE,NCVE,MDIM)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     INTEGRACIJA MATRICE B ZA MASE
C
      DIMENSION HE(NCVE,*),BET(3,*)
C
      NCV3=NCVE*MDIM-MDIM+1
      DO 150 I=1,NCV3,MDIM
      NCI=(I+MDIM-1)/MDIM
      BET(1,I)=HE(NCI,1)
      BET(2,I)=0.0D0
      BET(1,I+1)=0.0D0
      BET(2,I+1)=HE(NCI,1)
      IF(MDIM.EQ.3)THEN
        BET(1,I+2)=0.0D0
        BET(2,I+2)=0.0D0
        BET(3,I)=0.0D0
        BET(3,I+1)=0.0D0
        BET(3,I+2)=HE(NCI,1)
      ENDIF
  150 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE PODMA3(NEL,CMC)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     PODELA MASA ELEMENTA PO CVOROVIMA
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /CDEBUG/ IDEBUG
      DIMENSION NEL(NE,*),CMC(*)
C
      IF(IDEBUG.GT.0) PRINT *, ' PODMA3'
C
      IF(NCVE.GT.8) GO TO 100
      C125=0.125D0
      CMC(1)=C125
      CMC(2)=C125
      CMC(3)=C125
      CMC(4)=C125
      CMC(5)=C125
      CMC(6)=C125
      CMC(7)=C125
      CMC(8)=C125
      GO TO 200
C
C     KOREKCIJA FUNKCIJA KADA JE BROJ CVOROVA VECI OD 8
C
  100 STOP '*** NOT IMPLEMENTED FOR NCVE.GT.8 --- PODMA3'
  200 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE LUMMAS(A,MAXA,NEL,LM,CMC,AMASA,NCVE,NE,NLM,MDIM)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     PAKOVANJE KONCETRISANIH MASA (IMASS.EQ.2)
C
      DIMENSION A(*),CMC(*),MAXA(*),NEL(NE,*),LM(*)
      DO 80 NC=1,NCVE
         IF(NEL(NLM,NC).EQ.0)GO TO 80
         NNC=MDIM*(NC-1)
         DO 75 I=1,MDIM
            NJ=LM(NNC+I)
            IF(NJ.EQ.0) GO TO 75
C            NJM=MAXA(NJ)
C            A(NJM)=A(NJM)+AMASA*CMC(NC)
            A(NJ)=A(NJ)+AMASA*CMC(NC)
   75    CONTINUE
   80 CONTINUE
      RETURN
      END
