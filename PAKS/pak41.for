C=======================================================================
C PAK41
C
C    UCITAVANJE ULAZNIH PODATAKA ZA IZOPARAMETARSKI
C    GREDNI KONACNI ELEMENT
C
C  SUBROUTINE UL4EGL
C             ULIGE
C             LMMHT
C             READEG
C             ULIGE1
C=======================================================================
      SUBROUTINE UL4EGL
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      CHARACTER*250 ACOZ
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /DIREKT/ LSTAZZ(9),LDRV0,LDRV1,LDRV,IDIREK
      COMMON /SRPSKI/ ISRPS
      COMMON /IGRGLA/ NGRU,NGSU,NGTU,NGS,NTE,NUD,IANAL,JTAU,NWE,NT6,INEL
     *,MATG
      COMMON /IGREP/  LIGNOP,LIGNDS,LIGDST,LIGCTR,MXAUG,LMXAUG,LAG,MAXAE
     *               ,LICTRP,LIGX  ,LIGY  ,LIGZ,LAE,LLM,LIGMAT
C
      COMMON /CDEBUG/ IDEBUG
      IF(IDEBUG.GT.0) PRINT *, ' UL4EGL'
      IDIREK=0
      CALL ISPITA(ACOZ)
      IF(INDFOR.EQ.1)
     *READ(IULAZ,*) NGRU,NGSU,NGTU,NTE,NUD
      IF(INDFOR.EQ.2)
     *READ(ACOZ,10)NGRU,NGSU,NGTU,NTE,NUD
      IF(NGRU.EQ.0) NGRU=2
      IF(NGSU.EQ.0) NGSU=2
      IF(NGTU.EQ.0) NGTU=2
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      CALL WBROJK(KARTIC,0)
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2000) NGRU,NGSU,NGTU,NTE,NUD
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6000) NGRU,NGSU,NGTU,NTE,NUD
      ENDIF
C
      NT6=6*NTE
      NGS=NGRU*NGSU*NGTU
      IANAL=0
      JTAU=3
      IF(NGSU.EQ.1.OR.NGTU.EQ.1) IANAL=1
      IF(IANAL.EQ.1) JTAU=8
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2025) IANAL
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6025) IANAL
      ENDIF
C ODREDJIVANJE INDIKATORA VRSTE GEOMETRIJSKE NELINEARNOSTI
C INEL=1 U.L ; INEL=2 T.L.
C
      IF(IATYP.EQ.3.OR.IATYP.EQ.0) INEL=1
      IF(IATYP.EQ.2) INEL=2
C
      IF(IANAL.EQ.0) GO TO 30
      IF(INEL.NE.2) GO TO 30
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2035)
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6035)
      ENDIF
      STOP
   30 CONTINUE
C
C REPERI U VEKTORU A ZA ULAZNE PODATKE
C
      NWE=NT6*(NT6+1)/2
      MAXAE=(18               + 33*NT6 + 2*NTE + NWE)*IDVA
      LIGCTR=1
      LIGDST=LIGCTR+NE*6*NTE*IDVA
      LICTRP=LIGDST+2*NUD*IDVA
      LIGNOP=LICTRP+NE*6*NTE*IDVA
      LIGNDS=LIGNOP+NE*NTE
      LIGMAT=LIGNDS+NE*NTE
      LLM=LIGMAT+NE
      LMXAUG=LLM+2*NT6*NE
      LCEL=LMXAUG
      IF(ICVEL.EQ.1) THEN
        LMXAUG=LCEL+NE
      ENDIF
      MXAUG =LMXAUG-1
      MAXAE  =(18+33*NT6+2*NTE+NT6*(NT6+1)/2)*IDVA
C
      CALL DELJIV(MXAUG,2,INDL)
      IF(INDL.EQ.1) MXAUG=MXAUG+1
      CALL DELJIV(LMAX ,2,INDL)
      IF(INDL.EQ.0) LMAX =LMAX +1
      LAG=LMAX
      LMAX=LAG+MXAUG
      CALL DELJIV(LMAX ,2,INDL)
      IF(INDL.EQ.0) LMAX =LMAX +1
      LMAX=LMAX+MAXAE
C
      IF(LMAX.LE.MTOT) GO TO 40
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2005) LMAX,MTOT
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6005) LMAX,MTOT
      ENDIF      
      STOP
  40  CONTINUE
C
C UCITAVANJE PODATAKA O ELEMENTIMA IZOPAR. KRIVE GREDE
C********
      CALL ULIGE(A(LAG))
C*********
C PRIVREMENI PROSTOR ZA PRIRASTAJE POMERANJA
      RETURN
C
   10 FORMAT(5I5)
C-----------------------------------------------------------------------
 2000 FORMAT(
     *11X,'BROJ GAUSOVIH TACAKA U PRAVCU OSE - R ........... NGRU=',I5/
     *16X,'EQ.0;  NGRU = 2'///
     *11X,'BROJ GAUSOVIH TACAKA U PRAVCU OSE - S ........... NGSU=',I5/
     *16X,'EQ.0;  NGSU = 2'///
     *11X,'BROJ GAUSOVIH TACAKA U PRAVCU OSE - T ........... NGTU=',I5/  
     *16X,'EQ.0;  NGTU = 2'///
     *11X,'BROJ CVOROVA PO ELEMENTU ......................... NTE=',I5/
     *//
     *11X,'BROJ RAZLICITO POMERENIH REFERENTNIH OSA ......... NUD=',I5)
 2025 FORMAT(///
     *11X,'VRSTA INTEGRACIJE .............................. IANAL=',I5/
     *16X,'EQ.0;  NUMERICKA'/
     *16X,'EQ.1;  ANALITICKA')
 2035 FORMAT(///' ','***  STOP'/
     *5X,'ANALITICKA INTEGRACIJA NE RADI SA TOTAL LAGRANGIAN')
 2005 FORMAT(' ','NEDOVOLJNA DIMENZIJA U VEKTORU A - ZA PODATKE O '/
     *       ' ','IZOPARAMETARSKOJ KRIVOJ GREDI'/
     *       ' ' ,'POTREBNA DIMENZIJA, LMAX=',I10/
     *       ' ','RASOPLOZIVA DIMENZIJA, NTOTA=',I10)
C-----------------------------------------------------------------------
 6000 FORMAT(
     611X,'NUMBER OF GAUSS POINTS IN R-DIRECTION ........... NGRU =',I5/
     616X,'EQ.0; NGRU = 1'///
     611X,'NUMBER OF GAUSS POINTS IN S-DIRECTION ........... NGSU =',I5/
     616X,'EQ.0; NGSU = 1'///
     611X,'NUMBER OF GAUSS POINTS IN T-DIRECTION ........... NGTU =',I5/
     616X,'EQ.0; NGTU = 1'///
     611X,'NUMBER OF NODES PER ELEMENT ....................... NTE=',I5/
     6//
     611X,'NUMBER OF AXIS EXCETRICITY TYPES .................. NUD=',I5)
 6025 FORMAT(///
     *11X,'TYPE OF INTEGRATION ............................. IANAL=',I5/
     *16X,'EQ.0;  NUMERICAL'/
     *16X,'EQ.1;  ANALITICAL')
 6035 FORMAT(///' ','***  STOP'/
     *5X,'ANALITICAL INTEGRATION NOT ALLOWED WITH TOTAL LAGRANGIAN')
 6005 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE ULIGE(AG)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  PROGRAM ZA POZIVANJE PROGRAMA ZA ULAZNIH PODATAKA
C  ZA IZOPARAMETARSKI GREDNI ELEMENT
C
      include 'paka.inc'

      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ITERBR/ ITER
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /IGRGLA/ NGRU,NGSU,NGTU,NGS,NTE,NUD,IANAL,JTAU,NWE,NT6,INEL
     *,MATG
      COMMON /IGREP/  LIGNOP,LIGNDS,LIGDST,LIGCTR,MXAUG,LMXAUG,LAG,MAXAE
     1               ,LICTRP,LIGX  ,LIGY  ,LIGZ,LAE,LLM,LIGMAT
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /POSTPR/ LNDTPR,LNDTGR,NBLPR,NBLGR,INDPR,INDGR
      COMMON /ZAPISI/ LSTAZA(5)
C
      DIMENSION AG(*)
      REAL AG
C
      COMMON /CDEBUG/ IDEBUG
      IF(IDEBUG.GT.0) PRINT *, ' ULIGE'
      CALL  CLEAR(AG(LIGCTR),12*NE*NTE+2*NUD)
      CALL ICLEAR(AG(LIGNOP), 2*NE*NTE+NE+2*NE*NT6)
C     CALL  CLEAR(AG(LIGDST), 6*NE*NTE+2*NUD+3*NP)
C     CALL  CLEAR(AG(LIGDST), 6*NE*NTE+2*NUD)
C
C POZIVANJE PROGRAMA ZA UCITAVANJE PODATAKA O ELEMENTIMA
C
      NMA=0
      NMI=0
      CALL ULIGE1(AG(LIGNOP),AG(LIGNDS),AG(LIGMAT),AG(LIGDST),AG(LIGCTR)
     1,AG(LICTRP),AG(LCEL),NMA,NMI,ICVEL)
      IF(NBLGR.GE.0) CALL TGRAF4(AG(LIGNOP),AG(LCEL),ICVEL)
      IF(ICVEL.EQ.1) CALL VEZACE(AG(LIGNOP),A(LELCV),NE,NTE)
C
C FORMIRANJE VEKTORA LM I VISINA STUBOVA MHT
C
      LELC=LMXAUG
      IF(ICVEL.EQ.1) THEN
        LMXAUG=LELC+NMA-NMI+1
        CALL ICLEAR(AG(LELC),NMA-NMI+1)
        CALL VEZAEL(AG(LCEL),AG(LELC),NE,NMI)
        MXAUG =LMXAUG-1
        LMAX=LMAX+NMA-NMI+1
      ENDIF
      CALL ICLEAR(AG(LLM),NTE*6*NE)
      CALL LMMHT(A(LID),AG(LIGNOP),AG(LLM))
C
      LMAX8=LMAX8+1
      WRITE(IELEM,REC=LMAX8)
     *NGRU,NGSU,NGTU,NTE,NUD,LCEL,LELC,NMA,NMI,
     *MXAUG,LIGNOP,LIGNDS,LIGMAT,LIGDST,LIGCTR,LICTRP,LLM
C
C
      CALL WRITDD(AG(LIGCTR),MXAUG/IDVA,IELEM,LMAX8,LDUZI)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C
C
C     IF(IATYP.EQ.1.OR.IATYP.EQ.0) GO TO 10
      IF(IATYP.EQ.0) GO TO 10
      IF(NMODM.LT.5) GO TO 10
C
C      PODACI ZA PLASTICNOST
      NPROS=NE*NGS*(JTAU+10)*IDVA
      LPLAST=LMAX
      LPLAS1=LPLAST+NPROS
      LMAX  =LPLAS1+NPROS
      CALL CLEAR(A(LPLAST),NPROS*2/IDVA)
      CALL WRITDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      CALL WRITDD(A(LPLAS1),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
C
C PROSTOR ZA NAPONE
C
   10 LSIGMA=LMAX
      NPROS=NE*NGS*JTAU*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      CALL CLEAR(A(LSIGMA),NPROS/IDVA)
      CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE LMMHT(ID,NOP,LM)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  FORMIRANJE LM I VISINA STUBOVA MHT
C
      include 'paka.inc'
      
C
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /IGRGLA/ NGRU,NGSU,NGTU,NGS,NTE,NUD,IANAL,JTAU,NWE,NT6,INEL
     *,MATG
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /GEORGE/ TOLG,ALFAG,ICCGG
      COMMON /DRAKCE/ IDRAKCE,NELUK,NZERO,NEED1,NEED2,NEED3,NNZERO
     1                ,IROWS,LAILU,LUCG,LVCG,LWCG,LPCG,LRCG
C
      DIMENSION ID(NP,*),NOP(NE,*),LM(NT6,*)
      COMMON /CDEBUG/ IDEBUG
      IF(IDEBUG.GT.0) PRINT *, ' LMMHT'
C
C     PETLJA PO ELEMENTIMA
C
      DO 100 NLM=1,NE
      KK=0
      DO 2 I=1,NTE
      IF(NOP(NLM,I).EQ.0) GO TO 2
      N=NOP(NLM,I)
      DO 3 J=1,6
    3 LM(KK+J,NLM)=ID(N,J)
      KK=KK+6
    2 CONTINUE
C
C  FORMIRANJE VISINE STUBOVA
C
      CALL VISINE(A(LMHT),6*NTE,LM(1,NLM))
      IF (IABS(ICCGG).EQ.1) THEN
         ND=6*NTE
         WRITE(IDRAKCE) ND,(LM(I,NLM),I=1,ND)
         NELUK=NELUK+1
      ENDIF
  100 CONTINUE
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE READEG(AG)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  GLAVNI UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA O
C  IZOPARAMETARSKIM GREDNIM ELEMENTIMA
C
      include 'paka.inc'
      
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ITERBR/ ITER
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /IGRGLA/ NGRU,NGSU,NGTU,NGS,NTE,NUD,IANAL,JTAU,NWE,NT6,INEL
     *,MATG
      COMMON /IGREP/  LIGNOP,LIGNDS,LIGDST,LIGCTR,MXAUG,LMXAUG,LAG,MAXAE
     *               ,LICTRP,LIGX  ,LIGY  ,LIGZ,LAE,LLM,LIGMAT
      COMMON /ZAPISI/ LSTAZA(5)
C
      DIMENSION AG(*)
      REAL AG
      COMMON /CDEBUG/ IDEBUG
      IF(IDEBUG.GT.0) PRINT *, ' READEG'
C
C POZIVANJE PROGRAMA ZA ULAZNE PODATKE
C
      LSTAZA(1)=LMAX8
      READ(IELEM,REC=LMAX8)
     *NGRU,NGSU,NGTU,NTE,NUD,LCEL,LELC,NMA,NMI,
     *MXAUG,LIGNOP,LIGNDS,LIGMAT,LIGDST,LIGCTR,LICTRP,LLM
C
      LSTAZA(2)=LMAX8+1
C
      CALL READDD(AG(LIGCTR),MXAUG/IDVA,IELEM,LMAX8,LDUZI)
C
      LMAX=LAG+MXAUG
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C     IF(IATYP.EQ.1.OR.IATYP.EQ.0) GO TO 10
      IF(IATYP.EQ.0) GO TO 10
      IF(NMODM.LT.5) GO TO 10
      LPLAST=LMAX
      NPROS=NE*NGS*(JTAU+10)*IDVA
      LPLAS1=LPLAST+NPROS
      LMAX  =LPLAS1+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(3)=LMAX8+1
      CALL READDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      LSTAZA(4)=LMAX8+1
      CALL READDD(A(LPLAS1),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
   10 LSIGMA=LMAX
      NPROS=NE*NGS*JTAU*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(5)=LMAX8+1
      CALL READDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE ULIGE1(NOP,NDST,MATIG,DST,CTR,CTRP,MCVEL,NMA,NMI,ICVEL)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA
C  ZA IZOPARAMETARSKI GREDNI ELEMENT
C
      CHARACTER*250 ACOZ
C
      include 'paka.inc'
      
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /SRPSKI/ ISRPS
      COMMON /IGRGLA/ NGRU,NGSU,NGTU,NGS,NTE,NUD,IANAL,JTAU,NWE,NT6,INEL
     *,MATG
      COMMON /IGREP/  LIGNOP,LIGNDS,LIGDST,LIGCTR,MXAUG,LMXAUG,LAG,MAXAE
     *               ,LICTRP,LIGX  ,LIGY  ,LIGZ,LAE,LLM,LIGMAT
C
      DIMENSION NOP(NE,*),NDST(NE,*),DST(NUD,*),CTR(NE,6,*),CTRP(NE,6,*)
     1,MATIG(*),MCVEL(*)
      COMMON /CDEBUG/ IDEBUG
      IF(IDEBUG.GT.0) PRINT *, ' ULIGE1'
C
      I=0
    5 I=I+1
      CALL ISPITA(ACOZ)
      IF(I.EQ.1) KARTI=KARTIC
      IF(INDFOR.EQ.1)
     *READ(IULAZ,*)NN,(NOP(NN,J),J=1,NTE),(NDST(NN,J),J=1,NTE),MATIG(NN)
      IF(INDFOR.EQ.2)
     *READ(ACOZ,15)NN,(NOP(NN,J),J=1,NTE),(NDST(NN,J),J=1,NTE),MATIG(NN)
C      IF(MATIG(NN).EQ.0) MATIG(NN)=MATIG(NN-1)
      IF(I.EQ.NE) GO TO 25
      GO TO 5
C
C STAMPANJE ELEMENATA U REDOSLEDU
C
   25 IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      CALL WBROJK(KARTI,0)
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2001)
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6001)
      DO 30 I=1,NE
      WRITE(IZLAZ,20)I,(NOP(I,J),J=1,NTE),(NDST(I,J),J=1,NTE),
     *MATIG(I)
   30 CONTINUE
      ENDIF
C
C UCITAVANJE POMERENOSTI REFERENTNE OSE
C
      DO 40 I=1,NUD
      CALL ISPITA(ACOZ)
      IF(I.EQ.1) KARTI=KARTIC
      IF(INDFOR.EQ.1)
     *READ(IULAZ,*)(DST(I,J),J=1,2)
      IF(INDFOR.EQ.2)
     *READ(ACOZ,45)(DST(I,J),J=1,2)
   40 CONTINUE
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      CALL WBROJK(KARTI,0)
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2030)
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6030)
      DO 42 I=1,NUD
      WRITE(IZLAZ,50)I,(DST(I,J),J=1,2)
   42 CONTINUE
      ENDIF
C
C KOORDINATE TACAKA ZA DEFINISANJE VEKTORA GLAVNIH OSA PRESEKA
C
      DO 64  I=1,NE
      DO 65  J=1,NTE
      CALL ISPITA(ACOZ)
      IF(I.EQ.1.AND.J.EQ.1) KARTI=KARTIC
      JI=NOP(I,J)
      IF(JI.EQ.0) GO TO 65
      IF(INDFOR.EQ.1)
     *READ(IULAZ,*)NN,JJ,(CTR(NN,J1,JJ),J1=1,6)
      IF(INDFOR.EQ.2) THEN
      READ(ACOZ,60)NN,JJ,(CTR(NN,J1,JJ),J1=1,6)
      ENDIF
C CTRP - POCETNE VREDNOSTI (NE MENJAJU SE)
      DO 191 J1=1,6
      CTRP(NN,J1,JJ)=CTR(NN,J1,JJ)
  191 CONTINUE
   65 CONTINUE
   64 CONTINUE
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
      CALL WBROJK(KARTI,0)
      IF(ISRPS.EQ.0)
     *WRITE(IZLAZ,2040)
      IF(ISRPS.EQ.1)
     *WRITE(IZLAZ,6040)
      DO 66  I=1,NE
      DO 67  J=1,NTE
      JJ=NOP(I,J)
      IF(JJ.EQ.0) GO TO 67
      WRITE(IZLAZ,70)I,J,(CTR(I,J1,J),J1=1,6)
   67 CONTINUE
   66 CONTINUE
      RETURN
      ENDIF
C
   15 FORMAT(16I5)
   20 FORMAT(I5,6X,8I6,I10)
   45 FORMAT(2F10.3)
   50 FORMAT(' ',8X,I5,D12.5,6X,D12.5)
   60 FORMAT(2I5,6F10.3)
   70 FORMAT(I6,I10,6X,3D10.3,4X,3D10.3)
C-----------------------------------------------------------------------
 2001 FORMAT(' U C I T A N I    P O D A C I   O   I Z O P A R A M E'
     *,' T A R S K I M   G R E D N I M   E L E M E N T I M A '//
     *' ','ELEM.BROJ  CVOR1 CVOR2 CVOR3 CVOR4 '
     *,             '  INDIKATORI POMERENOSTI  ',' MATERIJAL BROJ'/)
 2030 FORMAT(' P O D A C I   O   P O M E R E N O S T I   R E F E R E',
     *' N T N I H   O S A'//
     *' POMERENOST REFER. OSE     U PRAVCU - R      U PRAVCU - S'/)
 2040 FORMAT(' PODACI ZA DEFINISANJE JEDINICNIH VEKTORA U PRESEKU'//
     *' ELEM.BR.  CVOR BR.  KOORDINATE X,Y,Z, PRVE TACKE   ',
     *' KOORDINATE X,Y,Z, DRUGE TACKE'/)
C-----------------------------------------------------------------------
 6001 FORMAT(' R E A D I N G    D A T A     A B O U T     I S O P A '
     *,'R A M E T R I C     B E A M     E L E M E N T S '//
     *' ','ELEM. NUM.  NODE1 NODE2 NODE3 NODE4'
     *,        ' EXCENTRICITY INDICATOR','MATERIAL TYPE '/)
 6030 FORMAT(' P O D A C I   O   P O M E R E N O S T I   R E F E R E',
     *' N T N I H   O S A'//
     *'      TIP POMERENOSTI      U PRAVCU - R      U PRAVCU - S'/)
 6040 FORMAT(' PODACI ZA DEFINISANJE JEDINICNIH VEKTORA U PRESEKU'//
     *' ELEM.BR.  CVOR BR.  KOORDINATE X,Y,Z, PRVE TACKE   ',
     *' KOORDINATE X,Y,Z, PRVE TACKE'/)
C-----------------------------------------------------------------------
      END
C=======================================================================
      SUBROUTINE TGRAF4(NEL,MCVEL,ICVEL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     PROGRAM ZA STAMPANJE STAPA  U UNIVERZALNI FILE
CE     PROGRAM FOR PRINTOUT DATA IN UNIVERSAL GRAPHICS FILE
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /SUMELE/ ISUMEL,ISUMGR
      COMMON /SRPSKI/ ISRPS
      COMMON /NIDEAS/ IDEAS
C
      DIMENSION NEL(NE,*),MCVEL(*)
      IF(ideas.eq.-1) return
      ISUMGR=ISUMGR+1
      IF(ISRPS.EQ.0.AND.NCVE.GT.2) WRITE(IZLAZ,2200) NGE
      IF(ISRPS.EQ.1.AND.NCVE.GT.2) WRITE(IZLAZ,6200) NGE
C     GRAFICKI OPIS GREDE: SA 2 CVORA = 1
      IFGD=1
C     VRSTA  ELEMENTA: KRIVA GREDA SA 2 CVORA  = 23
      IFDI=23
C     TABELA FIZICKIH OSOBINA
      IPTN=ISUMGR
C     TABELA MATERIJALA
      MPTN=ISUMGR
C     BOJA  
      ICOL=7
C     BROJ CVOROVA NA ELEMENTU
      NNODS=2
      IND=-1
      ITYP=71
      WRITE(IGRAF,1100) IND
      WRITE(IGRAF,1100) ITYP
      DO 10 I=1,NE
C        REDNI BROJ ELEMENTA
         IEL=I+ISUMEL
         IF(ICVEL.EQ.1) IEL=MCVEL(I)
         WRITE(IGRAF,1000) IEL,IFGD,IFDI,IPTN,MPTN,ICOL,NNODS
         WRITE(IGRAF,1000) (NEL(I,J),J=1,2)
   10 CONTINUE
      WRITE(IGRAF,1100) IND
      ISUMEL=ISUMEL+NE
      RETURN
C
 1000 FORMAT(8I10)
 1100 FORMAT(I6)
C-----------------------------------------------------------------------
 2200 FORMAT(//' PROGRAM ZA GRAFICKO PRIKAZIVANJE REZULTATA "IDEAS"'/
     1' ZAHTEVA GREDU SA 2 CVORA PO ELEMENTU U GRUPI ELEMENATA NGE ='
     1,I5)
C-----------------------------------------------------------------------
 6200 FORMAT(//' GRAPHIC PACKAGE   "IDEAS"'/
     1' PERMITS ONLY BEAM WITH 2 NODES PER ELEMENT IN GROUP',
     1'  NGE =',I5)
C-----------------------------------------------------------------------
      END
