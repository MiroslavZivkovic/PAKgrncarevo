C=======================================================================
C
C   SUBROUTINE UL11EG
C              UL11EK
C              UL11E2
C              LMMH11
C              TGRA11
C
C=======================================================================
      SUBROUTINE UL11EG
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA ULAZNE PODATAKE ELASTICNIH OSLONACA
CE     MAIN PROGRAM FOR INPUT DATA FOR ELASTIC SUPPORTS
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
C
CS     REPERI U VEKTORU ZA ULAZNE PODATKE
CE     POINTERS IN INPUT VECTOR
C
      NCVE=2
      LAPRS=1
      LTBTH=LAPRS+NE*5*IDVA
      LTDTH =LTBTH
      IF(INDBTH.EQ.1) LTDTH=LTBTH+NE*IDVA
      LNMAT =LTDTH
      IF(INDDTH.EQ.1) LNMAT=LTDTH+NE*IDVA
      LISNA=LNMAT
      LNEL=LISNA+NE
      LCEL=LNEL
      IF(ICVEL.EQ.1) LNEL=LCEL+NE
      LAU=LMAX
C
CS     POZIVANJE PROGRAMA ZA ULAZNE PODATKE U VEKTOR AU
CE     CALL ROUTINES FOR INPUT DATA IN VECTOR  AU
C
      CALL UL11EK(A(LAU))
      NCVE3=NCVE*3
      MXAE = 6+12*IDVA
      LMAX = LMAX + MXAE
C
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE UL11EK(AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
CE     MENAGEMENT PROGRAM FOR INPUT DATA IN  AU  VECTOR
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /POSTPR/ LNDTPR,LNDTGR,NBLPR,NBLGR,INDPR,INDGR
      COMMON /RESTAR/ TSTART,IREST
      COMMON /MATERM/ LMODEL,LGUSM
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION AU(*)
      REAL AU
C
CS     POZIVANJE PROGRAMA ZA ULAZNE PODATKE
CE     CALL ROUTINE FOR INPUT DATA
C
      LL=NE*NCVE
      CALL ICLEAR(AU(LNEL),LL)
      NMA=0
      NMI=0
      CALL UL11E2(A(LCORD),AU(LNEL),AU(LISNA),AU(LAPRS),
     1            AU(LTBTH),AU(LTDTH),AU(LCEL),NMA,NMI,ICVEL)
C      IF(NBLGR.GE.0) CALL TGRA11(AU(LNEL),AU(LCEL),ICVEL)
      IF(ICVEL.EQ.1) CALL VEZACE(AU(LNEL),A(LELCV),NE,NCVE)
C
      NCVE3=NCVE*3
      LLMEL=LNEL+NE*NCVE
      LELC=LLMEL
      IF(ICVEL.EQ.1) THEN
        LLMEL=LELC+NMA-NMI+1
        CALL ICLEAR(AU(LELC),NMA-NMI+1)
        CALL VEZAEL(AU(LCEL),AU(LELC),NE,NMI)
      ENDIF
      LMXAU=LLMEL+NE*6
      MXAU = LMXAU - 1
      CALL DELJIV(MXAU,2,INDL)
      IF(INDL.EQ.1) MXAU=MXAU+1
      LMAX=LAU+MXAU
      IF (LMAX.LE.MTOT) GO TO 5
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2005) LMAX,MTOT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6005) LMAX,MTOT
      STOP
C
CS     FORMIRANJE VEKTORA LM
CE     FORM  LM  VECTOR
C
    5 LL=NE*6
      CALL ICLEAR(AU(LLMEL),LL)
      CALL LMMH11(A(LID),AU(LNEL),AU(LLMEL))
C
      LMAX8=LMAX8+1
      WRITE(IELEM,REC=LMAX8)
     1 NCVE,ITERME,MXAU,LNEL,LNMAT,LAPRS,LISNA,LLMEL,LCEL,LELC,NMA,NMI,
     1 INDBTH,INDDTH,LTBTH,LTDTH
      CALL WRITDD(AU(LAPRS),MXAU/IDVA,IELEM,LMAX8,LDUZI)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C
      LSIGMA=LMAX
      NPROS=4*NE*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      CALL CLEAR(A(LSIGMA),NPROS/IDVA)
      CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      RETURN
C-----------------------------------------------------------------------
 2005 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A ZA PODATKE O STAPOVIM
     1A'/' POTREBNA DIMENZIJA , LMAX=',I10/' RASPOLOZIVA DIMENZIJA,
     2NTOT =',I10)
 6005 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE UL11E2(CORD,NEL,ISNAP,APRS,
     1                  TBTH,TDTH,MCVEL,NMA,NMI,ICVEL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     PODPROGRAM ZA UCITAVANJE PODATAKA O STAPOVIMA
CE     SUBROUTINE FOR READING DATA ABOUT TRUSSES
C
      CHARACTER*250 ACOZ
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /BROJUK/ KARTIC,INDFOR,NULAZ
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION NEL(NE,*),ISNAP(*),APRS(5,*),
     1          TBTH(*),TDTH(*),MCVEL(*),AAPR(5),CORD(NP,*)
C
CS     P  O  D  A  C  I      O      E  L  E  M  E  N  T  I  M  A
CE     D  A  T  A      A  B  O  U  T      E  L  E  M  E  N  T  S
C
      IF(ISRPS.EQ.0.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,2000)
      IF(ISRPS.EQ.1.AND.(NULAZ.EQ.1.OR.NULAZ.EQ.3))
     1WRITE(IZLAZ,6000)
      NMATS=  1
      ISNAA=  0
CC      APRSS=  0.D0
      I = 0
      NAUT=0
    5 I = I + 1
      CALL ISPITA(ACOZ)
      IF(I.EQ.1) KARTI=KARTIC
      IF(INDFOR.EQ.1)
     1READ(IULAZ,*) NN,NMAT,ISNA,AAPR,KORC,BTH,DTH
      IF(INDFOR.EQ.2)
     1READ(ACOZ,1010) NN,NMAT,ISNA,AAPR,KORC,BTH,DTH
      IF(ICVEL.EQ.1) THEN
         MCVEL(I)=NN
         NI=NN
         IF(I.EQ.1) THEN
            NMA=NN
            NMI=NN
         ELSE
            IF(NMA.LT.NN) NMA=NN
            IF(NMI.GT.NN) NMI=NN
         ENDIF
         NN=I
      ENDIF
      CALL ISPITA(ACOZ)
      IF(INDFOR.EQ.1)
     1READ(IULAZ,*) (NEL(NN,J),J=1,2)
      IF(INDFOR.EQ.2)
     1READ(ACOZ,1000) (NEL(NN,J),J=1,2)
      IF(NMAT.EQ.0) NMAT=NMATS
      NMATS=NMAT
      IF(ISNA.EQ.0) ISNA=ISNAA
      ISNAA=ISNA
      IF(ISNA.LT.0) ISNA = 0
      ISNAP(NN)=ISNA
CC      IF(DABS(APR).LT.1.0D-10) APR=APRSS
CC      APRSS=APR
CC      APRS(NN)=APR
      IF(NEL(NN,2).GT.0) THEN
       DO 7 JJ=I,3
       II=JJ+2
 7     AAPR(II)=CORD(NEL(NN,2),JJ)
      ENDIF
C
      DO 11 K=1,5
11    APRS(K,NN)=AAPR(K)
      IF(INDBTH.EQ.1) TBTH(NN)=BTH
      IF(INDDTH.EQ.1) TDTH(NN)=DTH
      IF(ICVEL.EQ.1) IN=MCVEL(NN)
      IF(ICVEL.EQ.0) IN=NN
      IF(NULAZ.EQ.1.OR.NULAZ.EQ.3) THEN
C         WRITE(IZLAZ,5001) IN,(NEL(NN,J),J=1,2),ISNA,AAPR,KORC
         WRITE(IZLAZ,5001) IN,(NEL(NN,J),J=1,2),AAPR,KORC
         IF(INDBTH.EQ.1) THEN
         IF(DABS(TBTH(NN)).GT.1.0D-10) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2040) TBTH(NN)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6040) TBTH(NN)
         ENDIF
         ENDIF
         IF(INDDTH.EQ.1) THEN 
         IF(DABS(TDTH(NN)).GT.1.0D-10) THEN 
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2050) TDTH(NN)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6050) TDTH(NN)
         ENDIF
         ENDIF
      ENDIF
C
      IF(NAUT.GT.0) GO TO 30
      IF(KORC.NE.0) GO TO 20
      IF(I.EQ.NE) GO TO 50
      GO TO 5
C
   20 NAUT=1
      IF(ICVEL.EQ.1) N1=NI
      IF(ICVEL.EQ.0) NN1=NN
      KORA=KORC
      GO TO 5
C
CS     AUTOMATSKO GENERISANJE PODATAKA IZMEDJU CVOROVA N1 I N2
CE     AUTOMATIC  GENERATION BETWEN NODES  N1  AND  N2
C
   30 NN2=NN
      IF(ICVEL.EQ.1) THEN
         NN1=NN-1
         N2=NI
      ENDIF
      N11=NEL(NN1,1)
      N22=NEL(NN2,1)
      N12=N22-N11
      CALL DELJIV(N12,KORA,INDD)
      IF(INDD.EQ.1) GO TO 100
      IF(ICVEL.EQ.1) NNN=N2-N1-1
      IF(ICVEL.EQ.0) NNN=NN2-NN1-1
      NGG=N12/KORA-1
      IF(NNN.NE.NGG) GO TO 150
      IAUT=N12/KORA-1
      IF(ICVEL.EQ.1) THEN
         NM=NN+IAUT
         MCVEL(NM)=MCVEL(NN)
         ISNAP(NM)=ISNAP(NN)
	 DO 25 II=1,5
  25     APRS(II,NM)=APRS(II,NN)
         IF(INDBTH.EQ.1) TBTH(NM) = TBTH(NN)
         IF(INDDTH.EQ.1) TDTH(NM) = TDTH(NN)
         DO 32 II=1,2
            NEL(NM,II)=NEL(NN,II)
   32    CONTINUE
      ENDIF
      DO 34 J=1,IAUT
         JJ=NN1+J
         IF(ICVEL.EQ.1) THEN
            N1=N1+1
            MCVEL(JJ)=N1
         ENDIF
         ISNAP(JJ)=ISNAP(NN1)
         NEL(JJ,2)=NEL(NN1,2)
	 DO 37 II=1,5
  37     APRS(II,JJ)=APRS(II,NN1)
         IF(INDBTH.EQ.1) TBTH(JJ) = TBTH(NN1)
         IF(INDDTH.EQ.1) TDTH(JJ) = TDTH(NN1)
C         DO 35 II=1,2
            NODP=NEL(JJ-1,1)
            NEL(JJ,1)=NODP+KORA
            IF(NODP.EQ.0) NEL(JJ,1) = 0
C   35    CONTINUE
   34 CONTINUE
      IF(ICVEL.EQ.1) I=NM
      IF(ICVEL.EQ.0) I=I+IAUT
      IF(I.EQ.NE) GO TO 50
      NAUT=0
      IF(KORC.EQ.0) GO TO 5
      KORA=KORC
      NAUT=1
      IF(ICVEL.EQ.1) N1=N2
      IF(ICVEL.EQ.0) NN1=NN2
      GO TO 5
C
CS     STAMPANJE UCITANIH I GENERISANIH PODATAKA
CE     PRINT INPUT AND GENERATED DATA
C
   50 IF(NULAZ.NE.1.AND.NULAZ.NE.3) RETURN
      CALL WBROJK(KARTI,0)
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2140)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6140)
      DO 70 I=1,NE
         IF(ICVEL.EQ.1) IN=MCVEL(I)
         IF(ICVEL.EQ.0) IN=I
         WRITE(IZLAZ,5001) IN,(NEL(I,J),J=1,2),
     1                     (APRS(K,I),K=1,5)
         IF(INDBTH.EQ.1) THEN
         IF(DABS(TBTH(I)).GT.1.0D-10) THEN
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2040) TBTH(I)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6040) TBTH(I)
         ENDIF
         ENDIF
         IF(INDDTH.EQ.1) THEN 
         IF(DABS(TDTH(I)).GT.1.0D-10) THEN 
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2050) TDTH(I)
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6050) TDTH(I)
         ENDIF
         ENDIF
   70 CONTINUE
      RETURN
C
  150 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2110) NN1,NN2,NGG
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6110) NN1,NN2,NGG
      STOP
  100 CONTINUE
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2100) N22,N11,KORA
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6100) N22,N11,KORA
      STOP
C
 1000 FORMAT(14I5)
 1010 FORMAT(3I5,5F10.3,I5,2F10.3)
 5001 FORMAT(10X,I5,4X,2I6,5X,2(1PD10.3),5X,3(1PD10.3),I5)
C 5001 FORMAT(11X,I5,20X,I6,8X,I6,12X,I6,14X,5(1PD10.3),I6)
C-----------------------------------------------------------------------
 2000 FORMAT(///6X,'U C I T A N I    P O D A C I    O    O S L O N C I M
     1 A'/6X,54('-')///
     111X,'(MOGUCE GENERISANJE PODATAKA O ELEMENTIMA,'/ 
     212X,'UCITATI POTREBAN BROJ KARTICA ZA DEF. SVIH ELEMENATA)'///11X,
     3'ELEMENT',4X,'CVOR',3X,'IND',11X,'KRUTOST',20X,'ORIJENTACIJA',11X,
     4'KORAK'/12X,'BROJ',20X,'  TRANSL.    ROTAC.')
 2100 FORMAT(///' BROJ CVORA N2=',I5,' NE MOZE SE DOBITI SABIRANJEM BROJ
     1A CVORA N1=',I5,' I KONACNOG BROJA KORAKA KORA=',I5)
 2110 FORMAT(///' IZMEDJU ELEMENATA N1=',I5,' I N2=',I5,' NEMA NG=',I5,
     1' ELEMENATA KOJE TRBA GENERISATI')
 2140 FORMAT(6X,'G E N E R I S A N I    P O D A C I    O    O S L O N C
     1I M A'/6X,60('-')///11X,
     2'ELEMENT',4X,'CVOR',3X,'IND',11X,'KRUTOST',20X,'ORIJENTACIJA',13X,
     3/12X,'BROJ',20X,'    TRANSL.   ROTAC.')
 2040 FORMAT(/' VREME NASTAJANJA ELEMENTA - TBTH =',1PD10.3)
 2050 FORMAT(/' VREME NESTAJANJA ELEMENTA - TDTH =',1PD10.3)
C-----------------------------------------------------------------------
 6000 FORMAT(///6X,'E N T E R E D  D A T A   F O R  F O U N D A T I O N 
     1S'/6X,54('-')///11X,
     3'ELEMENT',4X,'NODE',3X,'IND',11X,'STIFFNES',19X,'ORIENTATION',12X,
     4'STEP '/11X,'NUMBER',20X,'  TRANSL.    ROTAT.')
 6100 FORMAT(///' NODE NUMBER N2=',I5,' CAN NOT BE DEFINED BY SUPERPOSIT
     1ION OF NODE N1=',I5,' WITH A NUMBER OF STEPS  KORA=',I5)
 6110 FORMAT(///' AMONG ELEMENTS N1=',I5,' AND N2=',I5,' THERE IS NO  ',
     1'NG=',I5,' ELEMENTS TO GENERATE')
 6140 FORMAT(6X,'G E N E R A T E D   D A T A   F O R   F O U N D A T I O
     1 N S '/6X,60('-')///11X,
     3'ELEMENT',4X,'NODE',3X,'IND',11X,'STIFFNES',19X,'ORIENTATION',11X,
     4/11X,'NUMBER',20X,'  TRANSL.    ROTAT.')
 6040 FORMAT(/' ELEMENT BIRTH TIME - TBTH =',1PD10.3)
 6050 FORMAT(/' ELEMENT DEATH TIME - TDTH =',1PD10.3)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE LMMH11(ID,NEL,LMEL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE VEKTORA LM I VISINA STUBOVA (MHT)
CE     OBTAIN VECTOR  LM   AND COLUMN HEIGHTS
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /GEORGE/ TOLG,ALFAG,ICCGG
      COMMON /DRAKCE/ IDRAKCE,NELUK,NZERO,NEED1,NEED2,NEED3,NNZERO
     1                ,IROWS,LAILU,LUCG,LVCG,LWCG,LPCG,LRCG
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
C
      DIMENSION ID(NP,*),NEL(NE,*),LMEL(6,*)
C     PETLJA PO ELEMENTIMA
      NC=1
      DO 100 NLM=1,NE
      IF(NEL(NLM,NC).EQ.0) GO TO 10
      JJ=NEL(NLM,NC)
      DO 20 I=1,6
   20 LMEL(I,NLM)=ID(JJ,I)
   10 CONTINUE
C     FORMIRANJE VISINA STUBOVA
      ND=3
      CALL VISINE(A(LMHT),ND,LMEL(1,NLM))
      IF (IABS(ICCGG).EQ.1) THEN
         WRITE(IDRAKCE) ND,(LMEL(I,NLM),I=1,ND)
         NELUK=NELUK+1
      ENDIF
      CALL VISINE(A(LMHT),ND,LMEL(4,NLM))
      IF (IABS(ICCGG).EQ.1) THEN
         WRITE(IDRAKCE) ND,(LMEL(I,NLM),I=1,ND)
         NELUK=NELUK+1
      ENDIF
  100 CONTINUE
      RETURN
      END
C=======================================================================
      SUBROUTINE TGRA11(NEL,MCVEL,ICVEL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     PROGRAM ZA STAMPANJE STAPA  U UNIVERZALNI FILE
CE     PROGRAM FOR PRINTOUT DATA IN UNIVERSAL GRAPHICS FILE
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /SUMELE/ ISUMEL,ISUMGR
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION NEL(NE,*),MCVEL(*)
      ISUMGR=ISUMGR+1
      IF(ISRPS.EQ.0.AND.NCVE.GT.2) WRITE(IZLAZ,2200) NGE
      IF(ISRPS.EQ.1.AND.NCVE.GT.2) WRITE(IZLAZ,6200) NGE
C     GRAFICKI OPIS STAPA: SA 2 CVORA = 1
      IFGD=1
C     VRSTA  ELEMENTA: STAP SA 2 CVORA  = 11
      IFDI=11
C     TABELA FIZICKIH OSOBINA
      IPTN=ISUMGR
C     TABELA MATERIJALA
      MPTN=ISUMGR
C     BOJA  
      ICOL=7
C     BROJ CVOROVA NA ELEMENTU
      NNODS=2
      IND=-1
      ITYP=71
      WRITE(IGRAF,1100) IND
      WRITE(IGRAF,1100) ITYP
      DO 10 I=1,NE
C        REDNI BROJ ELEMENTA
         IEL=I+ISUMEL
         IF(ICVEL.EQ.1) IEL=MCVEL(I)
         WRITE(IGRAF,1000) IEL,IFGD,IFDI,IPTN,MPTN,ICOL,NNODS
         WRITE(IGRAF,1000) (NEL(I,J),J=1,2)
   10 CONTINUE
      WRITE(IGRAF,1100) IND
      ISUMEL=ISUMEL+NE
      RETURN
C
 1000 FORMAT(8I10)
 1100 FORMAT(I6)
C-----------------------------------------------------------------------
 2200 FORMAT(//' PROGRAM ZA GRAFICKO PRIKAZIVANJE REZULTATA "IDEAS"'/
     1' ZAHTEVA STAP SA 2 CVORA PO ELEMENTU U GRUPI ELEMENATA NGE ='
     1,I5)
C-----------------------------------------------------------------------
 6200 FORMAT(//' GRAPHIC PACKAGE   "IDEAS"'/
     1' PERMITS ONLY ROD WITH 2 NODES PER ELEMENT IN GROUP',
     1'  NGE =',I5)
C-----------------------------------------------------------------------
      END
