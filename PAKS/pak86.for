C=======================================================================
C
C   SUBROUTINE STRUL8
C              BETL18
C              KNL8
C=======================================================================
      SUBROUTINE STRUL8(H,NEL,LM,DRGTM0,U,STRAIN,T,DEB)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     UKUPNE DEFORMACIJE ZA   U.L.
CE     TOTAL STRAINS FOR U.L.
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /GRADPO/ EL11,EL12,EL13,EL21,EL22,EL23,EL31,EL32,EL33
C
      DIMENSION H(9,*),NEL(NE,*),U(*),STRAIN(*),DRGTM0(9,*),LM(*)
C
CS....  KOEFICIJENTI L
CE      COEFFICIENT L 
C
      EL11=0.D0
      EL12=0.D0
      EL13=0.D0
      EL21=0.D0
      EL22=0.D0
      EL23=0.D0
      EL31=0.D0
      EL32=0.D0
      EL33=0.D0
      D05=DEB*0.5
C
      DO 10 J=1,NCVE
      IF(NEL(NLM,J).EQ.0) GO TO 10
      J3=(J-1)*3
      KU1=J3+1
      KU2=J3+2
      KU3=J3+3
      AHX=XJ(1,1)*H(J,2)+XJ(1,2)*H(J,3)
      AHY=XJ(2,1)*H(J,2)+XJ(2,2)*H(J,3)
      AHZ=XJ(3,1)*H(J,2)+XJ(3,2)*H(J,3)
      TAX=D05*(T*AHX+XJ(1,3)*H(J,1))
      TAY=D05*(T*AHY+XJ(2,3)*H(J,1)) 
      TAZ=D05*(T*AHZ+XJ(3,3)*H(J,1))
      IF(LM(KU1).EQ.0) GO TO 15
      EL11=EL11+AHX*U(KU1)
      EL12=EL12+AHY*U(KU1)
      EL13=EL13+AHZ*U(KU1)
   15 EL11=EL11+TAX*DRGTM0(J,1)
      EL12=EL12+TAY*DRGTM0(J,1)
      EL13=EL13+TAZ*DRGTM0(J,1)
      IF(LM(KU2).EQ.0) GO TO 16
      EL21=EL21+AHX*U(KU2)
      EL22=EL22+AHY*U(KU2)
      EL23=EL23+AHZ*U(KU2)
   16 EL21=EL21+TAX*DRGTM0(J,2)
      EL22=EL22+TAY*DRGTM0(J,2)
      EL23=EL23+TAZ*DRGTM0(J,2)
      IF(LM(KU3).EQ.0) GO TO 17
      EL31=EL31+AHX*U(KU3)
      EL32=EL32+AHY*U(KU3)
      EL33=EL33+AHZ*U(KU3)
   17 EL31=EL31+TAX*DRGTM0(J,3)
      EL32=EL32+TAY*DRGTM0(J,3)
      EL33=EL33+TAZ*DRGTM0(J,3)
   10 CONTINUE
C
C.... DEFORMACIJE UKUPNE
C
C  LINEARNE - SLUZE ZA TESTIRANJE
C      STRAIN(1)=EL11
C      STRAIN(2)=EL22
C      STRAIN(3)=EL33
C      STRAIN(4)=EL12+EL21
C      STRAIN(5)=EL23+EL32
C      STRAIN(6)=EL13+EL31
CS     NELINEARNE DEFORMACIJE
CE     NONLINEAR STRAINS
      STRAIN(1)=EL11-(EL11*EL11+EL21*EL21+EL31*EL31)/2.D0
      STRAIN(2)=EL22-(EL12*EL12+EL22*EL22+EL32*EL32)/2.D0
      STRAIN(3)=EL33-(EL13*EL13+EL23*EL23+EL33*EL33)/2.D0
      STRAIN(4)=EL12+EL21-EL11*EL12-EL21*EL22-EL31*EL32
      STRAIN(5)=EL23+EL32-EL12*EL13-EL22*EL23-EL32*EL33
      STRAIN(6)=EL13+EL31-EL11*EL13-EL21*EL23-EL31*EL33
C
      RETURN
      END
C======================================================================
      SUBROUTINE BETL18(BET,NEL,LM,U,STRAIN,H,DRGTM0,GKS,T,DEB,IEPS)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     MATRICE BETL1 (BEL1 TRANSPONOVANO ZA T.L.)
CE     MATRIX  BETL1 (BEL1 TRANSPOSED FOR T.L.)
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /GRADPO/ EL11,EL12,EL13,EL21,EL22,EL23,EL31,EL32,EL33
C
      DIMENSION BET(6,*),NEL(NE,*),LM(*),U(*),STRAIN(*),
     1          H(9,*),DRGTM0(9,*),GKS(3,2,*)
CS.... DEFORMACIJE UKUPNE
CE     TOTAL STRAINS
      IF(IEPS.EQ.1)THEN
        STRAIN(1)=EL11+(EL11*EL11+EL21*EL21+EL31*EL31)/2.D0
        STRAIN(2)=EL22+(EL12*EL12+EL22*EL22+EL32*EL32)/2.D0
        STRAIN(3)=EL33+(EL13*EL13+EL23*EL23+EL33*EL33)/2.D0
        STRAIN(4)=EL12+EL21+EL11*EL12+EL21*EL22+EL31*EL32
        STRAIN(5)=EL23+EL32+EL12*EL13+EL22*EL23+EL32*EL33
        STRAIN(6)=EL13+EL31+EL11*EL13+EL21*EL23+EL31*EL33
C
      RETURN
      ENDIF
C....  KOEFICIJENTI L
CE     COEFFICIENTS L 
      EL11=0.D0
      EL12=0.D0
      EL13=0.D0
      EL21=0.D0
      EL22=0.D0
      EL23=0.D0
      EL31=0.D0
      EL32=0.D0
      EL33=0.D0
      D05=DEB*0.5
C
      JJ=-2
      DO 10 J=1,NCVE
      JJ=JJ+3
      IF(NEL(NLM,J).EQ.0) GO TO 10
      J3=(J-1)*3
      KU1=J3+1
      KU2=J3+2
      KU3=J3+3
C
      AHX=BET(1,JJ)
      AHY=BET(4,JJ)
      AHZ=BET(6,JJ)
      TAX=D05*(T*AHX+XJ(1,3)*H(J,1))
      TAY=D05*(T*AHY+XJ(2,3)*H(J,1)) 
      TAZ=D05*(T*AHZ+XJ(3,3)*H(J,1))
C
      IF(LM(KU1).EQ.0) GO TO 15
      EL11=EL11+AHX*U(KU1)
      EL12=EL12+AHY*U(KU1)
      EL13=EL13+AHZ*U(KU1)
   15 EL11=EL11+TAX*DRGTM0(J,1)
      EL12=EL12+TAY*DRGTM0(J,1)
      EL13=EL13+TAZ*DRGTM0(J,1)
      IF(LM(KU2).EQ.0) GO TO 16
      EL21=EL21+AHX*U(KU2)
      EL22=EL22+AHY*U(KU2)
      EL23=EL23+AHZ*U(KU2)
   16 EL21=EL21+TAX*DRGTM0(J,2)
      EL22=EL22+TAY*DRGTM0(J,2)
      EL23=EL23+TAZ*DRGTM0(J,2)
      IF(LM(KU3).EQ.0) GO TO 17
      EL31=EL31+AHX*U(KU3)
      EL32=EL32+AHY*U(KU3)
      EL33=EL33+AHZ*U(KU3)
   17 EL31=EL31+TAX*DRGTM0(J,3)
      EL32=EL32+TAY*DRGTM0(J,3)
      EL33=EL33+TAZ*DRGTM0(J,3)
   10 CONTINUE
C----------------------------------------------
      JJ=-2
      JF=NCVE*3-2
C
      DO 40 I=1,NCVE
C
      JJ=JJ+3
      J1=JJ+1
      J2=JJ+2
      JF=JF+3
      JF1=JF+1
      IF(NEL(NLM,I).EQ.0) GO TO 40
C
      AHX=BET(1,JJ)
      AHY=BET(4,JJ)
      AHZ=BET(6,JJ)
      G1=T*AHX+XJ(1,3)*H(I,1)
      G2=T*AHY+XJ(2,3)*H(I,1) 
      G3=T*AHZ+XJ(3,3)*H(I,1)
      FI11=GKS(1,1,I)*EL11+GKS(2,1,I)*EL21+GKS(3,1,I)*EL31
      FI12=GKS(1,1,I)*EL12+GKS(2,1,I)*EL22+GKS(3,1,I)*EL32
      FI13=GKS(1,1,I)*EL13+GKS(2,1,I)*EL23+GKS(3,1,I)*EL33
      FI21=GKS(1,2,I)*EL11+GKS(2,2,I)*EL21+GKS(3,2,I)*EL31
      FI22=GKS(1,2,I)*EL12+GKS(2,2,I)*EL22+GKS(3,2,I)*EL32
      FI23=GKS(1,2,I)*EL13+GKS(2,2,I)*EL23+GKS(3,2,I)*EL33
C
      BET(1,JJ)=BET(1,JJ)+EL11*AHX
      BET(2,JJ)=EL12*AHY
      BET(3,JJ)=EL13*AHZ
      BET(4,JJ)=BET(4,JJ)+EL11*AHY+EL12*AHX
      BET(5,JJ)=EL12*AHZ+EL13*AHY
      BET(6,JJ)=BET(6,JJ)+EL11*AHZ+EL13*AHX
C
      BET(1,J1)=EL21*AHX
      BET(2,J1)=BET(2,J1)+EL22*AHY
      BET(3,J1)=EL23*AHZ
      BET(4,J1)=BET(4,J1)+EL21*AHY+EL22*AHX
      BET(5,J1)=BET(5,J1)+EL22*AHZ+EL23*AHY
      BET(6,J1)=EL21*AHZ+EL23*AHX
C
      BET(1,J2)=EL31*AHX
      BET(2,J2)=EL32*AHY
      BET(3,J2)=BET(3,J2)+EL33*AHZ
      BET(4,J2)=EL31*AHY+EL32*AHX
      BET(5,J2)=BET(5,J2)+EL32*AHZ+EL33*AHY
      BET(6,J2)=BET(6,J2)+EL31*AHZ+EL33*AHX
C.....  ROTACIJE
      BET(1,JF)=BET(1,JF)+FI11*G1
      BET(2,JF)=BET(2,JF)+FI12*G2
      BET(3,JF)=BET(3,JF)+FI13*G3
      BET(4,JF)=BET(4,JF)+FI12*G1+FI11*G2
      BET(5,JF)=BET(5,JF)+FI13*G2+FI12*G3
      BET(6,JF)=BET(6,JF)+FI13*G1+FI11*G3
C
      BET(1,JF1)=BET(1,JF1)+FI21*G1
      BET(2,JF1)=BET(2,JF1)+FI22*G2
      BET(3,JF1)=BET(3,JF1)+FI23*G3
      BET(4,JF1)=BET(4,JF1)+FI22*G1+FI21*G2
      BET(5,JF1)=BET(5,JF1)+FI23*G2+FI22*G3
      BET(6,JF1)=BET(6,JF1)+FI23*G1+FI21*G3
C
   40 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE KNL8(SKE,H,NEL,LM,TP,T,WTU,BNL,GKS,ND,MTR,TTR,IND6)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     NELINEARNI DEO MATRICE KRUTOSTI
CE     NONLINEAR PART OF STIFFNESS MATRIX
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
C
      DIMENSION SKE(*),H(9,*),NEL(NE,*),LM(*),TP(*),TE(9,9),BNL(9,*),
     1          GKS(3,2,*),SG(9),MTR(*)
C
      DO 38 I=1,9
      DO 38 J=I,9
   38 TE(I,J)=0.
      II=-3
      DO 39 I=1,3
      II=II+3
      DO 39 J=1,3
   39 TE(II+J,II+J)=TP(I)
      TE(1,4)=TP(4)
      TE(2,5)=TP(4)
      TE(3,6)=TP(4)
      TE(1,7)=TP(6)
      TE(2,8)=TP(6)
      TE(3,9)=TP(6)
      TE(4,7)=TP(5)
      TE(5,8)=TP(5)
      TE(6,9)=TP(5)
      DO 41 I=1,9
      DO 41 J=I,9
   41 TE(J,I)=TE(I,J)
C
CS     FORMIRANJE   BNL
CE     FORM    BNL
C
CS.....   DEO VEZAN ZA TRANSLACIJE
CE       CONNECTEDD TO TRANSLATIONS 
      JJ=-2
      JF=NCVE*3-2
      DO 40 I=1,NCVE
      JJ=JJ+3
      JJ1=JJ+1
      JJ2=JJ+2
      JF=JF+3
      IF(NEL(NLM,I).EQ.0) GO TO 40
C 
      DO 5 K=1,9
      BNL(K,JJ)=0.
      BNL(K,JJ1)=0.
   5  BNL(K,JJ2)=0.
C     D/DX
      BNL(1,JJ)=XJ(1,1)*H(I,2)+XJ(1,2)*H(I,3)
      BNL(2,JJ1)=BNL(1,JJ)
      BNL(3,JJ2)=BNL(1,JJ)
C     D/DY
      BNL(4,JJ)=XJ(2,1)*H(I,2)+XJ(2,2)*H(I,3)
      BNL(5,JJ1)=BNL(4,JJ)
      BNL(6,JJ2)=BNL(4,JJ)
C     D/DZ
      BNL(7,JJ)=XJ(3,1)*H(I,2)+XJ(3,2)*H(I,3)
      BNL(8,JJ1)=BNL(7,JJ)
      BNL(9,JJ2)=BNL(7,JJ)
C
CS     ROTACIJE
CE     ROTATIONS PART
C
      G1=T*BNL(1,JJ)+XJ(1,3)*H(I,1)
      G2=T*BNL(4,JJ)+XJ(2,3)*H(I,1) 
      G3=T*BNL(7,JJ)+XJ(3,3)*H(I,1)
      JF1=JF+1
      BNL(1,JF) =GKS(1,1,I)*G1
      BNL(2,JF) =GKS(2,1,I)*G1
      BNL(3,JF) =GKS(3,1,I)*G1
      BNL(1,JF1)=GKS(1,2,I)*G1
      BNL(2,JF1)=GKS(2,2,I)*G1
      BNL(3,JF1)=GKS(3,2,I)*G1
      BNL(4,JF) =GKS(1,1,I)*G2
      BNL(5,JF) =GKS(2,1,I)*G2
      BNL(6,JF) =GKS(3,1,I)*G2
      BNL(4,JF1)=GKS(1,2,I)*G2
      BNL(5,JF1)=GKS(2,2,I)*G2
      BNL(6,JF1)=GKS(3,2,I)*G2
      BNL(7,JF) =GKS(1,1,I)*G3
      BNL(8,JF) =GKS(2,1,I)*G3
      BNL(9,JF) =GKS(3,1,I)*G3
      BNL(7,JF1)=GKS(1,2,I)*G3
      BNL(8,JF1)=GKS(2,2,I)*G3
      BNL(9,JF1)=GKS(3,2,I)*G3
      IF(MTR(I).EQ.1)THEN
       JF2=JF+2
       DO 30 KZ=1,9
   30  BNL(KZ,JF2)=0.D0
      ENDIF
   40 CONTINUE
C**  TRANSFORMACIJA ZA LOKALNI SISTEM CVORA MATRICE B
      IF(IND6.GT.0)THEN
        ICD=9
        CALL TRAB(BNL,ICD,MTR,TTR,NCVE)
      ENDIF
C
CS     SKE NELINEARNO
CE     NONLINEAR SKE
C
      IJ=0
      DO 430 I=1,ND
      IF(LM(I).GT.0)THEN
        DO 420 M=1,9
        SG(M)=0.0
        DO 420 K=1,9
        SG(M)=SG(M)+BNL(K,I)*TE(K,M)
  420   CONTINUE
      ENDIF
      DO 430 J=I,ND
      IJ=IJ+1
      IF(LM(I).EQ.0.OR.LM(J).EQ.0) GO TO 430
      XX=0.0
      DO 440 K=1,9
  440 XX=XX+SG(K)*BNL(K,J)
      SKE(IJ)=SKE(IJ)+XX*WTU
  430 CONTINUE
C
      RETURN
      END
