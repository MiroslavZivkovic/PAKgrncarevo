C=======================================================================
C
C   SUBROUTINE S11EGL
C              READ11
C              SIST11
C              ELTE11
C=======================================================================
      SUBROUTINE S11EGL
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM ZA ELASTICNE OSLONCE
CE     MAIN PROGRAM FOR ELASTIC SUPPORTS
C
      include 'paka.inc'
      
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /DUPLAP/ IDVA
      COMMON /SRPSKI/ ISRPS
C
      LAU=LMAX
      CALL READ11(A(LAU),0)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      LAE=LMAX
      NCVE3= NCVE*3
      MXAE = 6+12*IDVA
      LMAX = LAE + MXAE
      IF(LMAX.LT.MTOT) GO TO 70
      IF(ISRPS.EQ.0)
     1WRITE(IZLAZ,2009) LMAX,MTOT
      IF(ISRPS.EQ.1)
     1WRITE(IZLAZ,6009) LMAX,MTOT
      STOP
C
C
C
   70 CALL SIST11(A(LAE),A(LAU))
C
      RETURN
C-----------------------------------------------------------------------
 2009 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A ZA MATRICE ELEMENATA'
     1/' POTREBNA DIMENZIJA   , LMAX=',I10
     2/' RASPOLOZIVA DIMENZIJA, MTOT=',I10)
 6009 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE READ11(AU,INA)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
CE     MENAGEMENT ROUTINE FOR INPUT DATA IN    AU
C
      include 'paka.inc'
      
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /CVOREL/ ICVEL,LCVEL,LELCV,NPA,NPI,LCEL,LELC,NMA,NMI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /ZAPISI/ LSTAZA(5)
C
      DIMENSION AU(*)
      REAL AU
C
CS     CITANJE SKALARA IZ DIREKT ACCES FILE 
CE     READ SCALARS FROM A DIRECT ACCESS FILE
C
      LSTAZA(1)=LMAX8
      READ(IELEM,REC=LMAX8)
     1 NCVE,ITERME,MXAU,LNEL,LNMAT,LAPRS,LISNA,LLMEL,LCEL,LELC,NMA,NMI,
     1 INDBTH,INDDTH,LTBTH,LTDTH
      LSTAZA(2)=LMAX8+1
      CALL READDD(AU(LAPRS),MXAU/IDVA,IELEM,LMAX8,LDUZI)
      LMAX=LAU+MXAU
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C
      LSIGMA=LMAX
      NPROS=4*NE*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(5)=LMAX8+1
      IF(INA.EQ.1) CALL READDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      IF(INA.EQ.0) CALL CLEAR(A(LSIGMA),NPROS/IDVA)
      RETURN
      END
C======================================================================
      SUBROUTINE SIST11(AE,AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     GLAVNI UPRAVLJACKI PROGRAM  ZA MATRICE ELEMENATA I SISTEMA
CE     MAIN MANAGEMENT  PROGRAM  FOR ELEMENT MATRIX
C
      include 'paka.inc'
      
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ELEMAU/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1 LMXAU,LAPRS
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TEMPCV/ LTECV,ITEMP
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /DUPLAP/ IDVA
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /BTHDTH/ INDBTH,INDDTH,LTBTH,LTDTH
      COMMON /UPDLAG/ LUL,LCORUL
C
      DIMENSION AE(*),AU(*)
      REAL AE,AU
C       REPERI U VEKTORU ELEMENATA AE
      NCVE3=3
      ND=6
      NDXY=3
      NWE=12
      LSKE=1
      LLM=LSKE+NWE*IDVA
      MXAE1=LLM+6
      LSKEXX=LSKE
      LSKEXY=LSKEXX+6*IDVA
      LLMXX=LLM
      LLMXY=LLM+3
C
CS            P E T L J A    P O    E L E M E N T I M A
CE            L O O P    O V E R    E L E M E N T S
C
      DO 100 NLM=1,NE
C
CS       NASTAJANJE I NESTAJANJE ELEMENATA
CE       ELEMENT BIRTH AND DEATH OPTION
C
         IBD=0
         CALL DTHBTH(AU(LTBTH),AU(LTDTH),VREME,NLM,IBD)
         IF(IBD.EQ.1) GO TO 100
C
      IZBR=MXAE/IDVA
      CALL CLEAR(AE,IZBR)
C
      KORD=LCORD
      CALL ELTE11(AE(LSKE),AE(LLM),AU(LNEL),
     1            AU(LAPRS),A(KORD),A(LRTDT),A(LFTDT),
     2            A(LSIGMA),AU(LLMEL),ND)
C
CS     RASPOREDJIVANJE MATRICE KRUTOSTI (SKE)
CE     ASSEMBLE STIFFNESS MATRIX
C
      IF(ISKNP.EQ.2) GO TO 100
      CALL SPAKUJ(A(LSK),A(LMAXA),AE(LSKEXX),AE(LLMXX),NDXY)
      CALL SPAKUJ(A(LSK),A(LMAXA),AE(LSKEXY),AE(LLMXY),NDXY)
  100 CONTINUE
C
      NPROS=4*NE*IDVA
      LMA8=LSTAZA(5)-1
      CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMA8,LDUZI)
      RETURN
      END
C=======================================================================
      SUBROUTINE ELTE11(SKE,LM,NEL,APRS,CORD,
     1                  RTDT,FTDT,SIGMA,LMEL,ND)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     FORMIRANJE MATRICA ELEMENATA I SISTEMA
CE     FORM ELEMENT MATRIX
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TEMPCV/ LTECV,ITEMP
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ANALIZ/ LINEAR,ITERGL,INDDIN
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /MPOINC/ MMP,NMPC,NEZAV,LCMPC,LMPC,NEZA1
      COMMON /SRPSKI/ ISRPS
C
      DIMENSION SKE(*),LM(*),NEL(NE,*),APRS(5,*),
     1    CORD(NP,*),RTDT(*),FTDT(*),SIGMA(NE,*),LMEL(ND,*)
      DIMENSION UE(6),FE(6),ST(3),D(3),DUE(3)
C
      SQRT(X)=DSQRT(X)
C
CS     VEKTOR LM, POMERANJA NA ELEMENTU
CE     VECTOR LM, ELEMENT DISPLACEMENTS
C
      DO 10 NC=1,ND
        K=LMEL(NC,NLM)
        LM(NC)=K
        IF(K.GT.0)THEN
          UE(NC)=RTDT(K)
          ELSEIF(K.EQ.0)THEN
            UE(NC)=0.D0
          ELSE
            UE(NC)=CONDOF(RTDT,A(LCMPC),A(LMPC),K)
        ENDIF
   10 CONTINUE
C
CS...... PETLJA PO GAUSOVIM TACKAMA 
CE...... LOOP OVER GAUSS POINTS
C
      NC1=NEL(NLM,1)
      NC2=NEL(NLM,2)
C      WRITE(3,*)'******* NC1,NC2   ',NC1,NC2
      IF(NC2.NE.0)THEN
        DL2=0.D0
        XL2=0.D0
        DO 503 L=1,3
        IF(NC2.GT.0)THEN
          D(L)=CORD(NC1,L)-CORD(NC2,L)
        ELSE
          D(L)=CORD(NC1,L)-APRS(L+2,NLM)
C      write(3,*)'D(L),CORD(NC1,L),APRS(L+2,NLM)  ',
C     1 D(L),CORD(NC1,L),APRS(L+2,NLM)  
        ENDIF
        XL2=XL2+D(L)*D(L)
        DUE(L)=D(L)+UE(L)
  503   DL2=DL2+DUE(L)*DUE(L)
        XL=SQRT(XL2)
        DL=SQRT(DL2)-XL
CS     PRAVAC OSLONCA
        DO 510 L=1,3
  510   ST(L)=D(L)/XL
      ELSE
        XL=1.D0
        ST(1)=APRS(3,NLM)
        ST(2)=APRS(4,NLM)
        ST(3)=APRS(5,NLM)
      ENDIF
C      write(3,*)'nc,st  ',NC1,ST
C      if(NC1.eq.2077) stop
CS     UKUPNA POMERANJA
CE     TOTAL DISPLACEMENT
      IF(ISKNP.EQ.1) GO TO 400
C     IF(IATYP.EQ.0.OR.IATYP.EQ.1) THEN
        STRXX=0.D0
        STRXY=0.D0
        DO 110 L=1,3
        STRXX=STRXX+ST(L)*UE(L)
        L3=L+3
  110   STRXY=STRXY+ST(L)*UE(L3)
C     ENDIF
C
CS     RACUNANJE SILA
CE     CALCULATE FORCE
C
        TAUXX=APRS(1,NLM)*STRXX
        TAUXY=APRS(2,NLM)*STRXY
        SIGMA(NLM,1)=TAUXX
        SIGMA(NLM,2)=TAUXY
        SIGMA(NLM,3)=STRXX
        SIGMA(NLM,4)=STRXY
C      WRITE(3,*)'UE',UE
C      WRITE(3,*)'STRXX,STRXY,TAUXX,TAUXY',STRXX,STRXY,TAUXX,TAUXY
C
CS   RACUNANJE UNUTRASNJIH SILA FTDT=BET*TAU
CE   INTERNAL FORCE
C
      IF(NGENL.EQ.0) GO TO 400
c      BETTXX=TAUXX*APRS(1,NLM)
c      BETTXY=TAUXY*APRS(2,NLM)
      BETTXX=TAUXX
      BETTXY=TAUXY
      DO 310 L=1,3
      IF(LM(L).NE.0)  FE(L) =ST(L)*BETTXX
      L3=L+3
  310 IF(LM(L3).NE.0) FE(L3)=ST(L)*BETTXY
C
C    RAZMESTANJE SILA U GLOBALNI VEKTOR   FTDT
C
      CALL SILPAK(FTDT,FE,LM,ND,A(LCMPC),A(LMPC))
C
CS     INTEGRACIJA SKE
CE     ELEMENT STIFFNESS MATRIX
C
  400 IF(ISKNP.EQ.2) GO TO 50
CS.....  LINEARNI DEO
CE       LINEAR PART
      XX=APRS(1,NLM)*XL
      KL=0
      DO 600 L=1,3
      YY=ST(L)*XX
        DO 600 K=L,3
        KL=KL+1
  600   SKE(KL)=ST(K)*YY
      XY=APRS(2,NLM)*XL
      DO 605 L=1,3
      YY=ST(L)*XY
        DO 605 K=L,3
        KL=KL+1
  605   SKE(KL)=ST(K)*YY
C
   50 CONTINUE
      RETURN
      END
