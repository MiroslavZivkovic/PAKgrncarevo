C=======================================================================
C
C=======================================================================
      SUBROUTINE GELBOI(TAU,SKE,FTDT,T,WTU,GKS,LM,MSL,NOP,DRGTM0,DEB,
     &                  TGT,LPLAS,LPLA1,ISNA,MTR,TTR,IND6,CORD,COR,GMT,
     &                  R,S,N45,DEF)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.      TO FORM ELEMENT MATRIX FOR SHELL ELEMENT - GELIN & BOISSE
CS.   P R O G R A M
CS.      ZA FORMIRANJE MATRICA ELEMENATA LJUSKE - GELIN & BOISSE
C .
C .      IPODT.EQ.3; (3 CVORA)
C .
C ......................................................................
C
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /IZLE4B/ H(9,3),GM(3,9),BLT(6,54),BE(9,54),ETP(6,6),UEL(54)
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ANALIZ/ LINEAR,ITERGL,INDDIN
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /VELIKE/ LCOR0,LGM0,JG,NGR,NGS,NGT,NGS4
      COMMON /INDNAP/ NAPON
      COMMON /FILTER/ TOLNAP,TOLPOM
      COMMON /ZADATA/ LNZADJ,LNZADF,LZADFM,NZADP
      COMMON /MATIZO/ E,V
      COMMON /MATANI/ EX,EY,EZ,VXY,VYZ,VZX,GXY,GYZ,GZX
C
      DIMENSION NOP(NE,*),SKE(*),LM(*),FTDT(*),DEF(N45,NGS12,NE,*),
     2TAU(N45,NGS12,NE,*),TA(6),STRAIN(6),STRESS(6)
      DIMENSION GKS(3,2,*)
      DIMENSION DRGTM0(9,*),DEB(*),BNL(9,54)
      DIMENSION TRLN(6,6),CORD(NP,*),COR(9,*),GMT(3,*)
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' GELBOI'
C
CS    JAKOBIJAN - XJJ, INVERTOVAN JAKOBIJAN - XJ
CE    JACOBIAN - XJJ, INVERSE JACOBIAN - XJ
C
      CALL JEDNA1(XJJ,XJ,9)
      CALL MINV3(XJ,DET)
C
CS    MATRICA TRANSFORMACIJE - TRLN (KOVARIJANTNI - GLOBALNI DEKARTOV)
CE    TRANSFORMATION MATRIX - TRLN (COVARIANT - GLOBAL CARTESIAN)
C
      CALL TRANSE(TRLN,XJ)
C
CS    FORMIRANJE MATRICE BL LINEARNO  (BLT)
CE    FORM LINEAR BL MATRIX (BLT)
C
      CALL BTBEG(H,BLT,NOP,GKS,T,MTR,COR,GM,BT2,BT3,DC2,DC3)
C
CS    TRANSFORMACIJA MATRICE - BLT (KOVARIJANTNI - GLOBALNI DEKARTOV)
CE    TRANSFORM MATRIX - BLT (COVARIANT - GLOBAL CARTESIAN)
C
      CALL TRANS4(BLT,TRLN,ND)
C
C
CS    FORMIRANJE MATRICE BL1 GEOMETRIJSKA NELINEARNOST - T.L.
CE    FORM MATRIX BL1 GEOMETRICAL NONLINEARITY -  T.L.
C
      IF(IATYP.EQ.2)
     1CALL BETL18(BLT,NOP,LM,UEL,STRAIN,H,DRGTM0,GKS,T,DEB(NLM),0)
C
CS    TRANSFORMACIJA MATRICE - BLT ZA LOKALNI SISTEM CVORA
CE    TRANSFORM MATRIX - BLT FOR LOCAL NODAL SISTEM
C
      IF(IND6.GT.0) THEN
         ICD=6
         CALL TRAB(BLT,ICD,MTR,TTR,NCVE)
      ENDIF
C
CS    MATRICA TRANSFORM. DEFORMACIJA - TSG (GLOBALNI - LOKALNI DEKARTOV)
CE    STRAIN TRANSFORMATION MATRIX - TSG (GLOBAL - LOCAL CARTESIAN)
C
      CALL TRANAL(XJJ,TSG,0)
C
      IF(ISKNP.NE.1) THEN
C
C
CS       RACUNANJE DEFORMACIJA U GLOBALNOM DEKARTOVOM
CE       CALCULATE STRAINS IN GLOBAL CARTESIAN
C
C
         CALL CLEAR(STRAIN,6)
C
CS       LINEARNOST I M.N.O.
CE       LINEAR PART AND M.N.O.
C
         IF(IATYP.EQ.0.OR.IATYP.EQ.1)
     1   CALL MNOZI1(STRAIN,BLT,UEL,6,ND)
C
CS       GEOMETRIJSKA NELINEARNOST - UKUPNE DEFORMACIJE ZA T.L. I U.L.
CE       GEOMETRICAL NONLINEARITY - TOTAL STRAIN FOR T.L. AND U.L.
C
         IF(IATYP.EQ.2)
     1   CALL BETL18(BLT,NOP,LM,UEL,STRAIN,H,DRGTM0,GKS,T,DEB(NLM),1)
         IF(IATYP.EQ.3)
     &   CALL TUKDEF(COR,GM,GMT,UEL,H,R,S,T,STRAIN,TRLN,TSS,BT2,BT3,DC2,
     &               DC3)
C
CS       TRANSFORMACIJA DEFORMACIJA (GLOBALNI - LOKALNI DEKARTOV)
CE       TRANSFORM STRAIN (GLOBAL - LOCAL CARTESIAN) 
C
         CALL CLEAR(TA,6) 
         CALL MNOZI1(TA,TSG,STRAIN,6,6)
         TA(3)=0.D0
         CALL JEDNA1(STRAIN,TA,6)
C
CS       TERMICKE DEFORMACIJE   ETH=ALFA*(T-T0)
CE       THERMAL STRAINS
C
         IF(NMODM.EQ.3.OR.NMODM.EQ.4) THEN
            DTGT0=TGT-TEMP0
            STRAIN(1)=STRAIN(1)-ALFA(1)*DTGT0
            STRAIN(2)=STRAIN(2)-ALFA(2)*DTGT0
            IF(NAPON.EQ.1) STRAIN(3)=ALFA(3)*DTGT0
            STRAIN(4)=STRAIN(4)-ALFA(4)*DTGT0
         ENDIF
C          CALL WRR(STRAIN,6,'STRA')
C
C
CS       RACUNANJE NAPONA
CE       CALCULATE STRESS
C
C
         CALL CLEAR(STRESS,6) 
C
         IF(NMODM.LT.5) THEN
C
CS          MATERIJALNA LINEARNOST
CE          MATERIAL LINEAR
C
CS          KOSIJEVI NAPONI U GLOBAL. ILI LOKAL. DEKART. SISTEMU
CE          CAUCHY STRESS IN GLOBAL OR LOCAL CARTESIAN SYSTEM
C
            CALL MNOZI1(STRESS,ETP,STRAIN,6,6)
C           CALL WRR(STRESS,6,'STRE')
C
CS          TRANSFORMACIJA NAPONA NA GLOBALNI SISTEM
CE          TRANSFORM STRESS TO GLOBAL COORDS.
C
            CALL CLEAR(TA,6) 
            CALL MNOZI2(TA,TSG,STRESS,6,6)
            IF(ISNA.EQ.2) CALL JEDNA1(STRESS,TA,6)
C
CS          CISCENJE NUMERICKIH GRESAKA ZA NAPONE
CE          CLEANING NUMERICAL ERRORS FOR STRESS
C
C           IF(IATYP.EQ.0) CALL CISTIN(STRESS,6)
C
            CALL JEDNA1(TAU(1,JG,NLM,MSL),STRESS,6)
C
            IF(NAPON.EQ.1) THEN
               IF(NMODM.EQ.1.OR.NMODM.EQ.3) THEN
                  STRAIN(3)=STRAIN(3)-V*(STRAIN(1)+STRAIN(2))/(1.-V)
               ENDIF
               IF(NMODM.EQ.2.OR.NMODM.EQ.4) THEN
                  DUM=(1.-VXY*VXY*EY/EX)
                  DUM1=(VZX*EX/EZ+VXY*VYZ)/DUM
                  DUM2=(VYZ+VXY*VZX*EY/EZ)/DUM
                  STRAIN(3)=STRAIN(3)-DUM1*STRAIN(1)-DUM2*STRAIN(2)
               ENDIF
            ENDIF
C
         ELSE
C
CS          NAPONI ZA PLASTICAN MODEL
CE          STRESS FOR MATERIAL NONLINEARITY
C
            IRAC=1
            CALL MODMA8(STRAIN,STRESS,NMODM,IRAC,LPLAS,LPLA1,TGT)
C
CS          TRANSFORMACIJA NAPONA NA GLOBALNI SISTEM
CE          TRANSFORM STRESS TO GLOBAL COORDS.
C
            CALL CLEAR(TA,6)
            CALL MNOZI2(TA,TSG,STRESS,6,6)
         ENDIF
C
C        DEFORMACIJE ZBOG KOREKCIJE DEBLJINE
C          CALL WRR(ETP,36,'ELAS')
         IF(NAPON.EQ.1.AND.NMODM.LT.5) THEN
            CALL JEDNA1(DEF(1,JG,NLM,MSL),STRAIN,6)
C            CALL WRR(STRAIN,6,'STRA')
         ENDIF
C
        IF(NGENL.GT.0.OR.(NZADP.GT.0.AND.ISKNP.EQ.2.AND.NGENL.EQ.0))THEN
C
CS          RACUNANJE UNUTRASNJIH SILA
CE          CALCULATE INTERNAL FORCES
C           r = BT * S
C
            CALL INTEGF(FTDT,BLT,TA,LM,WTU,ND,6)
C
         ENDIF
C
      ENDIF
C
C
CS    INTEGRACIJA MATRICE KRUTOSTI ELEMENTA - SKE
CE    INTEGRATE ELEMENT STIFFNESS MATRIX - SKE
C
C
      IF(ISKNP.EQ.2) RETURN
C
CS    TRANSFORM. MATRICE ELAST. - ETP (LOKALNI - GLOBALNI DEKART.)
CE    TRANSFORM ELASTICITY MATRIX - ETP (LOCAL - GLOBAL CARTESIAN)
C
      CALL TRAETP(ETP,ELAST,TSG)
C
CS    INTEGRACIJA MATRICE KRUTOSTI ELEMENTA - SKE
CE    INTEGRATE ELEMENT STIFFNESS MATRIX - SKE
C     SKE = BT * C * B
C
      CALL INTEGK(SKE,BLT,ELAST,LM,WTU,ND,6)
C
CS    GEOMETRIJSKI NELINEARAN DEO MATRICE KRUTOSTI
CE    GEOMETRIC NONLINEAR PART OF STIFFNESS MATRIX
C
      IF(IATYP.GT.1)
     1CALL KNL8(SKE,H,NOP,LM,TA,T,WTU,BNL,GKS,ND,MTR,TTR,IND6)
C
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE TGRGSG(CO,GN,X,H,T,NCVE)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.      TO CALCULATE DERIVATION CARTESIAN BY NATURAL COORDINATES
CE.      COVARIANT BASIS VECTORS - JACOBIAN MATRIX
CS.   P R O G R A M
CS.      ZA RACUNANJE IZVODA DEKARTOVIH PO PRIRODNIM KOORDINATAMA
CS.      KOVARIJANTNI BAZNI VEKTORI - JAKOBIJEVA MATRICA
C .
C ......................................................................
C
      DIMENSION X(3,*),CO(9,*),GN(3,*),H(9,*)
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' GRGSGT'
C
      DO 10 J=1,3
         X(1,J)=0.D0
         X(2,J)=0.D0
         X(3,J)=0.D0
      DO 10 K=1,NCVE
         X(1,J)=X(1,J)+H(K,2)*(CO(K,J)+T*GN(J,K))
         X(2,J)=X(2,J)+H(K,3)*(CO(K,J)+T*GN(J,K))
         X(3,J)=X(3,J)+H(K,1)*GN(J,K)
   10 CONTINUE
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE TIZVOD(CO,GN,X,XG,T,H,GMC)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R  O G R A M
CE.      TO CALCULATE DERIVATION CARTESIAN BY NATURAL COORDINATES
CS.   P R O G R A M
CS.      ZA RACUNANJE IZVODA DEKARTOVIH PO PRIRODNIM KOORDINATAMA
C .
C ......................................................................
C
      DIMENSION CO(9,*),GN(3,*),H(9,*),X(3,*),XG(3,3),CH(3,3),GMC(3,3)
      COMMON /CDEBUG/ IDEBUG
      DATA CH /0.5D0,0.5D0,0.0D0,0.5D0,0.0D0,0.5D0,0.0D0,0.5D0,0.5D0/
C
      IF(IDEBUG.GT.0) PRINT *, ' TIZVOD'
C
CS    KOVARIJANTNI BAZNI VEKTORI - JAKOBIJEVA MATRICA U GAUSOVOJ TACKI
CE    COVARIANT BASIS VECTORS - JACOBIAN MATRIX IN GAUSS POINT
C
      CALL TGRGSG(CO,GN,X,H,T,3)
C
CS    KOVARIJANTNI BAZNI VEKTORI NA SREDINAMA STRANICA ELEMENTA
CE    COVARIANT BASIS VECTORS AT MIDDLE ELEMENT PAGE
C
      DO 50 K=1,3
        XG(1,K)=CO(2,K)-CO(1,K)
        XG(2,K)=CO(3,K)-CO(1,K)
   50   XG(3,K)=CO(2,K)-CO(3,K)
      DO 72 I=1,3
        DO 72 J=1,3
   72     GMC(I,J)=CH(1,I)*GN(J,1)+CH(2,I)*GN(J,2)+CH(3,I)*GN(J,3)
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE TUKDEF(COR,GM,GMT,UEL,H,R,S,T,STRAIN,TRLN,TSS,BT2,BT3,
     &                  DC2,DC3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C ......................................................................
C .
CE.   P R O G R A M
CE.      TO CALCULATE TOTAL STRAIN FOR U.L.
CS.   P R O G R A M
CS.      ZA RACUNANJE UKUPNIH DEFORMACIJA ZA U.L.
C .
C ......................................................................
C
      DIMENSION COR(9,*),CON(9,3),GM(3,*),GMT(3,*),H(9,*),UEL(*),
     1          STRAIN(*),TRLN(6,*),TSS(6,*),STRAIK(6)
      DIMENSION XT(3,3),XGT(3,3)
      DIMENSION X0(3,3),XG0(3,3),GMC0(3,3),GMCT(3,3)
      COMMON /CDEBUG/ IDEBUG
C
      IF(IDEBUG.GT.0) PRINT *, ' TUKDEF'
C
CS       KOORDINATE U TRENUTKU - 0
CE       COORDINATE IN TIME - 0
      II=0
      DO 7 I=1,3
      DO 7 K=1,3
         II=II+1
         CON(I,K)=COR(I,K)-UEL(II)
    7 CONTINUE
CS       KOVARIJANTNI BAZNI VEKTORI U KONFIGURACIJI - T
CE       COVARIANT BASIS VECTORS IN CONFIGURATION - T
      CALL TIZVOD(CON,GMT,X0,XG0,T,H,GMC0)
CS       KOVARIJANTNI BAZNI VEKTORI U KONFIGURACIJI - T+DT
CE       COVARIANT BASIS VECTORS IN CONFIGURATION - T+DT
      CALL TIZVOD(COR,GM,XT,XGT,T,H,GMCT)
C
CS    IZOPARAMETARSKE UKUPNE KOVARIJANTNE DEFORMACIJE U GAUSOVOJ TACKI
CE    ISOPARAMETRIC TOTAL COVARIANT STRAIN IN GAUS POINT
      STRAIK(1)=.5D0*(XT(1,1)*XT(1,1)+XT(1,2)*XT(1,2)+XT(1,3)*XT(1,3)
     1               -X0(1,1)*X0(1,1)-X0(1,2)*X0(1,2)-X0(1,3)*X0(1,3))
      STRAIK(2)=.5D0*(XT(2,1)*XT(2,1)+XT(2,2)*XT(2,2)+XT(2,3)*XT(2,3)
     1               -X0(2,1)*X0(2,1)-X0(2,2)*X0(2,2)-X0(2,3)*X0(2,3))
      STRAIK(3)=0.D0
      STRAIK(4)=XT(1,1)*XT(2,1)+XT(1,2)*XT(2,2)+XT(1,3)*XT(2,3)
     1         -X0(1,1)*X0(2,1)-X0(1,2)*X0(2,2)-X0(1,3)*X0(2,3)
C
C     TRANSVERZALNE SMICUCE KOMPONENTE
C
      GAMAA=XGT(1,1)*GMCT(1,1)+XGT(1,2)*GMCT(1,2)+XGT(1,3)*GMCT(1,3)
     &     -XG0(1,1)*GMC0(1,1)+XG0(1,2)*GMC0(1,2)+XG0(1,3)*GMC0(1,3)
      GAMAB=XGT(2,1)*GMCT(2,1)+XGT(2,2)*GMCT(2,2)+XGT(2,3)*GMCT(2,3)
     &     -XG0(2,1)*GMC0(2,1)+XG0(2,2)*GMC0(2,2)+XG0(2,3)*GMC0(2,3)
      GAMAC=XGT(3,1)*GMCT(3,1)+XGT(3,2)*GMCT(3,2)+XGT(3,3)*GMCT(3,3)
     &     -XG0(3,1)*GMC0(3,1)+XG0(3,2)*GMC0(3,2)+XG0(3,3)*GMC0(3,3)
CS    MESOVITE UKUPNE KOVARIJANTNE DEFORMACIJE U GAUSOVOJ TACKI
CE    MIXED TOTAL COVARIANT STRAIN IN GAUSS POINT
      STRAIK(5)=H(2,1)*BT2*GAMAA+(H(1,1)+H(3,1))*GAMAB-
     &          H(2,1)*DC2*GAMAC
      STRAIK(6)=(H(1,1)+H(2,1))*GAMAA+H(3,1)*BT3*GAMAB+
     &          H(3,1)*DC3*GAMAC
C
CS    TRANSFORMACIJA DEFORMACIJA (KOVARIJANTNE - GLOBALNI DEKARTOV)
CE    TRANSFORM STRAIN (COVARIANT - GLOBAL CARTESIAN)
      CALL MNOZI1(STRAIN,TRLN,STRAIK,6,6)
      RETURN
      END
C=======================================================================
      SUBROUTINE BTBEG(H,BET,NEL,GKS,T,MTR,COR,GM,BT2,BT3,DC2,DC3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
CS     MATRICE BET (BE TRANSPONOVANO) KOVARIJANTNO
CE     MATRIX BET (BE TRANSPOSED) COVARIANT
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ORIENT/ CPP(3,3),XJJ(3,3),TSG(6,6),BETA,LBET0,IBB0
      COMMON /CDEBUG/ IDEBUG
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      DIMENSION H(9,*),BET(6,*),NEL(NE,*),GKS(3,2,*),MTR(*),XG(3,3),
     &          G4(3),COR(9,*),XGG(3,3),CH(3,3),GM(3,*),GMC(3,3)
      DATA CH /0.5D0,0.5D0,0.0D0,0.5D0,0.0D0,0.5D0,0.0D0,0.5D0,0.5D0/
C
      IF(IDEBUG.GT.0) PRINT *, ' BETBEG'
C
C     PARAMETRI ZA KOREKCIJU
C
      DO 50 K=1,3
        XG(1,K)=COR(3,K)-COR(1,K)
        XG(2,K)=COR(2,K)-COR(3,K)
   50   XG(3,K)=GM(K,3)
      CALL DETER3(XG,DC3)
      DO 60 K=1,3
        G4(K)=XG(1,K)
        XG(1,K)=COR(2,K)-COR(1,K)
   60   XG(3,K)=GM(K,2)
      CALL DETER3(XG,DC2)
      DO 70 K=1,3
        XG(2,K)=G4(K)
   70   G4(K)=COR(2,K)-COR(3,K)
      CALL DETER3(XG,DETC2)
      DO 71 K=1,3
   71   XG(3,K)=GM(K,3)
      CALL DETER3(XG,DETC3)
      BT2=DC3/DC2
      BT3=DC2/DC3
      DC2=DETC2/DC2
      DC3=DETC3/DC3
C
C     JAKOBIJAN ZA TACKE U SREDISNJOJ RAVNI XGG, INVERTOVAN XG
C
      DO 72 I=1,3
        DO 72 J=1,3
   72     GMC(I,J)=CH(1,I)*GM(J,1)+CH(2,I)*GM(J,2)+CH(3,I)*GM(J,3)
      DO 73 K=1,3
   73   XG(3,K)=GMC(3,K)
      CALL JEDNA1(XGG,XG,9)
      CALL MINV3(XG,DETG)
      GC1=XG(1,1)*G4(1)+XG(2,1)*G4(2)+XG(3,1)*G4(3)
      GC2=XG(1,2)*G4(1)+XG(2,2)*G4(2)+XG(3,2)*G4(3)
      GC3=XG(1,3)*G4(1)+XG(2,3)*G4(2)+XG(3,3)*G4(3)
C
CS     DEO VEZAN ZA TRANSLACIJE
CE     TRANSLATION PART
C
      JJ=-2
      JF=NCVE*3-2
      DO 40 I=1,NCVE
      JJ=JJ+3
      JF=JF+3
      IF(NEL(NLM,I).EQ.0) GO TO 40
C
C     D/DX
      BET(1,JJ  )=XJJ(1,1)*H(I,2)
      BET(1,JJ+1)=XJJ(1,2)*H(I,2)
      BET(1,JJ+2)=XJJ(1,3)*H(I,2)
C     D/DY
      BET(2,JJ  )=XJJ(2,1)*H(I,3)
      BET(2,JJ+1)=XJJ(2,2)*H(I,3)
      BET(2,JJ+2)=XJJ(2,3)*H(I,3)
C     D/DZ
      BET(3,JJ  )=0.D0
      BET(3,JJ+1)=0.D0
      BET(3,JJ+2)=0.D0
C     D/DY , D/DX
      BET(4,JJ  )=XJJ(1,1)*H(I,3)+XJJ(2,1)*H(I,2)
      BET(4,JJ+1)=XJJ(1,2)*H(I,3)+XJJ(2,2)*H(I,2)
      BET(4,JJ+2)=XJJ(1,3)*H(I,3)+XJJ(2,3)*H(I,2)
C     D/DZ , D/DY
      BET(5,JJ  )=(H(1,1)+H(3,1))*GMC(2,1)*H(I,3)
     &            -H(2,1)*DC2*GMC(3,1)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(2,1)*BT2*GMC(1,1)*H(I,2)
      BET(5,JJ+1)=(H(1,1)+H(3,1))*GMC(2,2)*H(I,3)
     &            -H(2,1)*DC2*GMC(3,2)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(2,1)*BT2*GMC(1,2)*H(I,2)
      BET(5,JJ+2)=(H(1,1)+H(3,1))*GMC(2,3)*H(I,3)
     &            -H(2,1)*DC2*GMC(3,3)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(2,1)*BT2*GMC(1,3)*H(I,2)
C     D/DZ , D/DX
      BET(6,JJ  )=(H(1,1)+H(2,1))*GMC(1,1)*H(I,2)
     &            +H(3,1)*DC3*GMC(3,1)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(3,1)*BT3*GMC(2,1)*H(I,3)
      BET(6,JJ+1)=(H(1,1)+H(2,1))*GMC(1,2)*H(I,2)
     &            +H(3,1)*DC3*GMC(3,2)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(3,1)*BT3*GMC(2,2)*H(I,3)
      BET(6,JJ+2)=(H(1,1)+H(2,1))*GMC(1,3)*H(I,2)
     &            +H(3,1)*DC3*GMC(3,3)*(GC1*H(I,2)+GC2*H(I,3))
     &            +H(3,1)*BT3*GMC(2,3)*H(I,3)
C
CS     ROTACIJE
CE     ROTATION PART
C
      G1V1=0.D0
      G1V2=0.D0
      G2V1=0.D0
      G2V2=0.D0
      G3V1=0.D0
      G3V2=0.D0
      CG1V1=0.D0
      CG1V2=0.D0
      CG2V1=0.D0
      CG2V2=0.D0
      AG3V1=0.D0
      AG3V2=0.D0
      BG3V1=0.D0
      BG3V2=0.D0
      CG3V1=0.D0
      CG3V2=0.D0
      CGSV1=0.D0
      CGSV2=0.D0
      DO 20 J=1,3
        G1V1=G1V1+XJJ(1,J)*GKS(J,2,I)
        G1V2=G1V2+XJJ(1,J)*GKS(J,1,I)
        G2V1=G2V1+XJJ(2,J)*GKS(J,2,I)
        G2V2=G2V2+XJJ(2,J)*GKS(J,1,I)
        G3V1=G3V1+XJJ(3,J)*GKS(J,2,I)
        G3V2=G3V2+XJJ(3,J)*GKS(J,1,I)
        CG1V1=CG1V1+XGG(1,J)*GKS(J,2,I)
        CG1V2=CG1V2+XGG(1,J)*GKS(J,1,I)
        CG2V1=CG2V1+XGG(2,J)*GKS(J,2,I)
        CG2V2=CG2V2+XGG(2,J)*GKS(J,1,I)
        AG3V1=AG3V1+GMC(1,J)*GKS(J,2,I)
        AG3V2=AG3V2+GMC(1,J)*GKS(J,1,I)
        BG3V1=BG3V1+GMC(2,J)*GKS(J,2,I)
        BG3V2=BG3V2+GMC(2,J)*GKS(J,1,I)
        CG3V1=CG3V1+GMC(3,J)*GKS(J,2,I)
        CG3V2=CG3V2+GMC(3,J)*GKS(J,1,I)
        CGSV1=CGSV1+G4(J)*GKS(J,2,I)
        CGSV2=CGSV2+G4(J)*GKS(J,1,I)
   20 CONTINUE
      JF1=JF+1
      BET(1,JF) =T*H(I,2)*G1V2
      BET(2,JF) =T*H(I,3)*G2V2
      BET(3,JF) =  H(I,1)*G3V2
      BET(1,JF1)=T*H(I,2)*G1V1
      BET(2,JF1)=T*H(I,3)*G2V1
      BET(3,JF1)=  H(I,1)*G3V1
      BET(4,JF) =T*(H(I,3)*G1V2+H(I,2)*G2V2)
      BET(5,JF) =(H(1,1)+H(3,1))*(T*H(I,3)*BG3V2+CG2V2*CH(I,2))
     &           -H(2,1)*DC2*(CGSV2+GC3*CG3V2)*CH(I,3)
     &           +H(2,1)*BT2*(T*H(I,2)*AG3V2+CG1V2*CH(I,1))
      BET(6,JF) =(H(1,1)+H(2,1))*(T*H(I,2)*AG3V2+CG1V2*CH(I,1))
     &           +H(3,1)*DC3*(CGSV2+GC3*CG3V2)*CH(I,3)
     &           +H(3,1)*BT3*(T*H(I,3)*BG3V2+CG2V2*CH(I,2))
      BET(4,JF1)=T*(H(I,3)*G1V1+H(I,2)*G2V1)
      BET(5,JF1)=(H(1,1)+H(3,1))*(T*H(I,3)*BG3V1+CG2V1*CH(I,2))
     &           -H(2,1)*DC2*(CGSV1+GC3*CG3V1)*CH(I,3)
     &           +H(2,1)*BT2*(T*H(I,2)*AG3V1+CG1V1*CH(I,1))
      BET(6,JF1)=(H(1,1)+H(2,1))*(T*H(I,2)*AG3V1+CG1V1*CH(I,1))
     &           +H(3,1)*DC3*(CGSV1+GC3*CG3V1)*CH(I,3)
     &           +H(3,1)*BT3*(T*H(I,3)*BG3V1+CG2V1*CH(I,2))
C      IF(MTR(I).EQ.1)THEN
       JF2=JF+2
       DO 30 KZ=1,6
   30  BET(KZ,JF2)=0.D0
C      ENDIF
   40 CONTINUE
      RETURN
      END
