C=======================================================================
C
C        INTEGRALJENJE MATRICA OSNOSIMETRICNE LJUSKE
C
C   SUBROUTINE L04EGL
C              READE7
C              SISTT7
C              ELTE7
C              MEL017
C              MEL027
C              MEL037
C              MEL047
C              MEL057
C              BETBE7
C              JACTE7
C              JACGA7
C              FORMP7
C              FTDTR7
C              JACTP7
C
C=======================================================================
      SUBROUTINE L04EGL
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C    GLAVNI PROGRAM ZA POZIVANJE PROGRAMA ZA RACUNANJE MATRICA ELEMENATA
C
      include 'paka.inc'
      
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMA7/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1                LBETA
      COMMON /DUPLAP/ IDVA
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
C
      LAU=LMAX
      CALL READE7(A(LAU),0)
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      LAE=LMAX
      NCVE3=NCVE*3
      MXAE = NCVE3+(MSLOJ+2*NCVE+8*NCVE3+NCVE3*(NCVE3+1)/2)*IDVA
      CALL DELJIV(MXAE,2,INDL)
      IF(INDL.EQ.1) MXAE=MXAE+1
      LMAX = LAE + MXAE
      IF(LMAX.LT.MTOT) GO TO 70
      WRITE(IZLAZ,2009) LMAX,MTOT
      STOP
C
C     FORMIRANJE MATRICE KRUTOSTI ELEMENATA I PAKOVANJE U SISTEM
C
   70 CALL SISTT7(A(LAE),A(LAU))
C
      RETURN
C-----------------------------------------------------------------------
 2009 FORMAT(///' NOT ENOUGH SPACE IN WORKING VECTOR  A'
     1/' REQUESTED DIMENSION , LMAX=',I10
     2/' AVAILABLE DIMENSION , MTOT=',I10)
CS 2009 FORMAT(///' NEDOVOLJNA DIMENZIJA U VEKTORU A '
CS     1/' POTREBNA DIMENZIJA, LMAX=',I10/
CS     2' RASPOLOZIVA DIMENZIJA, MTOT=',I10)
C-----------------------------------------------------------------------
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE READE7(AU,INA)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     GLAVNI UPRAVLJACKI PROGRAM ZA UCITAVANJE ULAZNIH PODATAKA U AU
C
      include 'paka.inc'
      
      COMMON /ELEMA7/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1                LBETA
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /DUPLAP/ IDVA
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
C
      DIMENSION AU(*)
      REAL AU
C
C     POZIVANJE PROGRAMA ZA ULAZNE PODATKE .
      LSTAZA(1)=LMAX8
      READ(IELEM,REC=LMAX8)
     1 MSLOJ,NGAUSX,NGAUSY,NCVE,ITERME,
     1 MXAU,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,LLMEL,LBETA
      LSTAZA(2)=LMAX8+1
      CALL READDD(AU(LTHID),MXAU/IDVA,IELEM,LMAX8,LDUZI)
      LMAX=LAU+MXAU
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
C     PROVERI
C     LPLAST=LMAX
C     LPLAS1=LMAX
C     LSIGMA=LMAX
      IF(NMODM.LE.4) GO TO 10
      STOP 'ELASTOPLASTIC MODEL IS NOT IMPLEMENTED FOR ELEMENT TYPE 7'
C     NPROS=NE*NGAUSX*NGAUSY*16*IDVA
C     LPLAS1=LPLAST+NPROS
C     LMAX=LPLAS1+NPROS
C     IF(LMAX.GT.MTOT) CALL ERROR(1)
C     LSTAZA(3)=LMAX8+1
C     CALL READDD(A(LPLAST),NPROS/IDVA,IELEM,LMAX8,LDUZI)
C     LSTAZA(4)=LMAX8+1
C     CALL READDD(A(LPLAS1),NPROS/IDVA,IELEM,LMAX8,LDUZI)
C     RETURN
   10 LSIGMA=LMAX
      NPROS=NE*MSLOJ*NGAUSX*NGAUSY*6*IDVA
      LMAX=LSIGMA+NPROS
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      LSTAZA(5)=LMAX8+1
      IF(INA.EQ.1) CALL READDD(A(LSIGMA),NPROS/IDVA,IELEM,LMAX8,LDUZI)
      IF(INA.EQ.0) CALL CLEAR(A(LSIGMA),NPROS/IDVA)
      RETURN
      END
C======================================================================
      SUBROUTINE SISTT7(AE,AU)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     GLAVNI UPRAVLJACKI PROGRAM  ZA MATRICE ELEMENATA I SISTEMA(KCAL=1)
C     RACUNANJE NAPONA (KCAL=2)
C
      include 'paka.inc'
      
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ELEMA7/ MXAU,LAU,LLMEL,LNEL,LNMAT,LTHID,LIPGC,LIPRC,LISNA,
     1                LBETA
      COMMON /ELEMAE/ MXAE,LAE,LMXAE,LHE,LBET,LBED,LRTHE,LSKE,LLM
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /ZAPISI/ LSTAZA(5)
      COMMON /DUPLAP/ IDVA
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
C
      DIMENSION AE(*),AU(*)
      REAL AE,AU
C
C     REPERI U VEKTORU ELEMENATA AE
C
      NCVE3=NCVE*3
      NWE=NCVE3*(NCVE3+1)/2
      LHE=1
      LBET=LHE+2*NCVE*IDVA
      LBED=LBET+4*NCVE3*IDVA
      LSKE=LBED+4*NCVE3*IDVA
      LTHSL=LSKE+NWE*IDVA
      LLM=LTHSL+MSLOJ*IDVA
C
C     OSNOVNA PETLJA PO ELEMENTIMA
C
      DO 100 NLM=1,NE
      IZBR=MXAE/IDVA
      CALL CLEAR(AE,IZBR)
C
C     RACUNANJE MATRICE ELEMENATA I/ILI NAPONA
C
      CALL ELTE7(AE(LBET),AE(LSKE),AE(LLM),AU(LNEL),AU(LNMAT),
     1AU(LTHID),AE(LHE),AE(LBED),A(LCORD),A(LRTDT),A(LFTDT),
     2A(LSIGMA),AU(LIPGC),AU(LLMEL),AE(LTHSL),AU(LBETA),NCVE3)
C
C     PAKOVANJE MATRICA ELEMENATA U MATRICU SISTEMA
C
      IF(ISKNP.EQ.2) GO TO 100
C
      CALL SPAKUJ(A(LSK),A(LMAXA),AE(LSKE),AE(LLM),NCVE3)
  100 CONTINUE
C
      IF(NMODM.LE.4) GO TO 110
C     NPROS=NE*NGAUSX*NGAUSY*16*IDVA
C     LMA8=LSTAZA(4)-1
C     CALL WRITDD(A(LPLAS1),NPROS/IDVA,IELEM,LMA8,LDUZI)
C     RETURN
C
  110 NPROS=NE*MSLOJ*NGAUSX*NGAUSY*6*IDVA
      LMA8=LSTAZA(5)-1
      CALL WRITDD(A(LSIGMA),NPROS/IDVA,IELEM,LMA8,LDUZI)
      RETURN
      END
C=======================================================================
      SUBROUTINE ELTE7(BET,SKE,LM,NEL,NMAT,THID,HE,BED,CORD,
     1                 RTDT,FTDT,SIGMA,IPGC,LMEL,THSLOJ,BETA,NCVE3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     INTEGRACIJA PO POVRSINI MATRICA ELEMENATA
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /GRUPEE/ NGEL,NGENL,LGEOM,NGEOM,ITERM
      COMMON /ITERAC/ METOD,MAXIT,TOLE,TOLS,TOLM,KONVE,KONVS,KONVM
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /REPERM/ MREPER(4)
      COMMON /TEMPCV/ LTECV,ITEMP
      COMMON /PLASTI/ LPLAST,LPLAS1,LSIGMA
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /LUSKA7/ GX,GY,XJ0(2,2),DI,YM,YDE,DE2,V1X(4),V1Y(4),VX,VY,
     1                Q(3,4)
C
      DIMENSION BET(NCVE3,*),SKE(*),LM(*),NEL(NE,*),
     1CORD(NP,*),HE(NCVE,*),BED(NCVE3,*),IPGC(*),
     1RTDT(*),FTDT(*),SIGMA(NE,MSLOJ,NGAUSX,NGAUSY,*),LMEL(NCVE3,*),
     1NMAT(NE,*),THID(NE,MSLOJ,*),THSLOJ(*),BETA(NE,*)
C
      DIMENSION STRAIN(4),STRESS(4)
C
      DIMENSION XG(55),WGT(55),NREF(11),XGG(15)
C
      DATA NREF/0,1,3,6,10,15,21,28,36,45,55/
      DATA WGT/            2.D0,               1.D0,               1.D0,
     1       .555555555555556D0, .888888888888889D0, .555555555555556D0,
     2       .347854845137454D0, .652145154862546D0, .652145154862546D0,
     3       .347854845137454D0, .236926885056189D0, .478628670499366D0,
     4       .568888888888889D0, .478628670499366D0, .236926885056189D0,
     5       .171324492379170D0, .360761573048139D0, .467913934572691D0,
     6       .467913934572691D0, .360761573048139D0, .171324492379170D0,
     7       .129484966168870D0, .279705391489277D0, .381830050505119D0,
     8       .417959183673469D0, .381830050505119D0, .279705391489277D0,
     9       .129484966168870D0, .101228536290376D0, .222381034453374D0,
     9       .313706645877887D0, .362683783378362D0, .362683783378362D0,
     1       .313706645877887D0, .222381034453374D0, .101228536290376D0,
     2       .081274388361574D0, .180648160694857D0, .260610696402935D0,
     3       .312347077040003D0, .330239355001260D0, .312347077040003D0,
     4       .260610696402935D0, .180648160694857D0, .081274388361574D0,
     5       .066671344308688D0, .149451349150581D0, .219086362515982D0,
     6       .269266719309996D0, .295524224714753D0, .295524224714753D0,
     7       .269266719309996D0, .219086362515982D0, .149451349150581D0,
     8       .066671344308688D0/
      DATA XG /            0.D0,-.577350269189626D0, .577350269189626D0,
     1      -.774596669241483D0,               0.D0, .774596669241483D0,
     2      -.861136311594053D0,-.339981043584856D0, .339981043584856D0,
     3       .861136311594053D0,-.906179845938664D0,-.538469310105683D0,
     4                     0.D0, .538469310105683D0, .906179845938664D0,
     5      -.932469514203152D0,-.661209386466265D0,-.238619186083197D0,
     6       .238619186083197D0, .661209386466265D0, .932469514203152D0,
     7      -.949107912342759D0,-.741531185599394D0,-.405845151377397D0,
     8                     0.D0, .405845151377397D0, .741531185599394D0,
     9       .949107912342759D0,-.960289856497536D0,-.796666477413627D0,
     9      -.525532409916329D0,-.183434642495650D0, .183434642495650D0,
     1       .525532409916329D0, .796666477413627D0, .960289856497536D0,
     2      -.968160239507626D0,-.836031107326636D0,-.613371432700590D0,
     3      -.324253423403809D0,               0.D0, .324253423403809D0,
     4       .613371432700590D0, .836031107326636D0, .968160239507626D0,
     5      -.973906528517172D0,-.865063366688985D0,-.679409568299024D0,
     6      -.433395394129247D0,-.148874338981631D0, .148874338981631D0,
     7       .433395394129247D0, .679409568299024D0, .865063366688985D0,
     8       .973906528517172D0/
C
      DATA XGG/            0.D0,              -1.D0,               1.D0,
     1                    -1.D0,               0.D0,               1.D0,
     2                    -1.D0,-.333333333333333D0, .333333333333333D0,
     3                     1.D0,              -1.D0,              -.5D0,
     4                     0.D0,               .5D0,               1.D0/
C
C     FORMIRANJE VEKTORA LM
C
      DO 10 NC=1,NCVE3
      LM(NC)=LMEL(NC,NLM)
   10 CONTINUE
C
C     PETLJA PO GAUSOVIM TACKAMA
C
      IBTC=1
      IRAC=2
      IPGCT=IPGC(NLM)
C ...  JEDINICNI VEKTORI U CVORNIM TACKAMA
      R=-1.D0
      CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,1)
      R=1.D0
      CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,2)
      IF(NCVE.EQ.2) GO TO 12
      IF(NEL(NLM,3).EQ.0) GO TO 11
      IF(NCVE.EQ.3) THEN
          R=0.D0
          CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,3)
          GO TO 12
            ELSE
          R=-1.D0/3.D0
          CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,3)
            ENDIF
   11 IF(NCVE.EQ.3) GO TO 12
      IF(NEL(NLM,4).EQ.0) GO TO 12
      R=1.D0/3.D0
      CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,4)
   12 CONTINUE
C     WRITE(3,*)'JEDVEKT.',(V1X(I),I=1,4),(V1Y(J),J=1,4)
C
      DO 500 NGR=1,NGAUSX
      JGR=NREF(NGAUSX)+NGR
      R=XG(JGR)
      IF(IPGCT.EQ.1.AND.ISKNP.EQ.2) R=XGG(JGR)
      WR=WGT(JGR)
C ...  VREDNOSTI KOJE NE ZAVISE OD SLOJA
      CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,KSLOJ,0)
C     WRITE(3,*)'Q  ',((Q(I,J),J=1,4),I=1,3)
C
C ...    PETLJA PO SLOJEVIMA
C
      DO 490 MS=1,MSLOJ
      MAT=NMAT(NLM,MS)
C
C     FORMIRANJE MATRICE ELASTICNIH KONSTANTI (ELAST)
C
      GO TO (1,2,40,40,40,40,40,40),NMODM
    1 LFUN=MREPER(1)
      CALL MEL017(A(LFUN))
      CALL TRAEL7(ELAST,Q,3,4,ELAST)
C     WRITE(3,*)'        ELAST',((ELAST(I,J),J=1,4),I=1,4)
      GO TO 40
    2 LFUN=MREPER(1)
      CALL MEL027(A(LFUN),BETA(NLM,MS))
      CALL TRAEL7(ELAST,Q,3,4,ELAST)
      GO TO 40
C
   40 DO 50 NGS=1,NGAUSY
      JGS=NREF(NGAUSY)+NGS
      S=XG(JGS)
      IF(IPGCT.EQ.1.AND.ISKNP.EQ.2) S=XGG(JGS)
      WS=WGT(JGS)
      WRWS=WR*WS
C
C     FORMIRANJE MATRICE ELASTICNIH KONSTANTI ZA NMODM=3,4,5
C
      GO TO (70,70,3,4,5,5,5,5),NMODM
    3 LFUN=MREPER(1)
      LNTA=MREPER(2)
      LTEM=MREPER(3)
      MATE=MREPER(4)
      CALL MEL037(A(LFUN),A(LNTA),A(LTEM),MATE,TGT)
      CALL TRAEL7(ELAST,Q,3,4,ELAST)
      CALL TRAAL(ALFA,Q,3,4,ALFA)
      GO TO 70
    4 LFUN=MREPER(1)
      LNTA=MREPER(2)
      LTEM=MREPER(3)
      MATE=MREPER(4)
      CALL MEL047(A(LFUN),A(LNTA),A(LTEM),MATE,TGT,BETA(NLM,MS))
      CALL TRAEL7(ELAST,Q,3,4,ELAST)
      CALL TRAAL(ALFA,Q,3,4,ALFA)
      GO TO 70
    5 IBTC=IBTC+1
C     LPLAS=LPLAST+(NLM-1)*(NGAUSX*NGAUSY)*16*IDVA+(IBTC-1)*16*IDVA
C     LPLA1=LPLAS1+(NLM-1)*(NGAUSX*NGAUSY)*16*IDVA+(IBTC-1)*16*IDVA
C     CALL MODMA7(STRAIN,STRESS,NMODM,IRAC,LPLAS,LPLA1)
C     GO TO 70
C
C     JAKUBIJAN , DEBLJINA SLOJA , MATRICA ELAST I TEMPERATURA U TACKI
C
  70  CALL JACTE7(NEL,CORD,HE,R,S,THID,THSLOJ,MS,5)
      CALL JACGA7(NEL,CORD,A(LTECV),HE,X1,X2,TGT)
      WD=WRWS*DET
      CONAX=X1*WD
C     MATRICA BET
      CALL BETBE7(HE,BET,NEL,X1,NCVE3)
C
C     RACUNANJE UKUPNIH DEFORMACIJA  E=B*U
C
      IF(ISKNP.EQ.1) GO TO 400
      DO 100 I=1,4
      STRAIN(I)=0.D0
      DO 110 K=1,NCVE3
      IF(LM(K).EQ.0) GO TO 110
      LL=LM(K)
      STRAIN(I)=STRAIN(I)+BET(K,I)*RTDT(LL)
  110 CONTINUE
C
C     RACUNANJE TERMICKIH DEFORMACIJA - ETH; ETH=ALFA*(T-T0)
C
      KI=I
      IF(KI.EQ.3)GO TO 100
      IF(KI.EQ.4)KI=3
      IF(ITERME.EQ.0) GO TO 100
      DTGT0=TGT-TEMP0
      ALPOM=ALFA(KI)
      STRAIN(I)=STRAIN(I)-ALPOM*DTGT0
  100 CONTINUE
C     WRITE(3,*)'STRAIN  ',(STRAIN(I),I=1,4)
C
C     RACUNANJE NAPONA TAU=C*(E-ETH)
C
      IF(NMODM.GE.5) GO TO 250
      DO 200 I=1,4
      STRESS(I)=0.D0
C     NAPON OD UKUPNIH DEFORMACIJA TAU=C*E
      DO 210 L=1,4
  210 STRESS(I)=STRESS(I)+ELAST(I,L)*STRAIN(L)
  200 CONTINUE
C.... TRANSFORMACIJA NAPONA U LOKALNE KOORDINATE
      DO 230 I=1,3
      DO 230 J=1,4
  230 SIGMA(NLM,MS,NGR,NGS,I)=SIGMA(NLM,MS,NGR,NGS,I)+Q(I,J)*STRESS(J)
C     SIGMA(NLM,MS,NGR,NGS,I)=STRESS(I)
      SIGMA(NLM,MS,NGR,NGS,4)=TGT
      SIGMA(NLM,MS,NGR,NGS,5)=X1
      SIGMA(NLM,MS,NGR,NGS,6)=X2
      GO TO 300
C
C   RACUNANJE NAPONA ZA PLASTICNOST
C
  250 IRAC=1
C     CALL MODMA7(STRAIN,STRESS,NMODM,IRAC,LPLAS,LPLA1)
C
C   RACUNANJE UNUTRASNJIH SILA FTDT=BET*TAU
C
  300 IF(NGENL.EQ.0) GO TO 400
      DO 310 II=1,NCVE3
      IF(LM(II).EQ.0) GO TO 310
      LL=LM(II)
      DO 320 K=1,4
      BETT=BET(II,K)*STRESS(K)*CONAX
      FTDT(LL)=FTDT(LL)+BETT
  320 CONTINUE
  310 CONTINUE
C
C     INTEGRACIJA SKE
C
  400 IF(ISKNP.EQ.2) GO TO 50
C     PROIZVOD BET*ELAST
      DO 410 I=1,NCVE3
      IC=(I+2)/3
      IF(NEL(NLM,IC).EQ.0) GO TO 410
      DO 420 J=1,4
      BED(I,J)=0.0
      DO 420 K=1,4
      BED(I,J)=BED(I,J)+BET(I,K)*ELAST(K,J)
  420 CONTINUE
  410 CONTINUE
C     INTEGRACIJA SKE
      IJ=0
      DO 430 I=1,NCVE3
      DO 430 J=I,NCVE3
      IJ=IJ+1
      IF(LM(I).EQ.0.OR.LM(J).EQ.0) GO TO 430
      XX=0.0
      DO 440 K=1,4
  440 XX=XX+BED(I,K)*BET(J,K)
      SKE(IJ)=SKE(IJ)+XX*CONAX
  430 CONTINUE
   50 CONTINUE
  490 CONTINUE
  500 CONTINUE
C     WRITE(3,*)'SKE',(SKE(I),I=1,NCVE3*(NCVE3+1)/2)
      RETURN
      END
C======================================================================
      SUBROUTINE BETBE7(H,BET,NEL,X1,NCVE3)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     MATRICE BET (BE TRANSPONOVANO)
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /LUSKA7/ GX,GY,XJ0(2,2),DI,YM,YDE,DE2,V1X(4),V1Y(4),VX,VY,
     1                Q(3,4)
C
      DIMENSION H(NCVE,*),BET(NCVE3,*),NEL(NE,*)
C
      JJ=-2
      DO 40 I=1,NCVE
      JJ=JJ+3
      IF(NEL(NLM,I).EQ.0) GO TO 40
      BET(JJ,1)=XJ(1,1)*H(I,2)
      BET(JJ,2)=0.D0
      BET(JJ,3)=XJ(2,1)*H(I,2)
      BET(JJ+1,1)=0.D0
      BET(JJ+1,2)=BET(JJ,3)
      BET(JJ+1,3)=BET(JJ,1)
      BET(JJ+1,4)=0.D0
C
      BET(JJ+2,1)=(-YDE*BET(JJ,1)-DE2*XJ(1,2)*H(I,1))*V1X(I)
      BET(JJ+2,2)=(-YDE*BET(JJ,3)-DE2*XJ(2,2)*H(I,1))*V1Y(I)
      BET(JJ+2,3)=-YDE*(BET(JJ,3)*V1X(I)+BET(JJ,1)*V1Y(I))-
     1             DE2*H(I,1)*(XJ(2,2)*V1X(I)+XJ(1,2)*V1Y(I))
      IF(X1.GT.0.00000001) GO TO 30
      BET(JJ,4)=BET(JJ,1)
      BET(JJ+2,4)=0.D0
      GO TO 40
   30 BET(JJ,4)=H(I,1)/X1
      BET(JJ+2,4)=-BET(JJ,4)*YDE*V1X(I)
   40 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE JACTE7(NEL,CORD,H,R,S,THID,THSLOJ,KSLOJ,KCALL)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     JAKOBIJAN I INTERPOLACIJA
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /IZOL4B/ NGS12,ND,MSLOJ,MXS,MSET,LNSLOJ,LMATSL,LDSLOJ,LBBET
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /LUSKA7/ GX,GY,XJ0(2,2),DI,YM,YDE,DE2,V1X(4),V1Y(4),VX,VY,
     1                Q(3,4)
C
      DIMENSION NEL(NE,*),CORD(NP,*),H(NCVE,*),THID(NE,MSLOJ,*),
     1          THSLOJ(*)
C
      IF(KCALL.EQ.5) GO TO 342
C
        RP=1.+R
        RM=1.-R
        RK=1.-R*R
C
      H(1,1)=0.5*RM
        H(2,1)=0.5*RP
        H(1,2)=-0.5
        H(2,2)=0.5
      IF(NCVE.EQ.2) GO TO 233
C        TRECI CVOR -----------------------------------------------------
        IF(NEL(NLM,3).EQ.0)GO TO 230
        H(3,1)=RK
        H(3,2)=-2.*R
        DO 210 I=1,2
      DO 210 J=1,2
  210        H(I,J)=H(I,J)-0.5*H(3,J)
      IF(NCVE.EQ.3) GO TO 233
C        CETVRTI CVOR ---------------------------------------------------
  230 IF(NEL(NLM,4).EQ.0)GO TO 233
        R2=R*R
        R3=R2*R
        H(1,1)=H(1,1)+(-9.*R3+R2+9.*R-1.)/16.
        H(2,1)=H(2,1)+(9.*R3+R2-9.*R-1.)/16.
        H(3,1)=H(3,1)+(27.*R3+7.*R2-27.*R-7.)/16.
        H(4,1)=(-27.*R3-9.*R2+27.*R+9.)/16.
        H(1,2)=H(1,2)+(-27.*R2+2.*R+9.)/16.
        H(2,2)=H(2,2)+(27.*R2+2.*R-9.)/16.
           H(3,2)=H(3,2)+(81.*R2+14.*R-27.)/16.
        H(4,2)=(-81.*R2-18.*R+27.)/16.
C
C     JEDINICNI VEKTORI NA SREDNJOJ LINIJI
C
  233 GX=0.
      GY=0.
      DO 245 J=1,NCVE
        IF(NEL(NLM,J).EQ.0)GO TO 245
        IC=NEL(NLM,J)
        GX=GX+H(J,2)*CORD(IC,1)
        GY=GY+H(J,2)*CORD(IC,2)
  245 CONTINUE
      DG=DSQRT(GX*GX+GY*GY)
      VX=GX/DG
      VY=GY/DG
      IF(KCALL.EQ.0) GO TO 249
        V1X(KCALL)=VX
        V1Y(KCALL)=VY
        RETURN
C
C        ZAJEDNICKE VELICINE ZA SVE SLOJEVE U TACKI   R
C....   DEO JAKOBIJANA
  249 DO 250 K=1,2
        DO 250 KK=1,2
  250        XJ0(K,KK)=0.0
        DO 270 J=1,NCVE
        IF(NEL(NLM,J).EQ.0)GO TO 270
        XJ0(1,1)=XJ0(1,1)+H(J,2)*V1Y(J)
        XJ0(1,2)=XJ0(1,2)+H(J,2)*V1X(J)
        XJ0(2,1)=XJ0(2,1)+H(J,1)*V1Y(J)
        XJ0(2,2)=XJ0(2,2)+H(J,1)*V1X(J)
  270 CONTINUE
C ...  MATRICA TRANSFORMACIJE ( ZA ELAST() )
      VX2=VX*VX
      VY2=VY*VY
      VXVY=VX*VY
      Q(1,1)=VX2
      Q(1,2)=VY2
      Q(1,3)=VXVY
      Q(1,4)=0.D0
      Q(2,1)=0.D0
      Q(2,2)=0.D0
      Q(2,3)=0.D0
      Q(2,4)=1.D0
      Q(3,1)=-2.*VXVY
      Q(3,2)=-Q(3,1)
      Q(3,3)=VX2-VY2
      Q(3,4)=0.D0
C ... DEBLJINE POJEDINIH SLOJEVA I UKUPNA DEBLJINA ELEMENTA U  G. TACKI
C ...       THSLOJ()                          DI
      DO 360 MS=1,MSLOJ
  360 THSLOJ(MS)=0.
             DO 370 J=1,NCVE
        IF(NEL(NLM,J).EQ.0)GO TO 370
        IC=NEL(NLM,J)
        DO 365 MS=1,MSLOJ
  365          THSLOJ(MS)=THSLOJ(MS)+H(J,1)*THID(NLM,MS,J)
  370 CONTINUE
        DI=0.
      DO 340 MS=1,MSLOJ
  340 DI=DI+THSLOJ(MS)
      RETURN
C------------------------ VELICINE ZAVISNE OD SLOJA  -----------------
C
C...  RASTOJANJE YM OD SREDNJE LINIJE SLOJA;  PROMENLJIVE DE2 , YDE
  342        YM=0.
      IF(KSLOJ.GT.1) THEN
                     DO 345 MS=1,KSLOJ-1
  345                YM=YM+THSLOJ(MS)
                     ENDIF
      YM=YM-(DI-THSLOJ(KSLOJ))/2.
C
      DE2=THSLOJ(KSLOJ)/2.
      YDE=YM+DE2*S
C....   JAKOBIJAN
      XJ(1,1)=GX-YDE*XJ0(1,1)
      XJ(1,2)=GY+YDE*XJ0(1,2)
      XJ(2,1)=-THSLOJ(KSLOJ)*XJ0(2,1)/2.
      XJ(2,2)= THSLOJ(KSLOJ)*XJ0(2,2)/2.
C....        INVERZNI JAKOBIJAN
      DET=XJ(1,1)*XJ(2,2)-XJ(1,2)*XJ(2,1)
      IF(DET.GT.0.D0) GO TO 70
      WRITE(IZLAZ,2000) NLM,DET
      STOP
C
C...   INVERZNI JAKOBIJAN
   70 XJJ=XJ(1,1)
      XJ(1,1)=XJ(2,2)/DET
      XJ(2,2)=XJJ/DET
      XJ(1,2)=-XJ(1,2)/DET
      XJ(2,1)=-XJ(2,1)/DET
      RETURN
C-----------------------------------------------------------------------
 2000 FORMAT(/' ','ZERO OR NEGATIVE JACOBIAN DETERMINANTE'/
     1' ','ELEMENT NU. =',I10/' ','DETERMINANT =',D12.5)
CS 2000 FORMAT(/' ','DETERMINANTA JAKOBIJANA MANJA OD NULE'/
CS     1' ','ELEMENT BR. =',I10/' ','DETERMINANTA =',D12.5)
C-----------------------------------------------------------------------
      END
C=====================================================================
      SUBROUTINE TRAAL(ALFA,Q,II,JJ,ALF1)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C....  TRANSFORMACIJA MATRICE ELAST()
      DIMENSION ALFA(*),Q(II,*),ALF1(*),P(6)
      DO 80 I=1,JJ
        P(I)=0.D0
          DO 75 K=1,II
   75     P(I)=P(I)+Q(K,I)*ALFA(K)
   80 CONTINUE
      DO 84 I=1,JJ
      KI=I
      IF(KI.EQ.4)KI=3
   84 ALF1(KI)=P(I)
      RETURN
      END
C=====================================================================
      SUBROUTINE TRAEL7(ELAST,Q,II,JJ,ELA1)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C....  TRANSFORMACIJA MATRICE ELAST()
      DIMENSION ELAST(6,*),Q(II,*),ELA1(6,*),P(6,6)
      DO 80 I=1,JJ
        DO 80 J=1,II
        P(I,J)=0.D0
          DO 75 K=1,II
   75     P(I,J)=P(I,J)+Q(K,I)*ELAST(K,J)
   80 CONTINUE
      DO 84 I=1,JJ
        DO 84 J=I,JJ
        ELA1(I,J)=0.D0
          DO 82 K=1,II
   82     ELA1(I,J)=ELA1(I,J)+P(I,K)*Q(K,J)
        ELA1(J,I)=ELA1(I,J)
   84 CONTINUE
      RETURN
      END
C======================================================================
      SUBROUTINE JACGA7(NEL,CORD,TECV,H,X1,X2,TGT)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     JAKOBIJAN I INTERPOLACIJA
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /LUSKA7/ GX,GY,XJ0(2,2),DI,YM,YDE,DE2,V1X(4),V1Y(4),VX,VY,
     1                Q(3,4)
C
      DIMENSION NEL(NE,*),CORD(NP,*),TECV(*),H(NCVE,*)
C.... KOORDINATE I TEMPERATURE TACKE
      X1=0.0
      X2=0.0
      TGT=0.0
      DO 80 I=1,NCVE
      IF(NEL(NLM,I).EQ.0) GO TO 80
      IC=NEL(NLM,I)
      X1=X1+H(I,1)*CORD(IC,1)
      X2=X2+H(I,1)*CORD(IC,2)
      IF(ITERME.EQ.0) GO TO 80
      TGT=TGT+H(I,1)*TECV(IC)
   80 CONTINUE
      X1=X1-YDE*VY
      X2=X2+YDE*VX
      RETURN
      END
C======================================================================
      SUBROUTINE MEL017(FUN)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
C
      DIMENSION FUN(2,*)
      DATA AK/1.2D0/
C
      E=FUN(1,MAT)
      V=FUN(2,MAT)
C     NULOVANJE ELAST
      DO 15 I=1,3
      DO 15 J=I,3
   15 ELAST(I,J)=0.D0
C     RAVANSKO STANJE NAPONA
      ELAST(1,1)=E/(1.D0-V*V)
      ELAST(2,2)=ELAST(1,1)
      ELAST(1,2)=ELAST(1,1)*V
      ELAST(3,3)=ELAST(1,1)*(1.D0-V)/2.D0/AK
      DO 50 I=1,3
      DO 50 J=I,3
   50 ELAST(J,I)=ELAST(I,J)
      RETURN
      END
C======================================================================
      SUBROUTINE MEL027(FUN,BETA)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
C
      DIMENSION FUN(9,*)
      DATA AK/1.2D0/
C
      EX=FUN(1,MAT)
      EY=FUN(2,MAT)
      EZ=FUN(3,MAT)
      VXY=FUN(4,MAT)
      VYZ=FUN(5,MAT)
      VZX=FUN(6,MAT)
      GXY=FUN(7,MAT)
      GYZ=FUN(8,MAT)
      GZX=FUN(9,MAT)
C.... PROVERA DA LI SE SLOJ SASTOJI IZ UNAKRSNIH POLUSLOJEVA
      IF(DABS(BETA).LT.1.D-20) THEN
                    EX0=EX
                    EZ0=EZ
                    VZX0=VZX
                    GXY0=GXY
        ELSE
        CB=DCOS(BETA)
        SB=DSIN(BETA)
        CB2=CB*CB
        CB4=CB2*CB2
        SB2=SB*SB
        SB4=SB2*SB2
        SB22=2*SB*CB
        CB22=CB2-SB2
        AM1=-(SB22*(CB2-SB2*EX/EZ)-CB22*SB*CB*(EX/GZX-2.*VZX))
        AM2=-(SB22*(SB2-CB2*EX/EZ)+CB22*SB*CB*(EX/GZX-2.*VZX))
C.... MODULI ZA POLUSLOJ ZAOKRENUT ZA UGAO  BETA
        EX1=1./(CB4/EX+(1./GZX-2.*VZX/EX)*CB2*SB2+SB4/EZ)
        EZ1=1./(SB4/EX+(1./GZX-2.*VZX/EX)*CB2*SB2+CB4/EZ)
        GZX1=1./(1./GZX+(1./EX+1./EZ+1./GZX+2*VZX/EX)*SB22*SB22)
C.... MODULI ZA SLOJ SASTAVLJEN IZ DVA UNAKRSNA POLUSLOJA  +- BETA
        EX0=EX/(EX/EX1-AM1*AM1*GZX/EX)
        EZ0=EX/(EX/EZ1-AM2*AM2*GZX/EX)
        VZX0=EZ0/EX*(VZX*EZ1/EX+AM1*AM2*GZX/EX)
        GXY0=GXY*GYZ/(GXY*SB2+GYZ*CB2)
C       WRITE(3,*)'EX0,EZ0,VZX0',EX0,EZ0,VZX0
                        ENDIF
C     NULOVANJE ELAST
      DO 15 I=1,3
      DO 15 J=I,3
   15 ELAST(I,J)=0.0
C     RAVANSKO STANJE NAPONA
      POM=EX0-EZ0*VZX0*VZX0
      ELAST(1,1)=EX0*EX0/POM
      ELAST(2,2)=EX0*EZ0/POM
      ELAST(1,2)=EX0*EZ0*VZX0/POM
      ELAST(3,3)=GXY0/AK
      DO 50 I=1,3
      DO 50 J=I,3
   50 ELAST(J,I)=ELAST(I,J)
      RETURN
      END
C======================================================================
      SUBROUTINE MEL037(FUN,NTA,TEM,MATE,TGT)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
C
      DIMENSION FUN(2,MATE*3,*),NTA(*),TEM(*)
      DATA AK/1.2D0/
C
C     UCITAVANJE FUNKCIJE E,V,ALFA
C
      DO 6 J=1,3
      NFE=(MAT-1)*3+J
      CALL TABF(FUN,NTA,NFE,MATE*3,TGT,EVA,NTMX,IND)
      IF(IND.GT.0) GO TO 120
      IF(J.EQ.1) E=EVA
      IF(J.EQ.2) V=EVA
    6 CONTINUE
      ALFA(1)=EVA
      ALFA(2)=EVA
      ALFA(3)=0.D0
      TEMP0=TEM(MAT)
C     WRITE(3,*)'E,V',E,V
C     NULOVANJE ELAST
      DO 15 I=1,3
      DO 15 J=I,3
   15 ELAST(I,J)=0.0
C     RAVANSKO STANJE NAPONA
      ELAST(1,1)=E/(1.-V*V)
      ELAST(2,2)=ELAST(1,1)
      ELAST(1,2)=ELAST(1,1)*V
      ELAST(3,3)=ELAST(1,1)*(1.-V)/2./AK
      DO 50 I=1,3
      DO 50 J=I,3
   50 ELAST(J,I)=ELAST(I,J)
      RETURN
  120 WRITE(IZLAZ,2000) NFE,TGT
      STOP
C-----------------------------------------------------------------------
 2000 FORMAT(///' ARGUMENT IS OUT OF RANGE'/
     1' TEMPERATURE FUNCTION  =',I5/
     2' ARGUMENT TEMPERATURE  =',D12.4)
CS 2000 FORMAT(///' ARGUMENT VAN OPSEGA ZADATE KRIVE U MEL03'/
CS     1' TEMPERATURSKA FUNKCIJA BROJ =',I5/
CS     2' ARGUMENT TEMPERATURA =',D12.4)
C-----------------------------------------------------------------------
      END
C======================================================================
      SUBROUTINE MEL047(FUN,NTA,TEM,MATE,TGT,BETA)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE MATRICE ELAST
C
      COMMON /ELEIND/ NGAUSX,NGAUSY,NGAUSZ,NCVE,ITERME,MAT,IETYP
      COMMON /ELEMEN/ ELAST(6,6),XJ(3,3),ALFA(6),TEMP0,DET,NLM,KK
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
C
      DIMENSION FUN(2,MATE*12,*),NTA(*),TEM(*)
      DATA AK/1.2D0/
C
C     UCITAVANJE FUNKCIJE E,V,ALFA
C
      DO 6 J=1,12
      NFE=(MAT-1)*12+J
      CALL TABF(FUN,NTA,NFE,MATE*12,TGT,EVA,NTMX,IND)
      IF(IND.GT.0) GO TO 120
      IF(J.EQ.1) EX=EVA
      IF(J.EQ.2) EY=EVA
      IF(J.EQ.3) EZ=EVA
      IF(J.EQ.4) VXY=EVA
      IF(J.EQ.5) VYZ=EVA
      IF(J.EQ.6) VZX=EVA
      IF(J.EQ.7) GXY=EVA
      IF(J.EQ.8) GYZ=EVA
      IF(J.EQ.9) GZX=EVA
      IF(J.EQ.10) ALFAX=EVA
C     IF(J.EQ.11) ALFAY=EVA
      IF(J.EQ.12) ALFAZ=EVA
    6 CONTINUE
      ALFA(3)=0.D0
      TEMP0=TEM(MAT)
C.... PROVERA DA LI SE SLOJ SASTOJI IZ UNAKRSNIH POLUSLOJEVA
      IF(DABS(BETA).LT.1.D-20) THEN
                    EX0=EX
                    EZ0=EZ
                    VZX0=VZX
                    GXY0=GXY
                    ALFA(1)=ALFAX
                    ALFA(2)=ALFAZ
        ELSE
        CB=DCOS(BETA)
        SB=DSIN(BETA)
        CB2=CB*CB
        CB4=CB2*CB2
        SB2=SB*SB
        SB4=SB2*SB2
        SB22=2*SB*CB
        CB22=CB2-SB2
        AM1=-(SB22*(CB2-SB2*EX/EZ)-CB22*SB*CB*(EX/GZX-2.*VZX))
        AM2=-(SB22*(SB2-CB2*EX/EZ)+CB22*SB*CB*(EX/GZX-2.*VZX))
C.... MODULI ZA POLUSLOJ ZAOKRENUT ZA UGAO  BETA
        EX1=1./(CB4/EX+(1./GZX-2.*VZX/EX)*CB2*SB2+SB4/EZ)
        EZ1=1./(SB4/EX+(1./GZX-2.*VZX/EX)*CB2*SB2+CB4/EZ)
        GZX1=1./(1./GZX+(1./EX+1./EZ+1./GZX+2*VZX/EX)*SB22*SB22)
C.... MODULI ZA SLOJ SASTAVLJEN IZ DVA UNAKRSNA POLUSLOJA  +- BETA
        EX0=EX/(EX/EX1-AM1*AM1*GZX/EX)
        EZ0=EX/(EX/EZ1-AM2*AM2*GZX/EX)
        VZX0=EZ0/EX*(VZX*EZ1/EX+AM1*AM2*GZX/EX)
        GXY0=GXY*GYZ/(GXY*SB2+GYZ*CB2)
        ALFA(1)=CB2*ALFAX+SB2*ALFAZ
        ALFA(2)=SB2*ALFAX+CB2*ALFAZ
C       WRITE(3,*)'EX0,EZ0,VZX0',EX0,EZ0,VZX0
                        ENDIF
C     NULOVANJE ELAST
      DO 15 I=1,3
      DO 15 J=I,3
   15 ELAST(I,J)=0.0
C     RAVANSKO STANJE NAPONA
      POM=EX0-EZ0*VZX0*VZX0
      ELAST(1,1)=EX0*EX0/POM
      ELAST(2,2)=EX0*EZ0/POM
      ELAST(1,2)=EX0*EZ0*VZX0/POM
      ELAST(3,3)=GXY0/AK
      DO 50 I=1,3
      DO 50 J=I,3
   50 ELAST(J,I)=ELAST(I,J)
      RETURN
  120 WRITE(IZLAZ,2000) NFE,TGT
      STOP
C-----------------------------------------------------------------------
 2000 FORMAT(///' ARGUMENT IS OUT OF RANGE'/
     1' TEMPERATURE FUNCTION  =',I5/
     2' ARGUMENT TEMPERATURE  =',D12.4)
CS 2000 FORMAT(///' ARGUMENT VAN OPSEGA ZADATE KRIVE U MEL047'/
CS     1' TEMPERATURSKA FUNKCIJA BROJ =',I5/
CS     2' ARGUMENT TEMPERATURA =',D12.4)
C-----------------------------------------------------------------------
      END
C=======================================================================
      SUBROUTINE FORMP7
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     FORMIRANJE VEKTORA PRITISKA  OSNOSIM. LJUSKE U TRENUTKU T+DT
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /DUPLAP/ IDVA
      COMMON /DUZINA/ LMAX,MTOT,LMAXM,LRAD,NRAD
      COMMON /REPERI/ LCORD,LID,LMAXA,LMHT
      COMMON /SISTEM/ LSK,LRTDT,NWK,JEDN,LFTDT
      COMMON /DINAMI/ IMASS,IDAMP,PIP,DIP,MDVI
      COMMON /PERKOR/ LNKDT,LDTDT,LVDT,NDT,DT,VREME,KOR
      COMMON /OPTERE/ NCF,NPP2,NPP3,NPGR,NPGRI,NPLJ,NTEMP
      COMMON /ITERBR/ ITER
      COMMON /SOPSVR/ ISOPS,ISTYP,NSOPV,ISTSV,IPROV,IPROL
      COMMON /ELEALL/ NETIP,NE,IATYP,NMODM,NGE,ISKNP,LMAX8
      COMMON /UPDLAG/ LUL,LCORUL
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
      COMMON /OPSTIP/ JPS,JPBR,NPG,JIDG,JCORG,JCVEL,JELCV,NGA,NGI,NPK,
     1                NPUP,LIPODS,IPODS,LMAX13,MAX13,JEDNG,JMAXA,JEDNP,
     1                NWP,NWG,IDF,JPS1
C
      LFAKP = LRAD
      LNFUN = LFAKP + NPLJ*2*IDVA
      LIPRAV = LNFUN + NPLJ
      LNODPR = LIPRAV + NPLJ
      LMAX = LNODPR + NPLJ*4
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      CALL READDD(A(LFAKP),NPLJ*2,IPODS,LMAX13,LDUZI)
      CALL IREADD(A(LNFUN),NPLJ*6,IPODS,LMAX13,LDUZI)
C
      KORD=LCORD
      IF(IATYP.EQ.3) KORD=LCORUL
      CALL FTDTR7(A(LRTDT),A(LNFUN),A(LIPRAV),A(LFAKP),
     1A(LNODPR),A(KORD),A(LID),NPLJ,VREME)
      IF(ISOPS.GT.0.AND.ITER.EQ.0) GO TO 15
      IF(NDIN.EQ.0) RETURN
      IF(MDVI.NE.1) RETURN
   15 VREM = VREME-DT
      CALL DELJIV(LMAX,2,INDL)
      IF(INDL.EQ.0) LMAX=LMAX+1
      LPOMS=LMAX
      LMAX = LPOMS+JEDN*IDVA
      IF(LMAX.GT.MTOT) CALL ERROR(1)
      CALL RSTAZ(A(LIPODS),LPOMS,53)
      CALL FTDTR7(A(LPOMS),A(LNFUN),A(LIPRAV),A(LFAKP),
     1A(LNODPR),A(LCORD),A(LID),NPLJ,VREM)
      CALL WSTAZ(A(LIPODS),LPOMS,53)
      RETURN
      END
C=======================================================================
C
C=======================================================================
      SUBROUTINE FTDTR7(RTDT,NFUN,IPRAV,FAKP,NODPR,CORD,ID,NPP,VREME)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     INTEGRALJENJE PRITISAKA NA POVRSINI OSNOSIMETRICNE LJUSKE
C
      include 'paka.inc'
      
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /FVREME/ NTABFT,MAXTFT,LNTFT,LTABFT
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
C
      DIMENSION NFUN(*),IPRAV(*),FAKP(NPP,*),
     1NODPR(NPP,*),RTDT(*),CORD(NP,*),ID(NP,*),H(4,2)
C
      DIMENSION XG(15),WGT(15),NREF(6)
C
      DATA NREF/0,1,3,6,10,15/
C
      DATA WGT/            2.D0,               1.D0,               1.D0,
     1       .555555555555556D0, .888888888888889D0, .555555555555556D0,
     2       .347854845137454D0, .652145154862546D0, .652145154862546D0,
     3       .347854845137454D0, .236926885056189D0, .478628670499366D0,
     4       .568888888888889D0, .478628670499366D0, .236926885056189D0/
C
      DATA XG /            0.D0,-.577350269189626D0, .577350269189626D0,
     1      -.774596669241483D0,               0.D0, .774596669241483D0,
     2      -.861136311594053D0,-.339981043584856D0, .339981043584856D0,
     3       .861136311594053D0,-.906179845938664D0,-.538469310105683D0,
     4                     0.D0, .538469310105683D0, .906179845938664D0/
C
      DO 10 II=1,NPP
      NF = NFUN(II)
      IPR = IPRAV(II)
      P1 = FAKP(II,1)
      P2 = FAKP(II,2)
      CALL TABF(A(LTABFT),A(LNTFT),NF,NTABFT,VREME,FT,NTMX,IND)
      IF(IND.GT.0) GO TO 20
      ONE=1.D0
      CALL JACTP7(ONE,H,DET,CORD,NODPR,P1U,P1V,P2U,P2V,PU,PV,II,NPP,
     1CINA,CINB,0)
      P1U = P1*FT*CINA
      P1V =-P1*FT*CINB
      ONEM=-1.D0
      CALL JACTP7(ONEM,H,DET,CORD,NODPR,P1U,P1V,P2U,P2V,PU,PV,II,NPP,
     1CINA,CINB,0)
      P2U = P2*FT*CINA
      P2V =-P2*FT*CINB
C
      DO 100 NGX = 1,2
      JR = NREF(2)+NGX
      R = XG(JR)
      WT = WGT(JR)
      CALL JACTP7(R,H,DET,CORD,NODPR,P1U,P1V,P2U,P2V,PU,PV,II,NPP,
     1CINA,CINB,1)
      RAD = 0.
      DO 40 I=1,4
      NC = NODPR(II,I)
      IF(NC.EQ.0) GO TO  40
      RAD = RAD + H(I,1)*CORD(NC,1)
   40 CONTINUE
      WDT = WT*RAD*DET
C
      IF(IPR.EQ.2) GO TO 55
      DO 50 I=1,4
      NC = NODPR(II,I)
      IF(NC.EQ.0) GO TO  50
      NJ = ID(NC,1)
      IF(NJ.EQ.0) GO TO  50
      RTDT(NJ) = RTDT(NJ) + H(I,1)*PU*WDT
   50 CONTINUE
   55 IF(IPR.EQ.1) GO TO 100
      DO 60 I=1,4
      NC = NODPR(II,I)
      IF(NC.EQ.0) GO TO 60
      NJ = ID(NC,2)
      IF(NJ.EQ.0) GO TO  60
      RTDT(NJ) = RTDT(NJ) + H(I,1)*PV*WDT
   60 CONTINUE
  100 CONTINUE
   10 CONTINUE
      RETURN
C
   20 WRITE(IZLAZ,2000) NF,VREME
      STOP
C-----------------------------------------------------------------------
 2000 FORMAT(///' ARGUMENT IS OUT OF RANGE'/
     1' TIME FUNCTION  =',I5/
     2' ARGUMENT TIME  =',D12.4)
CS 2000 FORMAT(///' ARGUMENT VAN OPSEGA ZADATE KRIVE'/
CS     1' VREMENSKA FUNKCIJA BROJ =',I5/
CS     2' ARGUMENT VREME =',D12.4)
C-----------------------------------------------------------------------
      END
C======================================================================
      SUBROUTINE JACTP7(R,H,DET,CORD,NODPR,P1U,P1V,P2U,P2V,PU,PV,II,NPP,
     1CINA,CINB,IND)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C     POVRSINSKI JAKOBIJAN NA GRANICI OSNOSIMETRICNE LJUSKE
C
      COMMON /GLAVNI/ NP,NGELEM,NMATM,NPER,
     1                IOPGL(6),KOSI,NDIN,ITEST
      COMMON /TRAKEJ/ IULAZ,IZLAZ,IELEM,ISILE,IRTDT,IFTDT,ILISK,ILISE,
     1                ILIMC,ILDLT,IGRAF,IDINA,IPOME,IPRIT,LDUZI
C
      DIMENSION CORD(NP,*),NODPR(NPP,*),H(4,*),XJ(3)
C
        RP=1.+R
        RM=1.-R
        RK=1.-R*R
      DO 10 I=1,4
      H(I,1)=0.
   10 H(I,2)=0.
C
      H(1,1)=0.5*RM
        H(2,1)=0.5*RP
        H(1,2)=-0.5
        H(2,2)=0.5
      IF(IND.EQ.0) GO TO 45
      PU=H(1,1)*P1U+H(2,1)*P2U
      PV=H(1,1)*P1V+H(2,1)*P2V
C        TRECI CVOR -----------------------------------------------------
   45 IF(NODPR(II,3).EQ.0) GO TO 230
        H(3,1)=RK
        H(3,2)=-2.*R
        DO 210 I=1,2
      DO 210 J=1,2
  210        H(I,J)=H(I,J)-0.5*H(3,J)
C        CETVRTI CVOR ---------------------------------------------------
  230 IF(NODPR(II,4).EQ.0) GO TO 50
        R2=R*R
        R3=R2*R
        H(4,1)=(-27.*R3-9.*R2+27.*R+9.)/16.
        H(1,1)=H(1,1)+(-9.*R3+R2+9.*R-1.)/16.
        H(2,1)=H(2,1)+(9.*R3+R2-9.*R-1.)/16.
        H(3,1)=H(3,1)+(27.*R3+7.*R2-27.*R-7.)/16.
        H(4,2)=(-81.*R2-18.*R+27.)/16.
        H(1,2)=H(1,2)+(-27.*R2+2.*R+9.)/16.
        H(2,2)=H(2,2)+(27.*R2+2.*R-9.)/16.
           H(3,2)=H(3,2)+(81.*R2+14.*R-27.)/16.
C
   50 DO 60 J=1,2
      XJ(J)=0.
      DO 60 I=1,4
      NC = NODPR(II,I)
      IF(NC.EQ.0) GO TO 60
      XJ(J)=XJ(J)+H(I,2)*CORD(NC,J)
   60 CONTINUE
      DET = DSQRT(XJ(1)**2+XJ(2)**2)
      IF(IND.EQ.1) GO TO 65
      CINA=XJ(2)/DET
      CINB=XJ(1)/DET
   65 IF(DET.GT.0) RETURN
      STOP '***  DETERMINANTA MANJA OD NULE - PRITISCI TIP 7  ***'
      END
